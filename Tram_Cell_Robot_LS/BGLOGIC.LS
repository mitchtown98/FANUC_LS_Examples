/PROG  BGLOGIC
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "BACKGROUND";
PROG_SIZE	= 3441;
CREATE		= DATE 15-09-09  TIME 08:32:26;
MODIFIED	= DATE 18-04-03  TIME 22:35:12;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 191;
MEMORY_SIZE	= 3753;
PROTECT		= READ;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  !HOME POSITION LOGIC ;
   2:  IF (DO[23:ON :AT HOME] OR DO[24:ON :ROBOT PERCHED]) THEN ;
   3:  DO[33:ON :ROBOT CLEAR OF INBND]=ON ;
   4:  DO[70:ON :CLEAR OF AUDIT 1]=ON ;
   5:  DO[71:ON :CLEAR OF AUDIT 2]=ON ;
   6:  DO[105:ON :ROBOT CLEAR OF CMM]=ON ;
   7:  DO[127:ON :ROBOT CLEAR OF ZONE 1]=ON ;
   8:  DO[128:ON :ROBOT CLEAR OF ZONE 2]=ON ;
   9:  DO[141:ON :OP10R-1 ROBOT RETRACT]=ON ;
  10:  DO[173:ON :OP20L-1 ROBOT RETRACT]=ON ;
  11:  DO[222:ON :OP10R-2 ROBOT RETRACT]=ON ;
  12:  DO[254:ON :OP20L-2 ROBOT RETRACT]=ON ;
  13:  ENDIF ;
  14:   ;
  15:  IF (DO[23:ON :AT HOME]),F[10:OFF:ONE SHOT 1]=PULSE   ;
  16:   ;
  17:  IF (DO[23:ON :AT HOME] AND F[10:OFF:ONE SHOT 1]) THEN ;
  18:  DO[297:OFF:CONV ZONE REQ]=OFF ;
  19:  DO[298:OFF:OK 2 ZONE REQ]=OFF ;
  20:  DO[299:OFF:CMM ZONE REQ]=OFF ;
  21:  ENDIF ;
  22:   ;
  23:  IF (DO[297:OFF:CONV ZONE REQ] AND DO[306:OFF:IN CONV ZONE]),F[20:OFF:CONV ZONE ENTERED]=(ON) ;
  24:  IF (!DO[306:OFF:IN CONV ZONE] AND F[20:OFF:CONV ZONE ENTERED]),F[11:OFF:ONE SHOT 2]=PULSE   ;
  25:   ;
  26:  IF (!DO[306:OFF:IN CONV ZONE] AND F[11:OFF:ONE SHOT 2]) THEN ;
  27:  DO[297:OFF:CONV ZONE REQ]=OFF ;
  28:  F[20:OFF:CONV ZONE ENTERED]=(OFF) ;
  29:  ENDIF ;
  30:   ;
  31:  IF (DO[298:OFF:OK 2 ZONE REQ] AND DO[307:OFF:IN OK2 ZONE]),F[21:OFF:OKUMA 2 ZONE ENTERED]=(ON) ;
  32:  IF (!DO[307:OFF:IN OK2 ZONE] AND F[21:OFF:OKUMA 2 ZONE ENTERED]),F[11:OFF:ONE SHOT 2]=PULSE   ;
  33:   ;
  34:  IF (!DO[307:OFF:IN OK2 ZONE] AND F[11:OFF:ONE SHOT 2]) THEN ;
  35:  DO[298:OFF:OK 2 ZONE REQ]=OFF ;
  36:  F[21:OFF:OKUMA 2 ZONE ENTERED]=(OFF) ;
  37:  ENDIF ;
  38:   ;
  39:  IF (DO[299:OFF:CMM ZONE REQ] AND DO[308:OFF:IN CMM ZONE]),F[22:OFF:CMM ZONE ENTERED]=(ON) ;
  40:  IF (!DO[308:OFF:IN CMM ZONE] AND F[22:OFF:CMM ZONE ENTERED]),F[11:OFF:ONE SHOT 2]=PULSE   ;
  41:   ;
  42:  IF (!DO[308:OFF:IN CMM ZONE] AND F[11:OFF:ONE SHOT 2]) THEN ;
  43:  DO[299:OFF:CMM ZONE REQ]=OFF ;
  44:  F[22:OFF:CMM ZONE ENTERED]=(OFF) ;
  45:  ENDIF ;
  46:   ;
  47:  !GRIPPER CONTROL ;
  48:   ;
  49:  IF (RI[1:OFF:TOOL 1 OPEN] AND RO[1:OFF:TOOL 1 OPEN SOL]) THEN ;
  50:  DO[81:OFF:OP10 IN T1]=OFF ;
  51:  DO[82:OFF:AUDIT 1 IN T1]=OFF ;
  52:  DO[83:OFF:AUDIT 2 IN T1]=OFF ;
  53:  DO[84:OFF:REGRIP FOR OP10 IN T1]=OFF ;
  54:  DO[85:OFF:CMM STAND 1 IN T1]=OFF ;
  55:  DO[86:OFF:CMM STAND 2 IN T1]=OFF ;
  56:  DO[87:OFF:CMM 1 IN T1]=OFF ;
  57:  DO[88:OFF:CMM 2 IN T1]=OFF ;
  58:  ENDIF ;
  59:   ;
  60:  IF (RI[3:OFF:TOOL 2 OPEN] AND RO[3:OFF:TOOL 2 OPEN SOL]) THEN ;
  61:  DO[89:OFF:OP20 OK1 IN T2]=OFF ;
  62:  DO[90:OFF:OP20 OK2 IN T2]=OFF ;
  63:  DO[91:OFF:AUDIT 1 IN T2]=OFF ;
  64:  DO[92:OFF:AUDIT 2 IN T2]=OFF ;
  65:  DO[93:OFF:REGRIP 1 FOR OP20 IN T]=OFF ;
  66:  DO[94:OFF:REGRIP 2 FOR OP20 IN T]=OFF ;
  67:  ENDIF ;
  68:   ;
  69:  IF (RI[1:OFF:TOOL 1 OPEN] AND RO[1:OFF:TOOL 1 OPEN SOL] AND RI[3:OFF:TOOL 2 OPEN] AND RO[3:OFF:TOOL 2 OPEN SOL]) THEN ;
  70:  DO[121:OFF:ALL TOOLS OPEN]=ON ;
  71:  ELSE ;
  72:  DO[121:OFF:ALL TOOLS OPEN]=OFF ;
  73:  ENDIF ;
  74:   ;
  75:  IF (!RO[1:OFF:TOOL 1 OPEN SOL] AND !RI[1:OFF:TOOL 1 OPEN] AND !RI[2:OFF:TOOL 1 FULLY CLOSED]) THEN ;
  76:  DO[119:ON :PART IN T1]=ON ;
  77:  ELSE ;
  78:  DO[119:ON :PART IN T1]=OFF ;
  79:  ENDIF ;
  80:   ;
  81:  IF (!RO[3:OFF:TOOL 2 OPEN SOL] AND !RI[3:OFF:TOOL 2 OPEN] AND !RI[4:OFF:TOOL 2 FULLY CLOSED]) THEN ;
  82:  DO[120:ON :PART IN T2]=ON ;
  83:  ELSE ;
  84:  DO[120:ON :PART IN T2]=OFF ;
  85:  ENDIF ;
  86:   ;
  87:  DO[113:OFF:TOOL 1 OPEN SOL STATU]=(RO[1:OFF:TOOL 1 OPEN SOL]) ;
  88:  DO[114:OFF:TOOL 2 OPEN SOL STATU]=(RO[3:OFF:TOOL 2 OPEN SOL]) ;
  89:  DO[115:OFF:TOOL 1 OPEN STATUS]=(RI[1:OFF:TOOL 1 OPEN]) ;
  90:  DO[116:OFF:TOOL 1 CLOSED STATUS]=(RI[2:OFF:TOOL 1 FULLY CLOSED]) ;
  91:  DO[117:OFF:TOOL 2 OPEN STATUS]=(RI[3:OFF:TOOL 2 OPEN]) ;
  92:  DO[118:OFF:TOOL 2 CLOSED STATUS]=(RI[4:OFF:TOOL 2 FULLY CLOSED]) ;
  93:   ;
  94:  !OKUMA CONTROL ;
  95:  IF (DI[132:OFF:OP10R-1 DOOR OPEN 1] AND DI[140:OFF:OP10R-1 SYSTEM LINK] AND DI[142:OFF:OP10R-1 NC ALARM] AND DI[144:OFF:OP10R-1 MACHINE HOME]) THEN ;
  96:  DO[122:OFF:OP10R-1 SAFE FOR ROBO]=ON ;
  97:  ELSE ;
  98:  DO[122:OFF:OP10R-1 SAFE FOR ROBO]=OFF ;
  99:  ENDIF ;
 100:   ;
 101:  IF (DI[164:OFF:OP20L-1 DOOR OPEN 1] AND DI[172:OFF:OP20L-1 SYSTEM LINK] AND DI[174:OFF:OP20L-1 NC ALARM] AND DI[176:OFF:OP20L-1 MACHINE HOME]) THEN ;
 102:  DO[123:OFF:OP20L-1 SAFE FOR ROBO]=ON ;
 103:  ELSE ;
 104:  DO[123:OFF:OP20L-1 SAFE FOR ROBO]=OFF ;
 105:  ENDIF ;
 106:   ;
 107:  IF (DI[213:OFF:OP10R-2 DOOR OPEN 1] AND DI[221:OFF:OP10R-2 SYSTEM LINK] AND DI[223:OFF:OP10R-2 NC ALARM] AND DI[225:OFF:OP10R-2 MACHINE HOME]) THEN ;
 108:  DO[124:OFF:OP10R-2 SAFE FOR ROBO]=ON ;
 109:  ELSE ;
 110:  DO[124:OFF:OP10R-2 SAFE FOR ROBO]=OFF ;
 111:  ENDIF ;
 112:   ;
 113:  IF (DI[245:OFF:OP20L-2 DOOR OPEN 1] AND DI[253:OFF:OP20L-2 SYSTEM LINK] AND DI[255:OFF:OP20L-2 NC ALARM] AND DI[257:OFF:OP20L-2 MACHINE HOME]) THEN ;
 114:  DO[125:OFF:OP20L-2 SAFE FOR ROBO]=ON ;
 115:  ELSE ;
 116:  DO[125:OFF:OP20L-2 SAFE FOR ROBO]=OFF ;
 117:  ENDIF ;
 118:   ;
 119:  IF (DI[140:OFF:OP10R-1 SYSTEM LINK] AND DI[151:OFF:OP10R-1 REQUEST 1 M18] AND !DO[44:OFF:OP10-1 PRELOAD]) THEN ;
 120:  DO[44:OFF:OP10-1 PRELOAD]=ON ;
 121:  DO[148:OFF:OP10R-1 REQUEST ANSWE]=ON ;
 122:  ENDIF ;
 123:   ;
 124:  IF (DO[148:OFF:OP10R-1 REQUEST ANSWE] AND !DI[151:OFF:OP10R-1 REQUEST 1 M18]) THEN ;
 125:  DO[148:OFF:OP10R-1 REQUEST ANSWE]=OFF ;
 126:  ENDIF ;
 127:   ;
 128:  IF (DI[172:OFF:OP20L-1 SYSTEM LINK] AND DI[183:OFF:OP20L-1 REQUEST 1 M18] AND !DO[45:OFF:OP20-1 PRELOAD]) THEN ;
 129:  DO[45:OFF:OP20-1 PRELOAD]=ON ;
 130:  DO[180:OFF:OP20L-1 REQUEST ANSWE]=ON ;
 131:  ENDIF ;
 132:   ;
 133:  IF (DO[180:OFF:OP20L-1 REQUEST ANSWE] AND !DI[183:OFF:OP20L-1 REQUEST 1 M18]) THEN ;
 134:  DO[180:OFF:OP20L-1 REQUEST ANSWE]=OFF ;
 135:  ENDIF ;
 136:   ;
 137:  IF (DI[221:OFF:OP10R-2 SYSTEM LINK] AND DI[232:OFF:OP10R-2 REQUEST 1 M18] AND !DO[46:OFF:OP10-2 PRELOAD]) THEN ;
 138:  DO[46:OFF:OP10-2 PRELOAD]=ON ;
 139:  DO[229:OFF:OP10R-2 REQUEST ANSWE]=ON ;
 140:  ENDIF ;
 141:   ;
 142:  IF (DO[229:OFF:OP10R-2 REQUEST ANSWE] AND !DI[232:OFF:OP10R-2 REQUEST 1 M18]) THEN ;
 143:  DO[229:OFF:OP10R-2 REQUEST ANSWE]=OFF ;
 144:  ENDIF ;
 145:   ;
 146:  IF (DI[253:OFF:OP20L-2 SYSTEM LINK] AND DI[264:OFF:OP20L-2 REQUEST 1 M18] AND !DO[47:OFF:OP20-2 PRELOAD]) THEN ;
 147:  DO[47:OFF:OP20-2 PRELOAD]=ON ;
 148:  DO[261:OFF:OP20L-2 REQUEST ANSWE]=ON ;
 149:  ENDIF ;
 150:   ;
 151:  IF (DO[261:OFF:OP20L-2 REQUEST ANSWE] AND !DI[264:OFF:OP20L-2 REQUEST 1 M18]) THEN ;
 152:  DO[261:OFF:OP20L-2 REQUEST ANSWE]=OFF ;
 153:  ENDIF ;
 154:   ;
 155:  !AUDIT LOGIC ;
 156:  IF (!DI[73:OFF:AUDIT 1 REQUESTED] AND !DI[74:OFF:AUDIT 1 READY FOR LOAD] AND !DI[75:OFF:AUDIT 1 READY FOR UNLO] AND !DI[76:OFF:AUDIT 2 REQUESTED] AND !DI[77:OFF:AUDIT 2 READY FOR LOAD]) THEN ;
 157:  DO[69:ON :NO AUDITS ACTIVE]=ON ;
 158:  ELSE ;
 159:  DO[69:ON :NO AUDITS ACTIVE]=OFF ;
 160:  ENDIF ;
 161:   ;
 162:  !CMM LOGIC ;
 163:  IF (DI[106:OFF:CMM 1 MANUAL REQUEST] AND DI[100:OFF:ENABLE CMM]) THEN ;
 164:  DO[108:OFF:CMM 1 LAST]=OFF ;
 165:  DO[109:OFF:CMM 2 LAST]=ON ;
 166:  ENDIF ;
 167:   ;
 168:  IF (DI[107:OFF:CMM 2 MANUAL REQUEST] AND DI[100:OFF:ENABLE CMM]) THEN ;
 169:  DO[108:OFF:CMM 1 LAST]=ON ;
 170:  DO[109:OFF:CMM 2 LAST]=OFF ;
 171:  ENDIF ;
 172:   ;
 173:  !TORQUE MONITOR ;
 174:  IF (R[6:TORQUE AXIS]=1),R[7:TORQUE VALUE]=($MISC[1].$HPD_TRQ[1]) ;
 175:  IF (R[6:TORQUE AXIS]=2),R[7:TORQUE VALUE]=($MISC[1].$HPD_TRQ[2]) ;
 176:  IF (R[6:TORQUE AXIS]=3),R[7:TORQUE VALUE]=($MISC[1].$HPD_TRQ[3]) ;
 177:  IF (R[6:TORQUE AXIS]=4),R[7:TORQUE VALUE]=($MISC[1].$HPD_TRQ[4]) ;
 178:  IF (R[6:TORQUE AXIS]=5),R[7:TORQUE VALUE]=($MISC[1].$HPD_TRQ[5]) ;
 179:  IF (R[6:TORQUE AXIS]=6),R[7:TORQUE VALUE]=($MISC[1].$HPD_TRQ[6]) ;
 180:   ;
 181:  IF (R[7:TORQUE VALUE]>=R[8:MAX TORQUE]) THEN ;
 182:  R[9:TORQUE DETECTED]=1    ;
 183:  ELSE ;
 184:  R[9:TORQUE DETECTED]=0    ;
 185:  ENDIF ;
 186:   ;
 187:  IF (DO[121:OFF:ALL TOOLS OPEN] AND !DO[97:OFF:OP10 ON REGRIP 1] AND !DO[98:OFF:OP20 ON REGRIP 1] AND !DO[99:OFF:OP10 ON REGRIP 2] AND !DO[100:OFF:OP20 ON REGRIP 2] AND DI[136:OFF:OP10R-1 CHUCK OPEN] AND 
    :  DI[168:OFF:OP20L-1 CHUCK OPEN] AND DI[217:OFF:OP10R-2 CHUCK OPEN] AND DI[249:OFF:OP20L-2 CHUCK OPEN] AND !DI[75:OFF:AUDIT 1 READY FOR UNLO] AND !DI[77:OFF:AUDIT 2 READY FOR LOAD]) THEN ;
 188:  DO[38:OFF:SYSTEM EMPTY]=ON ;
 189:  ELSE ;
 190:  DO[38:OFF:SYSTEM EMPTY]=OFF ;
 191:  ENDIF ;
/POS
/END
