/PROG  RPS_OK1_OP10
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "OP10";
PROG_SIZE	= 3159;
CREATE		= DATE 19-06-05  TIME 00:41:40;
MODIFIED	= DATE 19-06-05  TIME 00:41:40;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 124;
MEMORY_SIZE	= 3599;
PROTECT		= READ;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  LBL[1:START] ;
   2:  CALL PRG_SELECT    ;
   3:  WAIT    .25(sec) ;
   4:  $WAITTMOUT=500 ;
   5:  !CHANGE REGISTER TO WHICHEVER ;
   6:  !CONTAINS PART MODEL INFO ;
   7:  R[200]=R[901:OK1 OP10 PRG]    ;
   8:  !R206 SHOULD BE SET TO THE ;
   9:  !DI NUM FOR BCD OK ;
  10:  R[206]=209    ;
  11:  !R210 SHOULD BE SET TO THE ;
  12:  !DO NUM FOR BCD INPUT 1 ;
  13:  R[210]=193    ;
  14:  !R207 SHOULD BE SET TO THE ;
  15:  !DO NUM FOR THE BUZZER ;
  16:  R[207]=37    ;
  17:  !R208 SHOULD BE SET TO THE ;
  18:  !GO NUM FOR RPS COMPARE ;
  19:  R[208]=11    ;
  20:  !R209 SHOULD BE SET TO THE ;
  21:  !GI NUM FOR RPS COMPARE ;
  22:  R[209]=11    ;
  23:   ;
  24:  LBL[10:CONVERT DO] ;
  25:  R[211]=R[210]+1    ;
  26:  R[212]=R[211]+1    ;
  27:  R[213]=R[212]+1    ;
  28:  R[214]=R[213]+1    ;
  29:  R[215]=R[214]+1    ;
  30:  R[216]=R[215]+1    ;
  31:  R[217]=R[216]+1    ;
  32:  R[218]=R[217]+1    ;
  33:  R[219]=R[218]+1    ;
  34:  R[220]=R[219]+1    ;
  35:  R[221]=R[220]+1    ;
  36:  R[222]=R[221]+1    ;
  37:  R[223]=R[222]+1    ;
  38:  R[224]=R[223]+1    ;
  39:  R[225]=R[224]+1    ;
  40:  R[226]=R[225]+1    ;
  41:  GO[R[208]]=0 ;
  42:   ;
  43:  LBL[20:CONVERT TO BCD] ;
  44:  R[202]=0    ;
  45:  R[203]=0    ;
  46:  R[204]=0    ;
  47:  R[205]=0    ;
  48:  SR[2]=SR[25]    ;
  49:  SR[3]=SR[25]    ;
  50:  SR[4]=SR[25]    ;
  51:  SR[5]=SR[25]    ;
  52:  SR[1]=R[200]    ;
  53:  R[201]=STRLEN SR[1] ;
  54:  SR[2]=SUBSTR SR[1],R[201],1 ;
  55:  R[201]=R[201]-1    ;
  56:  IF R[201]=0,JMP LBL[21] ;
  57:  SR[3]=SUBSTR SR[1],R[201],1 ;
  58:  R[201]=R[201]-1    ;
  59:  IF R[201]=0,JMP LBL[21] ;
  60:  SR[4]=SUBSTR SR[1],R[201],1 ;
  61:  R[201]=R[201]-1    ;
  62:  IF R[201]=0,JMP LBL[21] ;
  63:  SR[5]=SUBSTR SR[1],R[201],1 ;
  64:  LBL[21:SET BITS] ;
  65:  R[202]=SR[2]    ;
  66:  R[203]=SR[3]    ;
  67:  R[204]=SR[4]    ;
  68:  R[205]=SR[5]    ;
  69:   ;
  70:  LBL[100:SELECT BIT 1] ;
  71:  IF R[202]=0,JMP LBL[200] ;
  72:  IF (R[202]=1 OR R[202]=3 OR R[202]=5 OR R[202]=7 OR R[202]=9),DO[R[210]]=(ON) ;
  73:  IF (R[202]=2 OR R[202]=3 OR R[202]=6 OR R[202]=7),DO[R[211]]=(ON) ;
  74:  IF (R[202]=4 OR R[202]=5 OR R[202]=6 OR R[202]=7),DO[R[212]]=(ON) ;
  75:  IF (R[202]=8 OR R[202]=9),DO[R[213]]=(ON) ;
  76:   ;
  77:  LBL[200:SELECT BIT 2] ;
  78:  IF R[203]=0,JMP LBL[300] ;
  79:  IF (R[203]=1 OR R[203]=3 OR R[203]=5 OR R[203]=7 OR R[203]=9),DO[R[214]]=(ON) ;
  80:  IF (R[203]=2 OR R[203]=3 OR R[203]=6 OR R[203]=7),DO[R[215]]=(ON) ;
  81:  IF (R[203]=4 OR R[203]=5 OR R[203]=6 OR R[203]=7),DO[R[216]]=(ON) ;
  82:  IF (R[203]=8 OR R[203]=9),DO[R[217]]=(ON) ;
  83:   ;
  84:  LBL[300:SELECT BIT 3] ;
  85:  IF R[204]=0,JMP LBL[400] ;
  86:  IF (R[204]=1 OR R[204]=3 OR R[204]=5 OR R[204]=7 OR R[204]=9),DO[R[218]]=(ON) ;
  87:  IF (R[204]=2 OR R[204]=3 OR R[204]=6 OR R[204]=7),DO[R[219]]=(ON) ;
  88:  IF (R[204]=4 OR R[204]=5 OR R[204]=6 OR R[204]=7),DO[R[220]]=(ON) ;
  89:  IF (R[204]=8 OR R[204]=9),DO[R[221]]=(ON) ;
  90:   ;
  91:  LBL[400:SELECT BIT 4] ;
  92:  IF R[205]=0,JMP LBL[500] ;
  93:  IF (R[205]=1 OR R[205]=3 OR R[205]=5 OR R[205]=7 OR R[205]=9),DO[R[222]]=(ON) ;
  94:  IF (R[205]=2 OR R[205]=3 OR R[205]=6 OR R[205]=7),DO[R[223]]=(ON) ;
  95:  IF (R[205]=4 OR R[205]=5 OR R[205]=6 OR R[205]=7),DO[R[224]]=(ON) ;
  96:  IF (R[205]=8 OR R[205]=9),DO[R[225]]=(ON) ;
  97:   ;
  98:  LBL[500:STROBE] ;
  99:  DO[R[226]]=PULSE,1.0sec ;
 100:  R[227]=GO[R[208]]    ;
 101:  WAIT GI[R[209]]=R[227] TIMEOUT,LBL[998] ;
 102:  WAIT DI[R[206]]=ON TIMEOUT,LBL[998] ;
 103:  DO[R[226]]=OFF ;
 104:  R[990]=0    ;
 105:  JMP LBL[999] ;
 106:   ;
 107:  LBL[998:SELECT FAULT] ;
 108:  R[990]=R[990]+1    ;
 109:  IF R[990]<2,JMP LBL[1] ;
 110:  R[990]=0    ;
 111:   ;
 112:  CALL USERCLR    ;
 113:  MESSAGE[REMOTE PROGRAM SELECT] ;
 114:  MESSAGE[FAILED] ;
 115:  MESSAGE[...] ;
 116:  MESSAGE[INSPECT MACHINE FOR] ;
 117:  MESSAGE[ERRORS THEN RETRY] ;
 118:  DO[R[207]]=PULSE,3.0sec ;
 119:  UALM[9] ;
 120:  CALL USERCLR    ;
 121:  JMP LBL[1] ;
 122:   ;
 123:  LBL[999:END] ;
 124:  GO[R[208]]=0 ;
/POS
/END
