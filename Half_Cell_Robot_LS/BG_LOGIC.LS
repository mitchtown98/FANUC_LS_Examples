/PROG  BG_LOGIC
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "BACKGROUND LOGIC";
PROG_SIZE	= 5453;
CREATE		= DATE 19-12-13  TIME 06:00:34;
MODIFIED	= DATE 20-04-22  TIME 22:26:58;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 225;
MEMORY_SIZE	= 5885;
PROTECT		= READ;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !TURN ON BUZZER WHEN FAULTED ;
   2:  IF (DO[6:OFF:FAULTED]=ON AND SO[7:ON :TP enabled]=OFF),DO[37:OFF:SYSTEM BUZZER]=PULSE,2.0sec ;
   3:  IF (DI[70:OFF:REMOVE LAYER]=ON AND DO[6:OFF:FAULTED]=OFF) THEN ;
   4:  DO[37:OFF:SYSTEM BUZZER]=ON ;
   5:  ENDIF ;
   6:  IF (DI[70:OFF:REMOVE LAYER]=OFF AND DO[6:OFF:FAULTED]=OFF AND DO[37:OFF:SYSTEM BUZZER]=ON AND R[25:X COUNT]=1 AND R[27:Y COUNT]=1 AND R[35:LAYER PICK COUNT]=1) THEN ;
   7:  DO[37:OFF:SYSTEM BUZZER]=OFF ;
   8:  ENDIF ;
   9:  !HMI STATUS BUTTONS               ;
  10:  DO[35:ON :KEY SWITCH AUTO]=(SI[9:ON :CE/CR Select b1]) ;
  11:  DO[36:OFF:KEY SWITCH T1]=(!SI[9:ON :CE/CR Select b1]) ;
  12:  !STEP MODE ON OR OFF ;
  13:  R[120:STEP MODE]=$SSR.$SINGLESTEP ;
  14:  !ROBOT IS ACTIVE ;
  15:  IF (R[121:J1 TORQUE]=0 AND R[122:J2 TORQUE]=0 AND R[123:J3 TORQUE]=0 AND R[124:J4 TORQUE]=0 AND R[125:J5 TORQUE]=0 AND R[126:J6 TORQUE]=0) THEN ;
  16:  DO[101:OFF:ROBOT ACTIVE]=(OFF) ;
  17:  ELSE ;
  18:  DO[101:OFF:ROBOT ACTIVE]=(ON) ;
  19:  ENDIF ;
  20:  !GRIPPER CONTROL ;
  21:  IF (RO[1:OFF:OPEN TOOL 1]=ON AND RI[1:OFF:TOOL 1 OPENED]=ON AND DO[23:OFF:AT HOME]=ON AND F[12:OFF:REMOVE LAYER]=ON) THEN ;
  22:  DO[81:OFF:RAW DWN IN T1]=OFF ;
  23:  DO[82:OFF:RAW RGP4 ORNT T1]=OFF ;
  24:  DO[83:OFF:RAW FOR OP10 T1]=OFF ;
  25:  DO[85:OFF:OP20 FOR ORNT T1]=OFF ;
  26:  DO[86:OFF:ORNT FOR OP30 T1]=OFF ;
  27:  DO[87:OFF:ORNT FOR OP40 T1]=OFF ;
  28:  ENDIF ;
  29:  IF (RO[3:OFF:OPEN TOOL 2]=ON AND RI[5:OFF:TOOL 2 OPENED]=ON AND DO[23:OFF:AT HOME]=ON AND F[12:OFF:REMOVE LAYER]=ON) THEN ;
  30:  DO[84:OFF:OP10 FOR OP20 T2]=OFF ;
  31:  DO[88:OFF:OP30 FOR OP40 T2]=OFF ;
  32:  ENDIF ;
  33:  !ALL TOOLS OPEN ;
  34:  IF (RI[1:OFF:TOOL 1 OPENED]=ON AND RI[3:OFF:TOOL 1 CLOSED]=OFF AND RI[5:OFF:TOOL 2 OPENED]=ON AND RI[7:OFF:TOOL 2 CLOSED]=OFF) THEN ;
  35:  DO[121:OFF:ALL TOOLS OPEN]=ON ;
  36:  ELSE ;
  37:  DO[121:OFF:ALL TOOLS OPEN]=OFF ;
  38:  ENDIF ;
  39:  !PART IN TOOL 1 ;
  40:  IF (RI[1:OFF:TOOL 1 OPENED]=OFF AND RI[3:OFF:TOOL 1 CLOSED]=OFF) THEN ;
  41:  DO[119:ON :PART IN T1]=ON ;
  42:  ELSE ;
  43:  DO[119:ON :PART IN T1]=OFF ;
  44:  ENDIF ;
  45:  !PART IN TOOL 2 ;
  46:  IF (RI[5:OFF:TOOL 2 OPENED]=OFF AND RI[7:OFF:TOOL 2 CLOSED]=OFF) THEN ;
  47:  DO[120:ON :PART IN T2]=ON ;
  48:  ELSE ;
  49:  DO[120:ON :PART IN T2]=OFF ;
  50:  ENDIF ;
  51:  !HMI BUTTONS ;
  52:  !OPEN GRIPPER/RELEASE PART TOOL 1 ;
  53:  IF (DO[24:OFF:AT MAINTENANCE]=ON AND RI[1:OFF:TOOL 1 OPENED]=OFF AND RI[3:OFF:TOOL 1 CLOSED]=ON AND DI[24:OFF:TOOL 1]=ON) THEN ;
  54:  RO[2:OFF:CLOSE TOOL 1]=OFF ;
  55:  RO[1:OFF:OPEN TOOL 1]=ON ;
  56:  ENDIF ;
  57:  IF (DO[24:OFF:AT MAINTENANCE]=ON AND RI[1:OFF:TOOL 1 OPENED]=OFF AND RI[3:OFF:TOOL 1 CLOSED]=OFF AND DI[24:OFF:TOOL 1]=ON) THEN ;
  58:  RO[2:OFF:CLOSE TOOL 1]=OFF ;
  59:  RO[1:OFF:OPEN TOOL 1]=ON ;
  60:  ENDIF ;
  61:  !CLOSE GRIPPER/GRIP PART TOOL 1 ;
  62:  IF (DO[24:OFF:AT MAINTENANCE]=ON AND RI[1:OFF:TOOL 1 OPENED]=ON AND RI[3:OFF:TOOL 1 CLOSED]=OFF AND DI[24:OFF:TOOL 1]=ON) THEN ;
  63:  RO[1:OFF:OPEN TOOL 1]=OFF ;
  64:  RO[2:OFF:CLOSE TOOL 1]=ON ;
  65:  ENDIF ;
  66:  !HMI BUTTONS ;
  67:  !OPEN GRIPPER/RELEASE PART TOOL 2 ;
  68:  IF (DO[24:OFF:AT MAINTENANCE]=ON AND RI[5:OFF:TOOL 2 OPENED]=OFF AND RI[7:OFF:TOOL 2 CLOSED]=ON AND DI[25:OFF:TOOL 2]=ON) THEN ;
  69:  RO[4:OFF:CLOSE TOOL 2]=OFF ;
  70:  RO[3:OFF:OPEN TOOL 2]=ON ;
  71:  ENDIF ;
  72:  IF (DO[24:OFF:AT MAINTENANCE]=ON AND RI[5:OFF:TOOL 2 OPENED]=OFF AND RI[7:OFF:TOOL 2 CLOSED]=OFF AND DI[25:OFF:TOOL 2]=ON) THEN ;
  73:  RO[4:OFF:CLOSE TOOL 2]=OFF ;
  74:  RO[3:OFF:OPEN TOOL 2]=ON ;
  75:  ENDIF ;
  76:  !CLOSE GRIPPER/GRIP PART TOOL 2 ;
  77:  IF (DO[24:OFF:AT MAINTENANCE]=ON AND RI[5:OFF:TOOL 2 OPENED]=ON AND RI[7:OFF:TOOL 2 CLOSED]=OFF AND DI[25:OFF:TOOL 2]=ON) THEN ;
  78:  RO[3:OFF:OPEN TOOL 2]=OFF ;
  79:  RO[4:OFF:CLOSE TOOL 2]=ON ;
  80:  ENDIF ;
  81:  !HMI GRIPPER STATUS ;
  82:  DO[113:OFF:TOOL 1 OPEN SOL STATU]=(RO[1:OFF:OPEN TOOL 1]) ;
  83:  DO[114:OFF:TOOL 2 OPEN SOL STATU]=(RO[3:OFF:OPEN TOOL 2]) ;
  84:  DO[115:OFF:TOOL 1 OPEN STATUS]=(RI[1:OFF:TOOL 1 OPENED]) ;
  85:  DO[116:OFF:TOOL 1 CLOSED STATUS]=(RI[3:OFF:TOOL 1 CLOSED]) ;
  86:  DO[117:OFF:TOOL 2 OPEN STATUS]=(RI[5:OFF:TOOL 2 OPENED]) ;
  87:  DO[118:OFF:TOOL 2 CLOSED STATUS]=(RI[7:OFF:TOOL 2 CLOSED]) ;
  88:  !RESET LAYER BUTTON 4 HMI ;
  89:  IF (R[25:X COUNT]>=3 AND R[27:Y COUNT]=4 OR F[13:OFF:LAYER CHANGE]=ON) THEN ;
  90:  DO[68:OFF:HMI RESET LAYER]=ON ;
  91:  ELSE ;
  92:  DO[68:OFF:HMI RESET LAYER]=OFF ;
  93:  ENDIF ;
  94:  !CHECK LAYER HEIGHT ;
  95:  IF (R[24:LAYER COUNT (Z)]<1),F[14:OFF:CHECK LAYER HEIGHT]=(ON) ;
  96:  IF (R[24:LAYER COUNT (Z)]>=1),F[14:OFF:CHECK LAYER HEIGHT]=(OFF) ;
  97:   ;
  98:  !RESET LAYER CHANGE ;
  99:  IF (DI[48:OFF:RESET LAYER CHANGE]=ON AND F[13:OFF:LAYER CHANGE]=ON AND R[25:X COUNT]=1 AND R[27:Y COUNT]=1),F[13:OFF:LAYER CHANGE]=(OFF) ;
 100:  !COMPARE LAYER COUNT ;
 101:  IF (R[24:LAYER COUNT (Z)]<>R[50:LAYERS INPUT]) THEN ;
 102:  R[994:LAYER TIMER]=R[994:LAYER TIMER]+1    ;
 103:  ENDIF ;
 104:  IF (R[994:LAYER TIMER]>=R[996:TIMER MAX]) THEN ;
 105:  R[24:LAYER COUNT (Z)]=R[50:LAYERS INPUT]    ;
 106:  R[994:LAYER TIMER]=0    ;
 107:  ENDIF ;
 108:  !OKUMA OP10 RIGHT READY ;
 109:  IF (DI[164:OFF:OP20L-1 DOOR OPEN 1] AND DI[172:OFF:OP20L-1 SYSTEM LINK] AND DI[174:OFF:OP20L-1 NC ALARM] AND DI[176:OFF:OP20L-1 MACHINE HOME]=ON AND DO[176:OFF:OP20L-1 CYCLE START]=OFF) THEN ;
 110:  DO[502:OFF:OKUMA 1L READY]=ON ;
 111:  ELSE ;
 112:  DO[502:OFF:OKUMA 1L READY]=OFF ;
 113:  ENDIF ;
 114:  !OKUMA OP20 LEFT READY ;
 115:  IF (DI[132:OFF:OP10R-1 DOOR OPEN 1]=ON AND DI[140:OFF:OP10R-1 SYSTEM LINK]=ON AND DI[142:OFF:OP10R-1 NC ALARM]=ON AND DI[144:OFF:OP10R-1 MACHINE HOME]=ON AND DO[144:OFF:OP10R-1 CYCLE START]=OFF) 
    :  THEN ;
 116:  DO[501:OFF:OKUMA 1R READY]=ON ;
 117:  ELSE ;
 118:  DO[501:OFF:OKUMA 1R READY]=OFF ;
 119:  ENDIF ;
 120:  !OKUMA 2 READY ;
 121:  IF (DI[210:OFF:OK2 NC ALARM]=OFF AND DI[211:OFF:OK2 SYSTEM LINK]=ON) THEN ;
 122:  DO[503:OFF:OKUMA 2 READY]=ON ;
 123:  ELSE ;
 124:  DO[503:OFF:OKUMA 2 READY]=OFF ;
 125:  ENDIF ;
 126:  --eg:IF 0 PARTS ARE LEFT AND FLAG TO CHANGE LAYER HASNT BEEN TURNED ON
    :  YET ;
 127:  IF (DI[48:OFF:RESET LAYER CHANGE]=ON AND F[13:OFF:LAYER CHANGE]=OFF AND R[24:LAYER COUNT (Z)]>0 AND R[27:Y COUNT]=R[28:Y MAX] AND R[25:X COUNT]<>R[26:X MAX]) THEN ;
 128:  R[25:X COUNT]=1    ;
 129:  R[27:Y COUNT]=1    ;
 130:  R[24:LAYER COUNT (Z)]=R[24:LAYER COUNT (Z)]-1    ;
 131:  ENDIF ;
 132:   ;
 133:  !DUNNAGE SEPERATION LAYERS ;
 134:  !0 = NO SELECTION MADE ;
 135:  IF (R[51:LAYER TYPE]=0),R[47:LAYER THICKNESS]=(0) ;
 136:  IF (R[51:LAYER TYPE]=0),DO[37:OFF:SYSTEM BUZZER]=PULSE,5.0sec ;
 137:  !1=PLASTIC ;
 138:  IF (R[51:LAYER TYPE]=1),R[47:LAYER THICKNESS]=(3.175) ;
 139:  !2= 1/4" PLYWOOD ;
 140:  IF (R[51:LAYER TYPE]=2),R[47:LAYER THICKNESS]=(10) ;
 141:  !3= 1/2" PLYWOOD ;
 142:  IF (R[51:LAYER TYPE]=3),R[47:LAYER THICKNESS]=(12.7) ;
 143:  !4= 5/8" PLYWOOD ;
 144:  IF (R[51:LAYER TYPE]=4),R[47:LAYER THICKNESS]=(15.875) ;
 145:  !5= 3/4" PLYWOOD ;
 146:  IF (R[51:LAYER TYPE]=5),R[47:LAYER THICKNESS]=(19.05) ;
 147:  !LAYER THICKNESS ;
 148:  R[49:LAYER THICKNESS]=R[48:RAW PART HGHT]+R[47:LAYER THICKNESS]    ;
 149:  !LAYER HEIGHT ;
 150:  R[46:LAYER HEIGHT]=(R[24:LAYER COUNT (Z)]*R[49:LAYER THICKNESS]-R[49:LAYER THICKNESS]) ;
 151:  !APP Z CALCULATION ;
 152:  IF (R[24:LAYER COUNT (Z)]>0) THEN ;
 153:  R[30:APP Z HEIGHT]=(R[24:LAYER COUNT (Z)]*R[49:LAYER THICKNESS]) ;
 154:  ELSE ;
 155:  R[30:APP Z HEIGHT]=0    ;
 156:  ENDIF ;
 157:  !ROBOT PERCHED ;
 158:  IF (DO[23:OFF:AT HOME]=ON OR DO[25:OFF:AT INBOUND PERCH]=ON OR DO[26:OFF:AT OKUMA 1 PERCH]=ON OR DO[27:OFF:AT OKUMA 2 PERCH]=ON OR DO[28:OFF:AT AUDIT PERCH]=ON) THEN ;
 159:  DO[496:OFF:ROBOT PERCHED]=ON ;
 160:  ELSE ;
 161:  DO[496:OFF:ROBOT PERCHED]=OFF ;
 162:  ENDIF ;
 163:  !TORQUE MONITOR ;
 164:  R[121:J1 TORQUE]=($MISC[1].$HPD_TRQ[1]) ;
 165:  R[122:J2 TORQUE]=($MISC[1].$HPD_TRQ[2]) ;
 166:  R[123:J3 TORQUE]=($MISC[1].$HPD_TRQ[3]) ;
 167:  R[124:J4 TORQUE]=($MISC[1].$HPD_TRQ[4]) ;
 168:  R[125:J5 TORQUE]=($MISC[1].$HPD_TRQ[5]) ;
 169:  R[126:J6 TORQUE]=($MISC[1].$HPD_TRQ[6]) ;
 170:  !TORQUE ACHIEVED ;
 171:  IF (R[123:J3 TORQUE]>=R[128:TORQUE TARGET2]),R[130:TORQUE ACHIEVED2]=(1) ;
 172:  IF (R[125:J5 TORQUE]>=R[127:TORQUE TARGET1]),R[129:TORQUE ACHIEVED1]=(1) ;
 173:  IF (R[124:J4 TORQUE]>=R[131:TORQUE TARGET 3]),R[132:TORQUE ACHIEVED3]=(1) ;
 174:  !AUDIT LOGIC ;
 175:  IF (!DI[73:OFF:AUDIT 1 REQUESTED] AND !DI[74:OFF:AUDIT READY LOAD] AND !DI[75:OFF:AUDIT 1 READY FOR UNLO] AND !DI[76:OFF] AND !DI[77:OFF]) THEN ;
 176:  DO[69:ON :NO AUDITS ACTIVE]=ON ;
 177:  ELSE ;
 178:  DO[69:ON :NO AUDITS ACTIVE]=OFF ;
 179:  ENDIF ;
 180:  !OKUMA 1R SAFE FOR ROBOT  ;
 181:  IF (DI[132:OFF:OP10R-1 DOOR OPEN 1]=ON AND DI[140:OFF:OP10R-1 SYSTEM LINK]=ON AND DI[142:OFF:OP10R-1 NC ALARM]=ON AND DI[143:OFF:OP10R-1 PRGM END]=ON AND DI[144:OFF:OP10R-1 MACHINE HOME]=ON) THEN ;
 182:  DO[122:OFF:OP10R SAFE FOR ROBOT]=ON ;
 183:  ELSE ;
 184:  DO[122:OFF:OP10R SAFE FOR ROBOT]=OFF ;
 185:  ENDIF ;
 186:  !OKUMA 1L SAFE FOR ROBOT  ;
 187:  IF (DI[164:OFF:OP20L-1 DOOR OPEN 1]=ON AND DI[172:OFF:OP20L-1 SYSTEM LINK]=ON AND DI[174:OFF:OP20L-1 NC ALARM]=ON AND DI[175:OFF:OP20L-1 PRGM END]=ON AND DI[176:OFF:OP20L-1 MACHINE HOME]=ON) THEN ;
 188:  DO[123:OFF:OP20L SAFE FOR ROBOT]=ON ;
 189:  ELSE ;
 190:  DO[123:OFF:OP20L SAFE FOR ROBOT]=OFF ;
 191:  ENDIF ;
 192:  !OKUMA 2 SAFE FOR ROBOT  ;
 193:  IF (DI[210:OFF:OK2 NC ALARM]=OFF AND DI[211:OFF:OK2 SYSTEM LINK]=ON AND DI[228:OFF:OK2 PALLET CHANGE REQ]=ON) THEN ;
 194:  DO[124:OFF:OK2 SAFE FOR ROBOT]=ON ;
 195:  ELSE ;
 196:  DO[124:OFF:OK2 SAFE FOR ROBOT]=OFF ;
 197:  ENDIF ;
 198:  !VISION HOLD ;
 199:  IF (R[37:PASS/FAIL POCKET]=1) THEN ;
 200:  DO[512:ON :HOLD VISION]=ON ;
 201:  ELSE ;
 202:  DO[512:ON :HOLD VISION]=OFF ;
 203:  ENDIF ;
 204:  !LAYER CHANGE RESET ;
 205:  IF (F[13:OFF:LAYER CHANGE]=ON AND DI[5:OFF:FAULT RESET]=ON),F[13:OFF:LAYER CHANGE]=(OFF) ;
 206:   ;
 207:  !TURN OFF PURGE ;
 208:  IF (DI[5:OFF:FAULT RESET]=ON) THEN ;
 209:  R[995:RESET TIMER]=R[995:RESET TIMER]+1    ;
 210:  ENDIF ;
 211:  IF (DI[5:OFF:FAULT RESET]=OFF),R[995:RESET TIMER]=(0) ;
 212:  IF (R[995:RESET TIMER]>=R[996:TIMER MAX] AND DI[21:OFF:PURGE REQUEST]=ON),DO[21:OFF:PURGE COMPLETE]=(ON) ;
 213:  IF (DI[5:OFF:FAULT RESET]=OFF AND DI[21:OFF:PURGE REQUEST]=OFF AND DO[21:OFF:PURGE COMPLETE]=ON AND DO[3:OFF:RUNNING]=OFF),DO[21:OFF:PURGE COMPLETE]=(OFF) ;
 214:   ;
 215:  !TURN OFF DOOR SIGNAL OUTPUT ;
 216:  IF (DO[132:OFF:OP10R-1 DOOR OPEN]=ON AND DI[132:OFF:OP10R-1 DOOR OPEN 1]=ON),DO[132:OFF:OP10R-1 DOOR OPEN]=(OFF) ;
 217:  IF (DO[164:OFF:OP20L-1 DOOR OPEN]=ON AND DI[164:OFF:OP20L-1 DOOR OPEN 1]=ON),DO[164:OFF:OP20L-1 DOOR OPEN]=(OFF) ;
 218:  !REMOVE LAYER ;
 219:  IF (DI[70:OFF:REMOVE LAYER]=ON AND R[25:X COUNT]>1 OR DI[70:OFF:REMOVE LAYER]=ON AND R[27:Y COUNT]>1 OR DI[70:OFF:REMOVE LAYER]=ON AND R[35:LAYER PICK COUNT]>1) THEN ;
 220:  R[25:X COUNT]=1    ;
 221:  R[27:Y COUNT]=1    ;
 222:  R[35:LAYER PICK COUNT]=1    ;
 223:  R[24:LAYER COUNT (Z)]=R[24:LAYER COUNT (Z)]-1    ;
 224:  R[50:LAYERS INPUT]=R[50:LAYERS INPUT]-1    ;
 225:  ENDIF ;
/POS
/END
