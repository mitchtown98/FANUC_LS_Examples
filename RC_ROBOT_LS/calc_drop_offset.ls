/PROG  CALC_DROP_OFFSET
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "TABLE DROP OFFS";
PROG_SIZE	= 1229;
CREATE		= DATE 23-08-15  TIME 13:27:08;
MODIFIED	= DATE 23-08-15  TIME 13:27:08;
FILE_NAME	= CALC_PIC;
VERSION		= 0;
LINE_COUNT	= 40;
MEMORY_SIZE	= 1701;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  --eg:CALCULATE XYZ OFFSET OF RAW PART BASED ON CURRENT SLOT ;
   2:   ;
   3:  LBL[1:START] ;
   4:  --eg:MOVE ALL ZEROS IN PICK OFFSETS POSITION REGISTER ;
   5:  PR[40:TABLE DROP OFFS]=PR[30:CARTESIAN ZERO]    ;
   6:  --eg:CALCULATE ROW OFFSET (X) ;
   7:  PR[40,1:TABLE DROP OFFS]=((R[4:CUR ROW DROP (X)]*R[12:ROW SPACING (X)])-(R[12:ROW SPACING (X)])) ;
   8:  !CALC X OFFSET FOR DIAMETER       ;
   9:  R[80:RAW CENTER X OFF]=(.7071*(R[84:SIDE LENGTH]-R[81:FINISHED DIA])) ;
  10:  PR[40,1:TABLE DROP OFFS]=PR[40,1:TABLE DROP OFFS]-R[80:RAW CENTER X OFF]    ;
  11:   ;
  12:  --eg:CALCULATE COLUMN OFFSET (Y) ;
  13:  PR[40,2:TABLE DROP OFFS]=((R[5:CUR COL DROP (Y)]*R[13:COL SPACING (Y)])-(R[13:COL SPACING (Y)])) ;
  14:  --eg:GET PART LENGTH AND CALCULATE Z OFFSET ;
  15:  IF (DI[1:TABLE AT A POSITION]=ON) THEN ;
  16:  PR[40,3:TABLE DROP OFFS]=R[72:FIN PART LENGTH]-R[74:SPRING DIST A]+5    ;
  17:  ENDIF ;
  18:  IF (DI[2:TABLE AT B POSITION]=ON) THEN ;
  19:  PR[40,3:TABLE DROP OFFS]=R[72:FIN PART LENGTH]-R[83:SPRING DIST B]+5    ;
  20:  ENDIF ;
  21:   ;
  22:  --eg:OFFSETS FOR TWO OFFSET SLOTS ON TEMPLATE 5. ;
  23:   ;
  24:  IF (R[1:ACTIVE TEMPLATE]=5) THEN ;
  25:  IF (R[7:CUR DROP SLOT #]=20) THEN ;
  26:  PR[40,1:TABLE DROP OFFS]=202.66    ;
  27:  R[80:RAW CENTER X OFF]=(.7071*(R[84:SIDE LENGTH]-R[77:RAW DIAMETER])) ;
  28:  PR[40,1:TABLE DROP OFFS]=PR[40,1:TABLE DROP OFFS]-R[80:RAW CENTER X OFF]    ;
  29:  PR[40,2:TABLE DROP OFFS]=86.66    ;
  30:  ENDIF ;
  31:   ;
  32:  IF (R[7:CUR DROP SLOT #]=26) THEN ;
  33:  PR[40,1:TABLE DROP OFFS]=202.66    ;
  34:  R[80:RAW CENTER X OFF]=(.7071*(R[84:SIDE LENGTH]-R[77:RAW DIAMETER])) ;
  35:  PR[40,1:TABLE DROP OFFS]=PR[40,1:TABLE DROP OFFS]-R[80:RAW CENTER X OFF]    ;
  36:  PR[40,2:TABLE DROP OFFS]=793.44    ;
  37:  ENDIF ;
  38:  ENDIF ;
  39:   ;
  40:  LBL[9999:END] ;
/POS
/END
