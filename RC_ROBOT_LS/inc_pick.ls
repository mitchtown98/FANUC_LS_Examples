/PROG  INC_PICK
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "SLOT INCREMENT";
PROG_SIZE	= 1021;
CREATE		= DATE 23-11-21  TIME 03:26:46;
MODIFIED	= DATE 23-11-21  TIME 03:26:46;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 46;
MEMORY_SIZE	= 1469;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  --eg:INCREMENT REGISTERS TO NEXT AVAILABLE RAW PART PICK SLOT ;
   2:   ;
   3:  LBL[1:SELECT INC] ;
   4:  WAIT DO[46:RAW PART PICKED]=OFF AND DO[47:FINISHED PART DROPPED]=OFF AND DO[48:PART SLOT EMPTY]=OFF    ;
   5:  WAIT    .25(sec) ;
   6:  IF (R[3:CUR COL PICK (Y)]<R[11:NUMBER OF COLS]),JMP LBL[100] ;
   7:  IF (R[3:CUR COL PICK (Y)]>=R[R[37]]),JMP LBL[200] ;
   8:  JMP LBL[9999] ;
   9:   ;
  10:  LBL[100:INC COLUMN] ;
  11:  IF (R[3:CUR COL PICK (Y)]<R[R[36]]) THEN ;
  12:  R[3:CUR COL PICK (Y)]=R[R[36]]    ;
  13:  ELSE ;
  14:  R[3:CUR COL PICK (Y)]=R[3:CUR COL PICK (Y)]+2    ;
  15:  ENDIF ;
  16:   ;
  17:  IF (R[3:CUR COL PICK (Y)]>R[R[37]]),JMP LBL[200] ;
  18:  JMP LBL[400] ;
  19:   ;
  20:  LBL[200:INC ROW] ;
  21:  R[2:CUR ROW PICK (X)]=R[2:CUR ROW PICK (X)]+1    ;
  22:  R[3:CUR COL PICK (Y)]=1    ;
  23:  WAIT    .10(sec) ;
  24:  IF (R[2:CUR ROW PICK (X)]>R[10:NUMBER OF ROWS]),JMP LBL[300] ;
  25:  IF (R[3:CUR COL PICK (Y)]<R[R[36]]),JMP LBL[100] ;
  26:  JMP LBL[400] ;
  27:   ;
  28:  LBL[300:TABLE PICK COMP] ;
  29:  DO[43:TABLE PICK COMPLETE]=ON ;
  30:  DO[32:PURGE ACTIVE]=ON ;
  31:  R[2:CUR ROW PICK (X)]=1    ;
  32:  R[3:CUR COL PICK (Y)]=1    ;
  33:   ;
  34:  IF (DO[211:MACHINE EMPTY]) THEN ;
  35:  DO[44:TABLE DROP COMPLETE]=ON ;
  36:  ENDIF ;
  37:   ;
  38:  JMP LBL[9999] ;
  39:   ;
  40:  LBL[400:VERIFY SLOT] ;
  41:  WAIT    .10(sec) ;
  42:  IF (R[6:CUR PICK SLOT #]=R[32:1ST SLOT SKIP] OR R[6:CUR PICK SLOT #]=R[33:2ND SLOT SKIP] OR R[6:CUR PICK SLOT #]=R[34:3RD SLOT SKIP] OR R[6:CUR PICK SLOT #]=R[35:4TH SLOT SKIP]),JMP LBL[1] ;
  43:  IF (R[3:CUR COL PICK (Y)]<R[R[36]] OR R[3:CUR COL PICK (Y)]>R[R[37]]),JMP LBL[1] ;
  44:  JMP LBL[9999] ;
  45:   ;
  46:  LBL[9999:END] ;
/POS
/END
