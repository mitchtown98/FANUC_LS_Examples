/PROG  T2_GRIP
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "TOOL GRIP";
PROG_SIZE	= 1407;
CREATE		= DATE 21-10-21  TIME 10:42:08;
MODIFIED	= DATE 22-05-23  TIME 15:38:40;
FILE_NAME	= T1_GRIP;
VERSION		= 0;
LINE_COUNT	= 70;
MEMORY_SIZE	= 1943;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;

  AUTOZONE_INTERFERENCE_AVOID;
    AUTOZONE_ENABLED         : FALSE;
    AUTOZONE_SCHEDULE_NUMBER : 1;
/MN
   1:  LBL[1:START] ;
   2:  IF DO[118:T2 OD GRIP]=ON AND DO[119:T2 ID GRIP]=OFF,JMP LBL[100] ;
   3:  IF DO[119:T2 ID GRIP]=ON AND DO[118:T2 OD GRIP]=OFF,JMP LBL[200] ;
   4:  JMP LBL[9990] ;
   5:   ;
   6:  LBL[100:OD GRIP] ;
   7:  $WAITTMOUT=200 ;
   8:  RO[3:T2 OPEN]=OFF ;
   9:  RO[4:T2 CLOSE]=ON ;
  10:  WAIT RI[3:T2 FULLY OPENED]=OFF TIMEOUT,LBL[9991] ;
  11:  WAIT    .75(sec) ;
  12:  WAIT RI[4:T2 FULLY CLOSED]=OFF TIMEOUT,LBL[9992] ;
  13:  JMP LBL[9999] ;
  14:   ;
  15:  LBL[200:ID GRIP] ;
  16:  $WAITTMOUT=200 ;
  17:  RO[4:T2 CLOSE]=OFF ;
  18:  RO[3:T2 OPEN]=ON ;
  19:  WAIT RI[4:T2 FULLY CLOSED]=OFF TIMEOUT,LBL[9993] ;
  20:  WAIT    .75(sec) ;
  21:  WAIT RI[3:T2 FULLY OPENED]=OFF TIMEOUT,LBL[9994] ;
  22:  JMP LBL[9999] ;
  23:   ;
  24:  LBL[9990:GRIPPER FAULT] ;
  25:  CALL TOOL_MSG(8) ;
  26:  UALM[1] ;
  27:  JMP LBL[1] ;
  28:   ;
  29:  LBL[9991:GRIPPER FAULT] ;
  30:  RO[4:T2 CLOSE]=OFF ;
  31:  RO[3:T2 OPEN]=ON ;
  32:  WAIT    .25(sec) ;
  33:  CALL TOOL_MSG(11) ;
  34:  UALM[1] ;
  35:  JMP LBL[1] ;
  36:   ;
  37:  LBL[9992:GRIPPER FAULT] ;
  38:  RO[4:T2 CLOSE]=OFF ;
  39:  RO[3:T2 OPEN]=ON ;
  40:  WAIT    .25(sec) ;
  41:  IF DO[36:PART PRESENT CHECK ONLY]=ON,JMP LBL[9999] ;
  42:  CALL TOOL_MSG(12) ;
  43:  UALM[1] ;
  44:  JMP LBL[1] ;
  45:   ;
  46:  LBL[9993:GRIPPER FAULT] ;
  47:  RO[3:T2 OPEN]=OFF ;
  48:  RO[4:T2 CLOSE]=ON ;
  49:  WAIT    .25(sec) ;
  50:  CALL TOOL_MSG(13) ;
  51:  UALM[1] ;
  52:  JMP LBL[1] ;
  53:   ;
  54:  LBL[9994:GRIPPER FAULT] ;
  55:  RO[3:T2 OPEN]=OFF ;
  56:  RO[4:T2 CLOSE]=ON ;
  57:  WAIT    .25(sec) ;
  58:  IF DO[36:PART PRESENT CHECK ONLY]=ON,JMP LBL[9999] ;
  59:  CALL TOOL_MSG(14) ;
  60:  UALM[1] ;
  61:  JMP LBL[1] ;
  62:   ;
  63:  LBL[9999:END] ;
  64:  IF (DO[175:PART IN T2]) THEN ;
  65:  MONITOR MON_T2 ;
  66:  ENDIF ;
  67:   ;
  68:  CALL PAYLOAD    ;
  69:  R[901:TOOL 2 COUNT]=R[901:TOOL 2 COUNT]-1    ;
  70:  DO[36:PART PRESENT CHECK ONLY]=OFF ;
/POS
/END
