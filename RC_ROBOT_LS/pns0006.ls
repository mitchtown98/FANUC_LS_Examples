/PROG  PNS0006
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "CENTERLINE";
PROG_SIZE	= 5557;
CREATE		= DATE 23-01-06  TIME 11:31:52;
MODIFIED	= DATE 23-12-07  TIME 11:30:26;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 175;
MEMORY_SIZE	= 6073;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  --eg:*****  ATTENTION **** ;
   2:  --eg:ENTER PART / JAW LENGTH ON LINE 7 BEFORE STARTING  ;
   3:  !START PROGRAM HERE. DO NOT SKIP  ;
   4:  !LINES. THE PROGRAM WILL GUIDE    ;
   5:  !YOU THROUGH SETUP PROCESS        ;
   6:  LBL[1:START] ;
   7:  R[285:PART\JAW LENGTH]=117    ;
   8:  !ENTER LENGTH OF PART YOU ARE     ;
   9:  !USING TO TEACH CENTERLINE.       ;
  10:  !MEASURE FROM END OF PART         ;
  11:  !TO FACE OF CHUCK                 ;
  12:  !USE MILLIMETERS FOR LENGTH       ;
  13:  !ENTER LENGTH IN REGISTER 285     ;
  14:  IF (R[285:PART\JAW LENGTH]=0),JMP LBL[1] ;
  15:  IF DO[145:AT HOME]=OFF,CALL PNS0002 ;
  16:  !PROGRAM WILL CLEAR X DATA        ;
  17:  R[281:MAX X MAIN]=0    ;
  18:  R[282:MIN X MAIN]=0    ;
  19:  R[283:MAX X SUB]=0    ;
  20:  R[284:MIN X SUB]=0    ;
  21:  UFRAME_NUM=5 ;
  22:J PR[1:HOME POSITION] 100% CNT20    ;
  23:  R[40:RECOVER START PR]=1    ;
  24:J PR[23:MOVE TO MACH] 100% CNT60    ;
  25:  R[40:RECOVER START PR]=23    ;
  26:J PR[80:MACHINE PERCH] 100% CNT60    ;
  27:  R[40:RECOVER START PR]=80    ;
  28:  PAUSE ;
  29:  --eg:TOUCH UP BELOW FOR MACHINE PERCH. POINT SHOULD BE TAUGHT JUST
    :  OUTSIDE OF MACHINE DOOR WITH GRIPPERS CENTERED ON DOOR, TOOL 1 FACING
    :  THE MAIN SPINDLE. ;
  30:J PR[80:MACHINE PERCH] 100% CNT60    ;
  31:  --eg:^^^^TOUCH UP HERE^^^^ ;
  32:  R[40:RECOVER START PR]=80    ;
  33:  JMP LBL[100] ;
  34:  --eg:LOAD MAIN SPINDLE CENTERLINE ;
  35:  LBL[100:LOAD MAIN] ;
  36:  --eg:TOUCHUP BELOW FOR MACHINE APPROACH, THIS POSITION SHOULD BE JUST
    :  INSIDE THE DOOR AND HAVE TOOL 1 TOWARDS THE MAIN SPINDLE. THIS POINT
    :  WILL BE REFERENCED MULTIPLE TIMES. ;
  37:L PR[81:MACH APPR] 2000mm/sec FINE    ;
  38:  --eg:^^^^TOUCH UP HERE^^^^ ;
  39:  R[40:RECOVER START PR]=81    ;
  40:  PAUSE ;
  41:  UFRAME_NUM=5 ;
  42:  UTOOL_NUM=1 ;
  43:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  44:  PR[34,3:APP Z TOOL OFF]=75    ;
  45:L PR[89:MASTER LOAD MAIN] 640mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  46:  R[40:RECOVER START PR]=85    ;
  47:  PAUSE ;
  48:  --eg:TOUCHUP POINT BELOW, GRIPPER SHOULD BE CENTERED ON THE PART WITH NO
    :  MOVEMENT WHEN OPENING AND CLOSING THE GRIPPER. ;
  49:L PR[89:MASTER LOAD MAIN] 75mm/sec FINE    ;
  50:  --eg:^^^^TOUCH UP HERE^^^^ ;
  51:  PAUSE ;
  52:  !SET MIN X VALUE FOR MAIN SPIN ;
  53:  R[282:MIN X MAIN]=PR[89,1:MASTER LOAD MAIN]-R[285:PART\JAW LENGTH]    ;
  54:  PR[34,3:APP Z TOOL OFF]=75    ;
  55:L PR[89:MASTER LOAD MAIN] 500mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  56:  $SCR.$ORG_PTH_RSM=0 ;
  57:  $JCR_GRP[1].$JOG_COORD=3 ;
  58:  PAUSE ;
  59:  !ENSURE S/TOOL JOG MODE IS ACTIV ;
  60:  !JOG ROBOT IN +Z DIRECTION ;
  61:  !MOVE ROBOT AWAY FROM CHUCK ;
  62:  !STOP WHEN ROBOT IS AT THE ;
  63:  !FARTHEST POINT FROM CHUCK BUT ;
  64:  !STILL HAS CLEARANCE INSIDE OF ;
  65:  !MACHINE. ;
  66:  PR[47:LPOS 1]=LPOS    ;
  67:  R[281:MAX X MAIN]=PR[47,1:LPOS 1]    ;
  68:  R[40:RECOVER START PR]=17    ;
  69:J PR[81:MACH APPR] 100% FINE    ;
  70:  R[40:RECOVER START PR]=81    ;
  71:  $SCR.$ORG_PTH_RSM=1 ;
  72:  JMP LBL[200] ;
  73:  --eg:UNLOAD MAIN SPINDLE CENTERLINE ;
  74:  LBL[200:UNLOAD MAIN] ;
  75:  UFRAME_NUM=5 ;
  76:  UTOOL_NUM=2 ;
  77:  PR[12:JOINT OFFSET]=PR[12:JOINT OFFSET]-PR[12:JOINT OFFSET]    ;
  78:  IF (PR[81,6:MACH APPR]<0),PR[12,6:JOINT OFFSET]=(180) ;
  79:  IF (PR[81,6:MACH APPR]>=0),PR[12,6:JOINT OFFSET]=((-180)) ;
  80:J PR[81:MACH APPR] 100% FINE Offset,PR[12:JOINT OFFSET]    ;
  81:  R[40:RECOVER START PR]=81    ;
  82:  --eg:ROBOT WILL DO CALCULATION TO GET CLOSE TO THE LOAD POSITION, EACH
    :  TIME THE PROGRAM IS RAN THE TAUGHT UNLOAD WILL BE OVERWROTE UNLESS
    :  LINES BELOW ARE SKIPPED. ;
  83:  PR[90:MASTER UNLD MAIN]=PR[89:MASTER LOAD MAIN]    ;
  84:  --eg:MOVE CURSOUR TO THIS LINE TO SKIP MATH FOR CALCULATING UNLOAD
    :  POSITION. ;
  85:  PR[34,3:APP Z TOOL OFF]=75    ;
  86:L PR[90:MASTER UNLD MAIN] 500mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  87:  R[40:RECOVER START PR]=86    ;
  88:  PAUSE ;
  89:  --eg:TOUCHUP POINT BELOW, GRIPPER SHOULD BE CENTERED ON THE PART WITH NO
    :  MOVEMENT WHEN OPENING AND CLOSING THE GRIPPER. ;
  90:L PR[90:MASTER UNLD MAIN] 60mm/sec FINE    ;
  91:  --eg:^^^^TOUCHUP HERE^^^^ ;
  92:  PAUSE ;
  93:  !SET MIN X VALUE FOR MAIN SPIN ;
  94:  R[282:MIN X MAIN]=PR[89,1:MASTER LOAD MAIN]-R[285:PART\JAW LENGTH]    ;
  95:L PR[90:MASTER UNLD MAIN] 500mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  96:  PAUSE ;
  97:  R[40:RECOVER START PR]=81    ;
  98:L PR[81:MACH APPR] 2000mm/sec FINE Offset,PR[12:JOINT OFFSET]    ;
  99:  R[40:RECOVER START PR]=80    ;
 100:  $SCR.$ORG_PTH_RSM=1 ;
 101:L PR[80:MACHINE PERCH] 2000mm/sec CNT60 Offset,PR[12:JOINT OFFSET]    ;
 102:  --eg:Logic below will check for SUB SPINDLE, if an open or closed signal
    :  is received the robot will have you teavht he subspindle centerline. If
    :  NO SUBS PINDLE is present the program will jump to the end. ;
 103:  IF (DO[194:SUB CLAMPED COM]=ON OR DO[195:SUB UNCLAMPED COM]=ON) THEN ;
 104:  JMP LBL[300] ;
 105:  ELSE ;
 106:  JMP LBL[9999] ;
 107:  ENDIF ;
 108:  --eg:SUB SPINDLE SETUP ;
 109:  --eg:LOAD SUB SPINDLE CENTERLINE ;
 110:  LBL[300:LOAD SUB] ;
 111:  UFRAME_NUM=5 ;
 112:  UTOOL_NUM=1 ;
 113:  PR[12:JOINT OFFSET]=PR[12:JOINT OFFSET]-PR[12:JOINT OFFSET]    ;
 114:  IF (PR[81,6:MACH APPR]<0),PR[12,6:JOINT OFFSET]=(180) ;
 115:  IF (PR[81,6:MACH APPR]>=0),PR[12,6:JOINT OFFSET]=((-180)) ;
 116:J PR[81:MACH APPR] 100% FINE Offset,PR[12:JOINT OFFSET]    ;
 117:  R[40:RECOVER START PR]=81    ;
 118:  PR[34,3:APP Z TOOL OFF]=75    ;
 119:J PR[91:MASTER LOAD SUB] 100% CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 120:  R[40:RECOVER START PR]=87    ;
 121:  PAUSE ;
 122:  --eg:TOUCHUP POINT BELOW, GRIPPER SHOULD BE CENTERED ON THE PART WITH NO
    :  MOVEMENT WHEN OPENING AND CLOSING THE GRIPPER. ;
 123:L PR[91:MASTER LOAD SUB] 1000mm/sec FINE    ;
 124:  PAUSE ;
 125:  --eg:^^^^TOUCHUP HERE^^^^ ;
 126:  !SET MAX X VALUE FOR SUB SPIN ;
 127:  R[283:MAX X SUB]=PR[91,1:MASTER LOAD SUB]+R[285:PART\JAW LENGTH]    ;
 128:L PR[91:MASTER LOAD SUB] 2000mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 129:  $SCR.$ORG_PTH_RSM=0 ;
 130:  $JCR_GRP[1].$JOG_COORD=3 ;
 131:  PAUSE ;
 132:  !ENSURE S/TOOL JOG MODE IS ACTIV ;
 133:  !JOG ROBOT IN +Z DIRECTION ;
 134:  !MOVE ROBOT AWAY FROM CHUCK ;
 135:  !STOP WHEN ROBOT IS AT THE ;
 136:  !FARTHEST POINT FROM CHUCK BUT ;
 137:  !STILL HAS CLEARANCE INSIDE OF ;
 138:  !MACHINE. ;
 139:  PR[47:LPOS 1]=LPOS    ;
 140:  R[284:MIN X SUB]=PR[47,1:LPOS 1]    ;
 141:  R[40:RECOVER START PR]=81    ;
 142:J PR[81:MACH APPR] 100% FINE Offset,PR[12:JOINT OFFSET]    ;
 143:  $SCR.$ORG_PTH_RSM=1 ;
 144:  JMP LBL[400] ;
 145:  --eg:UNLOAD SUB SPINDLE CENTERLINE ;
 146:  LBL[400:UNLOAD SUB] ;
 147:J PR[81:MACH APPR] 100% CNT10    ;
 148:  R[40:RECOVER START PR]=81    ;
 149:  UFRAME_NUM=5 ;
 150:  UTOOL_NUM=2 ;
 151:  --eg:ROBOT WILL DO CALCULATION TO GET CLOSE TO THE LOAD POSITION, EACH
    :  TIME THE PROGRAM IS RAN THE TAUGHT UNLOAD WILL BE OVERWROTE UNLESS
    :  LINES BELOW ARE SKIPPED. ;
 152:  PR[92:MASTER UNLD SUB]=PR[91:MASTER LOAD SUB]    ;
 153:  --eg:MOVE CURSOUR TO THIS LINE TO SKIP MATH FOR CALCULATING UNLOAD
    :  POSITION. ;
 154:  PR[34,3:APP Z TOOL OFF]=75    ;
 155:L PR[92:MASTER UNLD SUB] 640mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 156:  R[40:RECOVER START PR]=88    ;
 157:  PAUSE ;
 158:  --eg:TOUCHUP POINT BELOW, GRIPPER SHOULD BE CENTERED ON THE PART WITH NO
    :  MOVEMENT WHEN OPENING AND CLOSING THE GRIPPER. ;
 159:L PR[92:MASTER UNLD SUB] 75mm/sec FINE    ;
 160:  PAUSE ;
 161:  --eg:^^^^TOUCHUP HERE^^^^ ;
 162:  !SET MAX X VALUE FOR SUB SPIN ;
 163:  R[283:MAX X SUB]=PR[91,1:MASTER LOAD SUB]+R[285:PART\JAW LENGTH]    ;
 164:  PR[34,3:APP Z TOOL OFF]=75    ;
 165:L PR[92:MASTER UNLD SUB] 500mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 166:J PR[81:MACH APPR] 100% CNT10    ;
 167:  R[40:RECOVER START PR]=81    ;
 168:J PR[80:MACHINE PERCH] 100% FINE    ;
 169:  R[40:RECOVER START PR]=80    ;
 170:  JMP LBL[9999] ;
 171:  LBL[9999:END] ;
 172:  R[40:RECOVER START PR]=23    ;
 173:J PR[23:MOVE TO MACH] 100% CNT20    ;
 174:  R[40:RECOVER START PR]=1    ;
 175:J PR[1:HOME POSITION] 100% FINE    ;
/POS
/END
