/PROG  TABLE_PICK
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "PICK RAW PART";
PROG_SIZE	= 2873;
CREATE		= DATE 23-11-18  TIME 07:37:06;
MODIFIED	= DATE 23-11-18  TIME 07:37:06;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 122;
MEMORY_SIZE	= 3345;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  --eg:PICK RAW PART FROM NEXT AVAILABLE SLOT ;
   2:  LBL[1:START] ;
   3:  IF DO[42:TABLE READY FOR SERVICE]=OFF,JMP LBL[9980] ;
   4:  IF DO[176:ALL TOOLS UNGRIPPED]=OFF,JMP LBL[9981] ;
   5:  IF DO[50:PALLET SETUP COMPLETE]=OFF,JMP LBL[9982] ;
   6:  IF R[67:MEAS PART LENGTH]=0,JMP LBL[9983] ;
   7:  CALL CALC_POUNCE_R    ;
   8:J PR[10:TABLE PK POUNCE] 100% CNT20 Offset,PR[31:APP Z OFFS]    ;
   9:  R[40:RECOVER START PR]=10    ;
  10:  CALL GRIP_CHECK    ;
  11:  --eg:SET R150 EQUAL TO THE POSITION REGISTER USED FOR THE ACTIVE TEMPLATE
    :  TYPE  ;
  12:  IF (R[1:ACTIVE TEMPLATE]=2),R[150:TABLE PICK PR]=(50) ;
  13:  IF (R[1:ACTIVE TEMPLATE]=3),R[150:TABLE PICK PR]=(51) ;
  14:  IF (R[1:ACTIVE TEMPLATE]=4),R[150:TABLE PICK PR]=(52) ;
  15:  IF (R[1:ACTIVE TEMPLATE]=5),R[150:TABLE PICK PR]=(53) ;
  16:  IF (R[1:ACTIVE TEMPLATE]=6),R[150:TABLE PICK PR]=(54) ;
  17:  JMP LBL[10] ;
  18:   ;
  19:  LBL[10:CALCULATE PICK] ;
  20:  IF (DO[31:CYCLE STOP ACTIVE]),JMP LBL[9999] ;
  21:  PR[38:TABLE PCK OFFSET]=PR[30:CARTESIAN ZERO]    ;
  22:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  23:  --eg:WHEN PICKING THE FIRST PART FROM THE TABLE CALL THIS PROGRAM TO
    :  DETERMINE STARTING LOCATION BASED ON TEMPLATE TYPE ;
  24:  IF (R[3:CUR COL PICK (Y)]=1),CALL FIRST_PICK_SLOT ;
  25:  --eg:CALCULATE XYZ OF THE CURRENT SLOT ;
  26:  CALL CALC_PICK_OFFSET    ;
  27:  --eg:CALCULATE XYZ OF THE CURRENT SLOT ;
  28:  CALL CALC_PICK_R    ;
  29:   ;
  30:  LBL[100:PICK TABLE] ;
  31:  IF (DI[1:TABLE AT A POSITION]=ON) THEN ;
  32:  UFRAME_NUM=1 ;
  33:  ELSE ;
  34:  UFRAME_NUM=2 ;
  35:  ENDIF ;
  36:  UTOOL_NUM=1 ;
  37:  PR[56:TEMP POSITION]=PR[R[150]]    ;
  38:  PR[56,1:TEMP POSITION]=PR[56,1:TEMP POSITION]+PR[38,1:TABLE PCK OFFSET]    ;
  39:  PR[56,2:TEMP POSITION]=PR[56,2:TEMP POSITION]+PR[38,2:TABLE PCK OFFSET]    ;
  40:  PR[56,3:TEMP POSITION]=PR[56,3:TEMP POSITION]+PR[38,3:TABLE PCK OFFSET]+50    ;
  41:  PR[56,6:TEMP POSITION]=PR[56,6:TEMP POSITION]+PR[38,6:TABLE PCK OFFSET]    ;
  42:  CALL CHK_CFG((-1),56,0,54) ;
  43:  IF (R[54:CONFIG STATUS]<>0) THEN ;
  44:  JMP LBL[1] ;
  45:  ENDIF ;
  46:J PR[56:TEMP POSITION] 100% FINE    ;
  47:  R[40:RECOVER START PR]=R[150:TABLE PICK PR]    ;
  48:  PR[56,3:TEMP POSITION]=PR[56,3:TEMP POSITION]-35    ;
  49:  CALL CHK_CFG((-1),56,0,54) ;
  50:L PR[56:TEMP POSITION] 2000mm/sec CNT25    ;
  51:  PR[56,3:TEMP POSITION]=PR[56,3:TEMP POSITION]-15    ;
  52:  CALL CHK_CFG((-1),56,0,54) ;
  53:  SKIP CONDITION RI[7:SPRING COMPRESSED]=ON    ;
  54:L PR[56:TEMP POSITION] 15mm/sec FINE Skip,LBL[105]    ;
  55:  LBL[105:GRIP] ;
  56:  DO[36:PART PRESENT CHECK ONLY]=(ON) ;
  57:  CALL T1_GRIP    ;
  58:  DO[130:T1 REGRIP]=ON ;
  59:  R[55:T1 X DROP]=R[2:CUR ROW PICK (X)]    ;
  60:  R[56:T1 Y DROP]=R[3:CUR COL PICK (Y)]    ;
  61:  PR[56,3:TEMP POSITION]=PR[56,3:TEMP POSITION]+10    ;
  62:  PR[56,1:TEMP POSITION]=PR[56,1:TEMP POSITION]+2    ;
  63:  CALL CHK_CFG((-1),56,0,54) ;
  64:L PR[56:TEMP POSITION] 1000mm/sec FINE ACC50    ;
  65:  IF (DO[174:PART IN T1]=ON) THEN ;
  66:  DO[46:RAW PART PICKED]=PULSE,1.0sec ;
  67:  PR[56,1:TEMP POSITION]=PR[38,1:TABLE PCK OFFSET]+R[80:RAW CENTER X OFF]    ;
  68:  CALL CHK_CFG((-1),56,0,54) ;
  69:  --eg:MOVE PART TO CENTER OF SLOT BEFORE RETRACTING ;
  70:L PR[56:TEMP POSITION] 500mm/sec FINE ACC50    ;
  71:  PR[56,3:TEMP POSITION]=R[67:MEAS PART LENGTH]+R[67:MEAS PART LENGTH]+50    ;
  72:  DO[114:COLLISION AT DROP]=OFF ;
  73:  CALL CHK_CFG((-1),56,0,54) ;
  74:L PR[56:TEMP POSITION] 1000mm/sec CNT20    ;
  75:  CALL CALC_POUNCE_R    ;
  76:J PR[10:TABLE PK POUNCE] 100% FINE Offset,PR[31:APP Z OFFS]    ;
  77:  R[40:RECOVER START PR]=10    ;
  78:  ELSE ;
  79:  DO[48:PART SLOT EMPTY]=PULSE,1.0sec ;
  80:  DO[46:RAW PART PICKED]=PULSE,1.0sec ;
  81:  PR[56,3:TEMP POSITION]=R[67:MEAS PART LENGTH]+100    ;
  82:  CALL CHK_CFG((-1),56,0,54) ;
  83:L PR[56:TEMP POSITION] 500mm/sec FINE ACC50    ;
  84:  ENDIF ;
  85:  CALL INC_PICK    ;
  86:  R[46:COLLISION SENSE]=100    ;
  87:J PR[10:TABLE PK POUNCE] 100% CNT50 Offset,PR[31:APP Z OFFS]    ;
  88:  IF (DO[118:T2 OD GRIP]=ON) THEN ;
  89:  RO[4:T2 CLOSE]=OFF ;
  90:  RO[3:T2 OPEN]=ON ;
  91:  ENDIF ;
  92:  IF (!DO[174:PART IN T1] AND !DO[43:TABLE PICK COMPLETE]),JMP LBL[10] ;
  93:  JMP LBL[9999] ;
  94:   ;
  95:  LBL[9980:FAULT] ;
  96:  CALL TABLE_MSG(2) ;
  97:  UALM[11] ;
  98:  JMP LBL[1] ;
  99:   ;
 100:  LBL[9981:FAULT] ;
 101:  CALL TABLE_MSG(5) ;
 102:  UALM[11] ;
 103:  JMP LBL[1] ;
 104:   ;
 105:  LBL[9982:FAULT] ;
 106:  CALL TABLE_MSG(4) ;
 107:  UALM[11] ;
 108:  JMP LBL[1] ;
 109:   ;
 110:  LBL[9983:FAULT] ;
 111:  CALL TABLE_MSG(9) ;
 112:  UALM[11] ;
 113:  JMP LBL[1] ;
 114:   ;
 115:  LBL[9999:END] ;
 116:  R[40:RECOVER START PR]=10    ;
 117:J PR[10:TABLE PK POUNCE] 100% CNT30    ;
 118:  R[40:RECOVER START PR]=1    ;
 119:   ;
 120:  IF (!DO[130:T1 REGRIP]) THEN ;
 121:J PR[1:HOME POSITION] 100% CNT5    ;
 122:  ENDIF ;
/POS
/END
