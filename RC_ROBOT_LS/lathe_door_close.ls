/PROG  LATHE_DOOR_CLOSE
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "DOOR CLOSE";
PROG_SIZE	= 1807;
CREATE		= DATE 23-01-06  TIME 11:28:32;
MODIFIED	= DATE 23-11-22  TIME 04:05:44;
FILE_NAME	= OKUMA_DO;
VERSION		= 0;
LINE_COUNT	= 77;
MEMORY_SIZE	= 2307;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  LBL[1:START] ;
   2:  $WAITTMOUT=600 ;
   3:  R[43:ACTION RETRIES]=0    ;
   4:   ;
   5:  LBL[100:CHOOSE CYCLE] ;
   6:  IF (DO[87:AUTODOORS ENABLED]=ON OR DO[88:SERVO DOOR ENABLED]=ON),JMP LBL[200] ;
   7:  IF (DO[89:MANUAL DOOR]=ON),JMP LBL[400] ;
   8:   ;
   9:   ;
  10:  LBL[180:FAULT] ;
  11:  CALL SETUP_MSG(2) ;
  12:  UALM[13] ;
  13:  JMP LBL[100] ;
  14:   ;
  15:  LBL[200:AUTO DOOR] ;
  16:  IF (DO[58:HAAS LATHE]=ON),JMP LBL[9999] ;
  17:  R[43:ACTION RETRIES]=R[43:ACTION RETRIES]+1    ;
  18:  IF R[43:ACTION RETRIES]>=3,JMP LBL[280] ;
  19:  DO[184:CLOSE DOOR COM]=PULSE,1.0sec ;
  20:  WAIT (DO[197:DOOR CLOSED 1 COM]=ON) TIMEOUT,LBL[200] ;
  21:  JMP LBL[9999] ;
  22:   ;
  23:  LBL[280:FAULT] ;
  24:  CALL MACH_MSG(17) ;
  25:  UALM[5] ;
  26:  JMP LBL[100] ;
  27:   ;
  28:  LBL[400:MANUAL DOOR] ;
  29:  $WAITTMOUT=100 ;
  30:  UFRAME_NUM=5 ;
  31:  UTOOL_NUM=4 ;
  32:J PR[80:MACHINE PERCH] 100% CNT10    ;
  33:  R[40:RECOVER START PR]=80    ;
  34:  --eg:TOUCHUP HERE FOR DOOR OPEN AND CLOSE PERCH, THIS SHOULD PREP THE
    :  MOTION TO GO TO THE DOOR OPEN/CLOSE POSITIONS ;
  35:J PR[24:DOOR OP/CL PERCH] 100% CNT20    ;
  36:  --eg:^^TOUCHUP HERE^^ ;
  37:  R[40:RECOVER START PR]=24    ;
  38:  WAIT DO[196:DOOR OPEN 1 COM]=ON TIMEOUT,LBL[480] ;
  39:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  40:  PR[34,2:APP Z TOOL OFF]=(-100)    ;
  41:L PR[146:DOOR OPEN POS] 500mm/sec FINE Offset,PR[34:APP Z TOOL OFF]    ;
  42:  --eg:TOUCH UP HERE FOR DOOR OPEN/CLOSE MOVEMENT ;
  43:  R[40:RECOVER START PR]=146    ;
  44:L PR[146:DOOR OPEN POS] 500mm/sec FINE    ;
  45:  --eg:^^^^TOUCH UP HERE^^^^ ;
  46:  R[40:RECOVER START PR]=146    ;
  47:   ;
  48:  --eg:TOUCH UP HERE FOR DOOR OPEN/CLOSE MOVEMENT ;
  49:L PR[145:DOOR OP/CL MID] 500mm/sec FINE ACC50    ;
  50:  --eg:^^^^TOUCH UP HERE^^^^ ;
  51:  R[40:RECOVER START PR]=145    ;
  52:   ;
  53:  --eg:TOUCH UP HERE FOR DOOR OPEN/CLOSE MOVEMENT ;
  54:L PR[147:DOOR CLOSED POS] 500mm/sec FINE ACC50    ;
  55:  --eg:^^^^TOUCH UP HERE^^^^ ;
  56:  R[40:RECOVER START PR]=147    ;
  57:  WAIT DO[197:DOOR CLOSED 1 COM]=ON TIMEOUT,LBL[482] ;
  58:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  59:  PR[34,2:APP Z TOOL OFF]=(-100)    ;
  60:L PR[147:DOOR CLOSED POS] 500mm/sec FINE Offset,PR[34:APP Z TOOL OFF]    ;
  61:  R[40:RECOVER START PR]=24    ;
  62:L PR[24:DOOR OP/CL PERCH] 2000mm/sec CNT50    ;
  63:  R[40:RECOVER START PR]=80    ;
  64:J PR[80:MACHINE PERCH] 100% CNT20    ;
  65:  JMP LBL[9999] ;
  66:   ;
  67:  LBL[480:FAULT] ;
  68:  CALL MACH_MSG(14) ;
  69:  UALM[5] ;
  70:  JMP LBL[100] ;
  71:   ;
  72:  LBL[581:FAULT] ;
  73:  CALL MACH_MSG(17) ;
  74:  UALM[5] ;
  75:  JMP LBL[100] ;
  76:   ;
  77:  LBL[9999:END] ;
/POS
/END
