/PROG  LATHE_SERVICE
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "STANDARD SERVICE";
PROG_SIZE	= 3695;
CREATE		= DATE 23-11-21  TIME 03:20:36;
MODIFIED	= DATE 23-11-22  TIME 04:08:06;
FILE_NAME	= OKUMA_LA;
VERSION		= 0;
LINE_COUNT	= 154;
MEMORY_SIZE	= 4191;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !PROGRAM TO SERVICE LATHE ;
   2:  LBL[1:START] ;
   3:  UFRAME_NUM=5 ;
   4:J PR[23:MOVE TO MACH] 100% CNT100    ;
   5:  R[40:RECOVER START PR]=23    ;
   6:  LBL[10:STATUS CHECK] ;
   7:J PR[80:MACHINE PERCH] 100% CNT10    ;
   8:  R[40:RECOVER START PR]=80    ;
   9:  !CHECK PROGRAM IS SET ;
  10:  IF ((R[410:CUT PRGM]=0 AND DO[58:HAAS LATHE]=ON) OR (R[411:WASH PRGM]=0 AND DO[221:RUN WASH COM]=ON AND DO[58:HAAS LATHE]=ON)) THEN ;
  11:  CALL MACH_MSG(22) ;
  12:  UALM[5] ;
  13:  JMP LBL[10] ;
  14:  ENDIF ;
  15:  !CHECK MACHINE IS READY STATE ;
  16:  !SYSTEM LINK IS ON ;
  17:  IF (DO[203:SYSTEM LINK COM]=OFF) THEN ;
  18:  IF (DO[145:AT HOME]=OFF) THEN ;
  19:  R[40:RECOVER START PR]=23    ;
  20:J PR[23:MOVE TO MACH] 100% CNT60    ;
  21:  R[40:RECOVER START PR]=1    ;
  22:J PR[1:HOME POSITION] 100% FINE    ;
  23:  ENDIF ;
  24:  CALL MACH_MSG(8) ;
  25:  UALM[5] ;
  26:  JMP LBL[10] ;
  27:  ENDIF ;
  28:  !CHECK MEASURE NOT ACTIVE ;
  29:  IF (DO[105:MEAS LOAD ACTIVE]=ON OR DO[106:MEAS UNLOAD ACTIVE]=ON) THEN ;
  30:  CALL MACH_MSG(10) ;
  31:  UALM[5] ;
  32:  JMP LBL[9999] ;
  33:  ENDIF ;
  34:  !CHECK MACHINE ALARM CLEAR ;
  35:  IF (DO[218:NC ALARM COM]=OFF) THEN ;
  36:  CALL MACH_MSG(4) ;
  37:  UALM[5] ;
  38:  JMP LBL[10] ;
  39:  ENDIF ;
  40:  !CHECK DIAMETER SET ;
  41:  IF (DI[50:MEAS FINISHED DIA]=OFF AND R[804:FINISHED DIA.]=0) THEN ;
  42:  CALL MACH_MSG(11) ;
  43:  UALM[5] ;
  44:  JMP LBL[1] ;
  45:  ENDIF ;
  46:  !CHECK LENGTH SET ;
  47:  IF (DI[51:MEAS FINISHED LENGTH]=OFF AND R[806:FINISHED LENGTH]=0) THEN ;
  48:  CALL MACH_MSG(12) ;
  49:  UALM[5] ;
  50:  JMP LBL[1] ;
  51:  ENDIF ;
  52:  JMP LBL[100] ;
  53:  !MAIN LOOP ;
  54:  LBL[100:CHOOSE OPERATION] ;
  55:  --eg:WAIT UNTIL MACHINE IS READY FOR SERVICE ;
  56:  DO[224:ROBOT WAITING]=ON ;
  57:  WAIT (DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON)    ;
  58:  --eg:OPEN DOOR IF NOT IN CNC PROGAM ;
  59:  IF (DO[196:DOOR OPEN 1 COM]=OFF AND DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON) THEN ;
  60:  CALL LATHE_DOOR_OPEN    ;
  61:  ENDIF ;
  62:  WAIT (DO[209:MACH READY FOR SERVICE]=ON AND DO[210:MACH SAFE FOR ROBOT]=ON)    ;
  63:  DO[224:ROBOT WAITING]=OFF ;
  64:  CALL MESSAGE19    ;
  65:  CALL MESSAGE20    ;
  66:  DO[217:ROBOT RETRACTED COM]=OFF ;
  67:  MONITOR MON_LATHE ;
  68:  !UNLOAD MAIN SPINDLE ;
  69:  IF (DO[173:T2 UNGRIPPED]=ON AND DO[66:UNLOAD MAINSPINDLE ON]=ON AND DO[192:MAIN CLAMPED COM]=ON AND DO[98:DEMO MODE]=OFF) THEN ;
  70:  CALL LATHE_UNLOAD_MAIN    ;
  71:  ENDIF ;
  72:  !UNLOAD SUB SPINDLE ;
  73:  IF (DO[173:T2 UNGRIPPED]=ON AND DO[67:UNLOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=ON AND DO[98:DEMO MODE]=OFF) THEN ;
  74:  CALL LATHE_UNLOAD_SUB    ;
  75:  ENDIF ;
  76:  --eg:CYCLE START WASH PROGRAM IF PART IN LOAD SPINDLE ;
  77:  IF (DO[58:HAAS LATHE]=ON AND DO[221:RUN WASH COM]=ON) THEN ;
  78:  IF (DO[211:MACHINE EMPTY]=ON) THEN ;
  79:  IF (DO[89:MANUAL DOOR]=ON) THEN ;
  80:  CALL LATHE_DOOR_CLOSE    ;
  81:  ENDIF ;
  82:  DO[222:CALL WASH COM]=ON ;
  83:  DO[223:CALL CUT COM]=OFF ;
  84:  WAIT R[300:PRGM STATUS]=2    ;
  85:  DO[217:ROBOT RETRACTED COM]=ON ;
  86:  MONITOR END MON_LATHE ;
  87:  CALL LATHE_START    ;
  88:  WAIT DO[209:MACH READY FOR SERVICE]=OFF    ;
  89:  DO[222:CALL WASH COM]=OFF ;
  90:  R[300:PRGM STATUS]=0    ;
  91:  ENDIF ;
  92:  --eg:WAIT UNTIL MACHINE IS READY FOR SERVICE ;
  93:  DO[224:ROBOT WAITING]=ON ;
  94:  WAIT (DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON)    ;
  95:  --eg:OPEN DOOR IF NOT IN CNC PROGAM ;
  96:  IF (DO[196:DOOR OPEN 1 COM]=OFF AND DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON) THEN ;
  97:  CALL LATHE_DOOR_OPEN    ;
  98:  ENDIF ;
  99:  WAIT (DO[209:MACH READY FOR SERVICE]=ON AND DO[210:MACH SAFE FOR ROBOT]=ON)    ;
 100:  DO[224:ROBOT WAITING]=OFF ;
 101:  CALL MESSAGE19    ;
 102:  CALL MESSAGE20    ;
 103:  DO[217:ROBOT RETRACTED COM]=OFF ;
 104:  MONITOR MON_LATHE ;
 105:  ENDIF ;
 106:  !LOAD MAIN SPINDLE ;
 107:  IF (DO[129:T1 MACHINE]=ON AND DO[61:LOAD MAINSPINDLE ON]=ON AND DO[193:MAIN UNCLAMPED COM]=ON AND DO[98:DEMO MODE]=OFF) THEN ;
 108:  CALL LATHE_LOAD_MAIN    ;
 109:  ENDIF ;
 110:  !LOAD SUB SPINDLE ;
 111:  IF (DO[129:T1 MACHINE]=ON AND DO[62:LOAD SUBSPINDLE ON]=ON AND DO[195:SUB UNCLAMPED COM]=ON AND DO[98:DEMO MODE]=OFF) THEN ;
 112:  CALL LATHE_LOAD_SUB    ;
 113:  ENDIF ;
 114:  !DEMO MODE ;
 115:  IF (DO[98:DEMO MODE]=ON) THEN ;
 116:  CALL LATHE_DEMOMODE    ;
 117:  JMP LBL[9999] ;
 118:  ENDIF ;
 119:  DO[217:ROBOT RETRACTED COM]=ON ;
 120:  MONITOR END MON_LATHE ;
 121:  R[40:RECOVER START PR]=80    ;
 122:J PR[80:MACHINE PERCH] 100% FINE    ;
 123:  --eg:CYCLE START CUTTING PROGRAM IF PART IN LOAD SPINDLE ;
 124:  IF ((DO[61:LOAD MAINSPINDLE ON]=ON AND DO[192:MAIN CLAMPED COM]=ON) OR (DO[62:LOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=ON) OR (DO[63:NO LOAD ON]=ON)) THEN ;
 125:  IF (DO[89:MANUAL DOOR]=ON) THEN ;
 126:  CALL LATHE_DOOR_CLOSE    ;
 127:  ENDIF ;
 128:  IF (DO[58:HAAS LATHE]=ON) THEN ;
 129:  DO[223:CALL CUT COM]=ON ;
 130:  DO[222:CALL WASH COM]=OFF ;
 131:  WAIT R[300:PRGM STATUS]=1    ;
 132:  ENDIF ;
 133:  IF (DO[91:TEST RIG ENABLED]=OFF) THEN ;
 134:  CALL LATHE_START    ;
 135:  WAIT DO[209:MACH READY FOR SERVICE]=OFF    ;
 136:  DO[223:CALL CUT COM]=OFF ;
 137:  R[64:UNLOAD X DROP]=R[57:LOAD X DROP]    ;
 138:  R[65:UNLOAD Y DROP]=R[58:LOAD Y DROP]    ;
 139:  R[57:LOAD X DROP]=0    ;
 140:  R[58:LOAD Y DROP]=0    ;
 141:  ENDIF ;
 142:  ENDIF ;
 143:  --eg:EXIT PROGRAM IF FINISHED PART IN T2 AND T1 EMPTY OR IF BOTH GRIPS
    :  EMPTY ;
 144:  IF ((DO[137:T2 FINISHED PART]=ON AND DO[129:T1 MACHINE]=OFF) OR (DO[176:ALL TOOLS UNGRIPPED]=ON)),JMP LBL[9999] ;
 145:  WAIT    .10(sec) ;
 146:  JMP LBL[100] ;
 147:  !PROGRAM END ;
 148:  LBL[9999:END] ;
 149:  IF (DO[145:AT HOME]=OFF) THEN ;
 150:  R[40:RECOVER START PR]=23    ;
 151:J PR[23:MOVE TO MACH] 100% CNT100    ;
 152:  ENDIF ;
 153:  R[40:RECOVER START PR]=1    ;
 154:J PR[1:HOME POSITION] 100% CNT5    ;
/POS
/END
