/PROG  MEASURE_MAIN_LOAD
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 1175;
CREATE		= DATE 23-11-18  TIME 09:22:32;
MODIFIED	= DATE 23-11-28  TIME 17:36:16;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 49;
MEMORY_SIZE	= 1683;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  DO[217:ROBOT RETRACTED COM]=OFF ;
   2:  MONITOR MON_LATHE ;
   3:  CALL FAULT_MESSAGE('Robot is measuring raw part','X location in machine.') ;
   4:  $WAITTMOUT=500 ;
   5:  UFRAME_NUM=5 ;
   6:  UTOOL_NUM=1 ;
   7:L PR[81:MACH APPR] 2000mm/sec CNT50    ;
   8:  R[40:RECOVER START PR]=81    ;
   9:  LBL[301:SEARCH] ;
  10:  --eg:SET SEARCH START AND STOP POSITIONS ;
  11:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  12:  PR[31:APP Z OFFS]=PR[30:CARTESIAN ZERO]    ;
  13:  PR[93:MAIN SRCH START]=PR[89:MASTER LOAD MAIN]    ;
  14:  PR[94:MAIN SRCH END]=PR[89:MASTER LOAD MAIN]    ;
  15:  R[280:START SEARCH X]=R[281:MAX X MAIN]-PR[89,1:MASTER LOAD MAIN]    ;
  16:  R[286:END SEARCH X]=R[285:PART\JAW LENGTH]*(-1)    ;
  17:  PR[34,3:APP Z TOOL OFF]=R[280:START SEARCH X]    ;
  18:L PR[93:MAIN SRCH START] 2000mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  19:  R[40:RECOVER START PR]=93    ;
  20:  IF (DO[98:DEMO MODE]=ON) THEN ;
  21:  PR[85:LOAD MAIN]=LPOS    ;
  22:  R[68:MAIN SPIN RAW X]=(PR[85,1:LOAD MAIN]) ;
  23:  DO[107:MEAS LOAD COMP]=ON ;
  24:  JMP LBL[2] ;
  25:  ENDIF ;
  26:  SKIP CONDITION RI[7:SPRING COMPRESSED]=ON+    ;
  27:  PR[34,3:APP Z TOOL OFF]=R[286:END SEARCH X]    ;
  28:L PR[94:MAIN SRCH END] R[86:SEARCH FAST SPD]mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF] Skip,LBL[380],PR[85:LOAD MAIN]=LPOS    ;
  29:  R[40:RECOVER START PR]=94    ;
  30:  --eg:BACK OFF TO PREVENT COLLISION. ;
  31:  PR[34,3:APP Z TOOL OFF]=1    ;
  32:L PR[85:LOAD MAIN] 100mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  33:  WAIT    .10(sec) ;
  34:  PR[85:LOAD MAIN]=LPOS    ;
  35:  R[68:MAIN SPIN RAW X]=(PR[85,1:LOAD MAIN]) ;
  36:  DO[107:MEAS LOAD COMP]=ON ;
  37:  PR[34,3:APP Z TOOL OFF]=75    ;
  38:L PR[85:LOAD MAIN] 2000mm/sec CNT30 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  39:  LBL[2:DEMO CONT] ;
  40:  R[40:RECOVER START PR]=81    ;
  41:J PR[81:MACH APPR] 100% CNT40    ;
  42:  JMP LBL[9999] ;
  43:  LBL[380:SKIP FAULT] ;
  44:  CALL MACH_MSG(20) ;
  45:  UALM[6] ;
  46:  JMP LBL[301] ;
  47:  LBL[9999] ;
  48:  CALL MESSAGE19    ;
  49:  CALL MESSAGE20    ;
/POS
/END
