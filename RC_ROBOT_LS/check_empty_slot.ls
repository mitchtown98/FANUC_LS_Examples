/PROG  CHECK_EMPTY_SLOT
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "PICK RAW PART";
PROG_SIZE	= 2821;
CREATE		= DATE 23-04-01  TIME 00:12:32;
MODIFIED	= DATE 23-11-16  TIME 03:45:04;
FILE_NAME	= TABLE_PI;
VERSION		= 0;
LINE_COUNT	= 120;
MEMORY_SIZE	= 3301;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  --eg:PICK RAW PART FROM NEXT AVAILABLE SLOT ;
   2:   ;
   3:  LBL[1:START] ;
   4:  IF DO[42:TABLE READY FOR SERVICE]=OFF,JMP LBL[9980] ;
   5:  IF DO[176:ALL TOOLS UNGRIPPED]=OFF,JMP LBL[9981] ;
   6:  IF DO[50:PALLET SETUP COMPLETE]=OFF,JMP LBL[9982] ;
   7:  IF R[67:MEAS PART LENGTH]=0,JMP LBL[9983] ;
   8:   ;
   9:J PR[10:TABLE PK POUNCE] 100% CNT20    ;
  10:  R[40:RECOVER START PR]=10    ;
  11:  CALL GRIP_CHECK    ;
  12:  --eg:SET R150 EQUAL TO THE POSITION REGISTER USED FOR THE ACTIVE TEMPLATE
    :  TYPE  ;
  13:  IF (R[1:ACTIVE TEMPLATE]=2),R[150:TABLE PICK PR]=(50) ;
  14:  IF (R[1:ACTIVE TEMPLATE]=3),R[150:TABLE PICK PR]=(51) ;
  15:  IF (R[1:ACTIVE TEMPLATE]=4),R[150:TABLE PICK PR]=(52) ;
  16:  IF (R[1:ACTIVE TEMPLATE]=5),R[150:TABLE PICK PR]=(53) ;
  17:  IF (R[1:ACTIVE TEMPLATE]=6),R[150:TABLE PICK PR]=(54) ;
  18:  JMP LBL[10] ;
  19:   ;
  20:  LBL[10:CALCULATE PICK] ;
  21:  IF (DO[31:CYCLE STOP ACTIVE]),JMP LBL[9999] ;
  22:  --eg:WHEN PICKING THE FIRST PART FROM THE TABLE CALL THIS PROGRAM TO
    :  DETERMINE STARTING LOCATION BASED ON TEMPLATE TYPE ;
  23:  IF (R[3:CUR COL PICK (Y)]=1),CALL FIRST_PICK_SLOT ;
  24:  --eg:CALCULATE XYZ OF THE CURRENT SLOT ;
  25:  CALL CALC_PICK_OFFSET    ;
  26:  --eg:CALCULATE XYZ OF THE CURRENT SLOT ;
  27:  CALL CALC_PICK_R    ;
  28:   ;
  29:  LBL[100:PICK TABLE] ;
  30:  IF (DI[1:TABLE AT A POSITION]=ON) THEN ;
  31:  UFRAME_NUM=1 ;
  32:  ELSE ;
  33:  UFRAME_NUM=2 ;
  34:  ENDIF ;
  35:  UTOOL_NUM=1 ;
  36:  PR[34,3:APP Z TOOL OFF]=75    ;
  37:  IF (R[6:CUR PICK SLOT #]=184),PR[34,3:APP Z TOOL OFF]=(30) ;
  38:L PR[R[150]] 2000mm/sec CNT10 Offset,PR[38:TABLE PCK OFFSET] Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  39:  R[46:COLLISION SENSE]=150    ;
  40:  R[40:RECOVER START PR]=R[150:TABLE PICK PR]    ;
  41:  --eg:TOUCH UP HERE AND SELECT YES TO OFFSET PR DATA ;
  42:  !PART DIAMETER AND HEIGHT MUST    ;
  43:  !MUST BE SET CORRECTLY          ;
  44:L PR[R[150]] 200mm/sec FINE Offset,PR[38:TABLE PCK OFFSET]    ;
  45:  LBL[105:GRIP] ;
  46:  DO[36:PART PRESENT CHECK ONLY]=(ON) ;
  47:  CALL T1_GRIP    ;
  48:  DO[130:T1 REGRIP]=ON ;
  49:  PR[34,3:APP Z TOOL OFF]=2    ;
  50:  PR[38,1:TABLE PCK OFFSET]=PR[38,1:TABLE PCK OFFSET]+2    ;
  51:L PR[R[150]] 500mm/sec FINE Offset,PR[38:TABLE PCK OFFSET] Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  52:  IF (DO[174:PART IN T1]=ON) THEN ;
  53:  DO[46:RAW PART PICKED]=PULSE,1.0sec ;
  54:  PR[38,1:TABLE PCK OFFSET]=PR[38,1:TABLE PCK OFFSET]+R[80:RAW CENTER X OFF]    ;
  55:   ;
  56:  --eg:MOVE PART TO CENTER OF SLOT BEFORE RETRACTING ;
  57:L PR[R[150]] 200mm/sec FINE Offset,PR[38:TABLE PCK OFFSET] Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  58:  DO[114:COLLISION AT DROP]=OFF ;
  59:  PR[29:POCKET OFFSET]=PR[29:POCKET OFFSET]-PR[29:POCKET OFFSET]    ;
  60:   ;
  61:  PR[34,3:APP Z TOOL OFF]=R[67:MEAS PART LENGTH]+25    ;
  62:   ;
  63:L PR[R[150]] 1000mm/sec CNT50 Offset,PR[38:TABLE PCK OFFSET] Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  64:  ELSE ;
  65:  PR[34,3:APP Z TOOL OFF]=100    ;
  66:  DO[48:PART SLOT EMPTY]=PULSE,1.0sec ;
  67:  DO[46:RAW PART PICKED]=PULSE,1.0sec ;
  68:L PR[R[150]] 2000mm/sec CNT20 Offset,PR[38:TABLE PCK OFFSET] Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  69:  --eg:MOVE TO PERCH POSN ;
  70:  LBL[9950] ;
  71:  IF ((R[1:ACTIVE TEMPLATE]=3 AND R[6:CUR PICK SLOT #]=74) OR (R[1:ACTIVE TEMPLATE]=3 AND R[6:CUR PICK SLOT #]=90) OR (R[1:ACTIVE TEMPLATE]=4 AND R[6:CUR PICK SLOT #]=44)) THEN ;
  72:  R[40:RECOVER START PR]=10    ;
  73:L PR[10:TABLE PK POUNCE] 2000mm/sec CNT30    ;
  74:  ENDIF ;
  75:  ENDIF ;
  76:  IF (DO[130:T1 REGRIP]=OFF) THEN ;
  77:  CALL INC_PICK    ;
  78:  ELSE ;
  79:  DO[128:T1 REJECT]=ON ;
  80:  ENDIF ;
  81:  R[46:COLLISION SENSE]=100    ;
  82:J PR[10:TABLE PK POUNCE] 100% CNT50    ;
  83:  RO[4:T2 CLOSE]=OFF ;
  84:  RO[3:T2 OPEN]=ON ;
  85:  JMP LBL[9999] ;
  86:   ;
  87:  LBL[9980:FAULT] ;
  88:  CALL TABLE_MSG(2) ;
  89:  UALM[11] ;
  90:  JMP LBL[1] ;
  91:   ;
  92:  LBL[9981:FAULT] ;
  93:  CALL TABLE_MSG(5) ;
  94:  UALM[11] ;
  95:  JMP LBL[1] ;
  96:   ;
  97:  LBL[9982:FAULT] ;
  98:  CALL TABLE_MSG(4) ;
  99:  UALM[11] ;
 100:  JMP LBL[1] ;
 101:   ;
 102:  LBL[9983:FAULT] ;
 103:  CALL TABLE_MSG(9) ;
 104:  UALM[11] ;
 105:  JMP LBL[1] ;
 106:   ;
 107:  LBL[9999:END] ;
 108:  R[40:RECOVER START PR]=10    ;
 109:J PR[10:TABLE PK POUNCE] 100% CNT30    ;
 110:  R[40:RECOVER START PR]=1    ;
 111:  IF (DO[128:T1 REJECT]=ON) THEN ;
 112:  CALL PART_REGRIP    ;
 113:  R[40:RECOVER START PR]=1    ;
 114:J PR[1:HOME POSITION] 100% CNT5    ;
 115:  CALL TABLE_MSG(27) ;
 116:  DO[128:T1 REJECT]=OFF ;
 117:  UALM[11] ;
 118:  CALL REGRIP_CHECK    ;
 119:  JMP LBL[1] ;
 120:  ENDIF ;
/POS
/END
