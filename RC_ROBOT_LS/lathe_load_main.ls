/PROG  LATHE_LOAD_MAIN
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 911;
CREATE		= DATE 23-11-21  TIME 02:52:14;
MODIFIED	= DATE 23-11-28  TIME 12:36:46;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 41;
MEMORY_SIZE	= 1271;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:J PR[80:MACHINE PERCH] 100% CNT20    ;
   2:  R[40:RECOVER START PR]=80    ;
   3:L PR[81:MACH APPR] 2000mm/sec CNT20    ;
   4:  R[40:RECOVER START PR]=81    ;
   5:  IF (DO[75:MAINSPINDLE AIR BLOW ON]=ON) THEN ;
   6:  DO[189:MAIN AIRBLOW COM]=PULSE,3.0sec ;
   7:  ENDIF ;
   8:  TIMER[3]=STOP ;
   9:  TIMER[3]=RESET ;
  10:  TIMER[3]=START ;
  11:  UFRAME_NUM=5 ;
  12:  UTOOL_NUM=1 ;
  13:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  14:  PR[31:APP Z OFFS]=PR[30:CARTESIAN ZERO]    ;
  15:  PR[34,3:APP Z TOOL OFF]=R[281:MAX X MAIN]-PR[85,1:LOAD MAIN]    ;
  16:  PR[31,3:APP Z OFFS]=R[808:LOAD OFFSET]    ;
  17:L PR[85:LOAD MAIN] 2000mm/sec CNT5 Tool_Offset,PR[34:APP Z TOOL OFF] Offset,PR[31:APP Z OFFS]    ;
  18:  R[40:RECOVER START PR]=85    ;
  19:  WAIT (DO[189:MAIN AIRBLOW COM]=OFF)    ;
  20:  CALL LATHE_ORIENT1_MAIN    ;
  21:L PR[85:LOAD MAIN] 100mm/sec FINE Offset,PR[31:APP Z OFFS]    ;
  22:  IF (DO[91:TEST RIG ENABLED]=OFF) THEN ;
  23:  CALL T1_UNGRIP    ;
  24:  CALL LATHE_CLAMP_MAIN    ;
  25:  ENDIF ;
  26:  IF (DO[91:TEST RIG ENABLED]=ON) THEN ;
  27:  CALL LATHE_CLAMP_MAIN    ;
  28:  CALL T1_UNGRIP    ;
  29:  ENDIF ;
  30:  R[57:LOAD X DROP]=R[55:T1 X DROP]    ;
  31:  R[58:LOAD Y DROP]=R[56:T1 Y DROP]    ;
  32:  R[55:T1 X DROP]=0    ;
  33:  R[56:T1 Y DROP]=0    ;
  34:  DO[105:MEAS LOAD ACTIVE]=OFF ;
  35:L PR[85:LOAD MAIN] 500mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF] Offset,PR[31:APP Z OFFS]    ;
  36:  R[40:RECOVER START PR]=81    ;
  37:J PR[81:MACH APPR] 100% CNT20    ;
  38:  R[40:RECOVER START PR]=80    ;
  39:L PR[80:MACHINE PERCH] 2000mm/sec CNT20    ;
  40:  TIMER[3]=STOP ;
  41:  R[122:SERVICE MACH TM]=TIMER[3]    ;
/POS
/END
