/PROG  LATHE_DOOR_OPEN
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "DOOR OPEN";
PROG_SIZE	= 1763;
CREATE		= DATE 23-01-06  TIME 11:28:32;
MODIFIED	= DATE 23-11-22  TIME 04:06:26;
FILE_NAME	= OKUMA_DO;
VERSION		= 0;
LINE_COUNT	= 75;
MEMORY_SIZE	= 2271;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  LBL[1:START] ;
   2:  $WAITTMOUT=600 ;
   3:  R[43:ACTION RETRIES]=0    ;
   4:   ;
   5:  LBL[100:CHOOSE CYCLE] ;
   6:  IF (DO[87:AUTODOORS ENABLED]=ON OR DO[88:SERVO DOOR ENABLED]=ON),JMP LBL[200] ;
   7:  IF (DO[89:MANUAL DOOR]=ON),JMP LBL[400] ;
   8:   ;
   9:  LBL[180:FAULT] ;
  10:  CALL SETUP_MSG(2) ;
  11:  UALM[13] ;
  12:  JMP LBL[100] ;
  13:   ;
  14:  LBL[200:AUTO DOOR] ;
  15:  IF (DO[58:HAAS LATHE]=ON),JMP LBL[9999] ;
  16:  R[43:ACTION RETRIES]=R[43:ACTION RETRIES]+1    ;
  17:  IF R[43:ACTION RETRIES]>=3,JMP LBL[280] ;
  18:  DO[183:OPEN DOOR COM]=PULSE,1.0sec ;
  19:  WAIT (DO[196:DOOR OPEN 1 COM]=ON) TIMEOUT,LBL[200] ;
  20:  JMP LBL[9999] ;
  21:   ;
  22:  LBL[280:FAULT] ;
  23:  CALL MACH_MSG(16) ;
  24:  UALM[5] ;
  25:  JMP LBL[100] ;
  26:   ;
  27:  LBL[400:MANUAL DOOR] ;
  28:  $WAITTMOUT=100 ;
  29:  UFRAME_NUM=5 ;
  30:  UTOOL_NUM=4 ;
  31:J PR[80:MACHINE PERCH] 100% CNT10    ;
  32:  R[40:RECOVER START PR]=80    ;
  33:  --eg:TOUCHUP HERE FOR DOOR OPEN AND CLOSE PERCH, THIS SHOULD PREP THE
    :  MOTION TO GO TO THE DOOR OPEN/CLOSE POSITIONS ;
  34:J PR[24:DOOR OP/CL PERCH] 100% CNT20    ;
  35:  --eg:^^TOUCHUP HERE^^ ;
  36:  R[40:RECOVER START PR]=24    ;
  37:  WAIT DO[197:DOOR CLOSED 1 COM]=ON TIMEOUT,LBL[480] ;
  38:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  39:  PR[34,2:APP Z TOOL OFF]=(-100)    ;
  40:L PR[147:DOOR CLOSED POS] 500mm/sec FINE Offset,PR[34:APP Z TOOL OFF]    ;
  41:  --eg:TOUCH UP HERE FOR DOOR OPEN/CLOSE MOVEMENT ;
  42:  R[40:RECOVER START PR]=147    ;
  43:L PR[147:DOOR CLOSED POS] 1000mm/sec FINE    ;
  44:  --eg:^^TOUCHUP HERE^^ ;
  45:   ;
  46:  --eg:TOUCH UP HERE FOR DOOR OPEN/CLOSE MOVEMENT ;
  47:L PR[145:DOOR OP/CL MID] 500mm/sec FINE ACC50    ;
  48:  --eg:^^TOUCHUP HERE^^ ;
  49:  R[40:RECOVER START PR]=145    ;
  50:   ;
  51:  --eg:TOUCH UP HERE FOR DOOR OPEN/CLOSE MOVEMENT ;
  52:L PR[146:DOOR OPEN POS] 500mm/sec FINE ACC50    ;
  53:  --eg:^^TOUCHUP HERE^^ ;
  54:  R[40:RECOVER START PR]=146    ;
  55:  WAIT DO[196:DOOR OPEN 1 COM]=ON TIMEOUT,LBL[482] ;
  56:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  57:  PR[34,2:APP Z TOOL OFF]=(-100)    ;
  58:L PR[146:DOOR OPEN POS] 500mm/sec FINE Offset,PR[34:APP Z TOOL OFF]    ;
  59:  R[40:RECOVER START PR]=24    ;
  60:L PR[24:DOOR OP/CL PERCH] 2000mm/sec CNT50    ;
  61:  R[40:RECOVER START PR]=80    ;
  62:J PR[80:MACHINE PERCH] 100% CNT20    ;
  63:  JMP LBL[9999] ;
  64:   ;
  65:  LBL[480:FAULT] ;
  66:  CALL MACH_MSG(15) ;
  67:  UALM[5] ;
  68:  JMP LBL[100] ;
  69:   ;
  70:  LBL[581:FAULT] ;
  71:  CALL MACH_MSG(16) ;
  72:  UALM[5] ;
  73:  JMP LBL[100] ;
  74:   ;
  75:  LBL[9999:END] ;
/POS
/END
