/PROG  CAL_LASER
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 4086;
CREATE		= DATE 23-03-08  TIME 01:44:42;
MODIFIED	= DATE 23-11-22  TIME 04:42:40;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 187;
MEMORY_SIZE	= 4490;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:J PR[1:HOME POSITION] 100% FINE    ;
   2:  R[40:RECOVER START PR]=1    ;
   3:   ;
   4:  UTOOL[10:LASER CAL]=PR[28:BASE UT10] ;
   5:  --eg:FIND FIRST OUTSIDE EDGE OF PART ;
   6:  RO[8:LASER ON]=ON ;
   7:  UFRAME_NUM=3 ;
   8:  UTOOL_NUM=10 ;
   9:  PR[200,3:ORIGIN SEARCH ST]=350    ;
  10:  PR[34,6:APP Z TOOL OFF]=180    ;
  11:J PR[200:ORIGIN SEARCH ST] 100% FINE Offset,PR[34:APP Z TOOL OFF]    ;
  12:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
  13:L P[3] 50mm/sec FINE Skip,LBL[999] Offset,PR[34:APP Z TOOL OFF]    ;
  14:  SKIP CONDITION RI[8:LASER INPUT]=OFF    ;
  15:L PR[200:ORIGIN SEARCH ST] 1mm/sec FINE Skip,LBL[999] Offset,PR[34:APP Z TOOL OFF]    ;
  16:  PR[200:ORIGIN SEARCH ST]=LPOS    ;
  17:  PR[35:APP Y TOOL OFFS]=PR[30:CARTESIAN ZERO]    ;
  18:  PR[35,3:APP Y TOOL OFFS]=2    ;
  19:L PR[200:ORIGIN SEARCH ST] 100mm/sec FINE Tool_Offset,PR[35:APP Y TOOL OFF]    ;
  20:  PR[35,1:APP Y TOOL OFFS]=100    ;
  21:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
  22:L PR[200:ORIGIN SEARCH ST] 1mm/sec FINE Tool_Offset,PR[35:APP Y TOOL OFF] Skip,LBL[181],PR[202:ORG EDGE 1 FOUND]=LPOS    ;
  23:  PR[202,3:ORG EDGE 1 FOUND]=PR[202,3:ORG EDGE 1 FOUND]-2    ;
  24:  WAIT    .25(sec) ;
  25:  --eg:FIND SECOND OUTSIDE EDGE OF PART ;
  26:  PR[203,3:ORG EDGE 2 SEARC]=PR[200,3:ORIGIN SEARCH ST]    ;
  27:  PR[35:APP Y TOOL OFFS]=PR[30:CARTESIAN ZERO]    ;
  28:  PR[35,3:APP Y TOOL OFFS]=2    ;
  29:J PR[203:ORG EDGE 2 SEARC] 50% FINE Tool_Offset,PR[35:APP Y TOOL OFF]    ;
  30:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
  31:  PR[35,1:APP Y TOOL OFFS]=50    ;
  32:L PR[203:ORG EDGE 2 SEARC] 1mm/sec FINE Tool_Offset,PR[35:APP Y TOOL OFF] Skip,LBL[181],PR[204:ORG EDGE 2 FOUND]=LPOS    ;
  33:  PR[204,3:ORG EDGE 2 FOUND]=PR[204,3:ORG EDGE 2 FOUND]-2    ;
  34:  --eg:FIND THIRD OUTSIDE EDGE OF PART ;
  35:  WAIT    .25(sec) ;
  36:  PR[205,3:ORG EDGE 3 SEARC]=PR[200,3:ORIGIN SEARCH ST]    ;
  37:  PR[35:APP Y TOOL OFFS]=PR[30:CARTESIAN ZERO]    ;
  38:  PR[35,3:APP Y TOOL OFFS]=2    ;
  39:J PR[205:ORG EDGE 3 SEARC] 50% FINE Tool_Offset,PR[35:APP Y TOOL OFF]    ;
  40:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
  41:  PR[35,1:APP Y TOOL OFFS]=50    ;
  42:L PR[205:ORG EDGE 3 SEARC] 1mm/sec FINE Tool_Offset,PR[35:APP Y TOOL OFF] Skip,LBL[181],PR[206:ORG EDGE 3 FOUND]=LPOS    ;
  43:  PR[206,3:ORG EDGE 3 FOUND]=PR[206,3:ORG EDGE 3 FOUND]-2    ;
  44:  WAIT    .25(sec) ;
  45:  --eg:CALCULATE CENTER ;
  46:  CALL CALC_CIRCLE(202,204,206,100,101,102) ;
  47:  PR[207:ORG CENTER]=PR[200:ORIGIN SEARCH ST]    ;
  48:  PR[207,1:ORG CENTER]=R[100:POST CENTER X]    ;
  49:  PR[207,2:ORG CENTER]=R[101:POST CENTER Y]    ;
  50:J PR[207:ORG CENTER] 100% FINE    ;
  51:  PR[207,1:ORG CENTER]=PR[207,1:ORG CENTER]-15    ;
  52:  PR[207,2:ORG CENTER]=PR[207,2:ORG CENTER]-15    ;
  53:  PR[207,3:ORG CENTER]=PR[207,3:ORG CENTER]+20    ;
  54:J PR[207:ORG CENTER] 100% FINE    ;
  55:  WAIT    .50(sec) ;
  56:  SKIP CONDITION RI[8:LASER INPUT]=OFF    ;
  57:  PR[207,3:ORG CENTER]=PR[207,3:ORG CENTER]+100    ;
  58:L PR[207:ORG CENTER] 1mm/sec FINE Skip,LBL[181]    ;
  59:  WAIT    .50(sec) ;
  60:  PR[207:ORG CENTER]=LPOS    ;
  61:  PR[207,3:ORG CENTER]=PR[207,3:ORG CENTER]-3    ;
  62:J PR[207:ORG CENTER] 100% FINE    ;
  63:  WAIT    .50(sec) ;
  64:  PR[207,1:ORG CENTER]=PR[207,1:ORG CENTER]+20    ;
  65:  SKIP CONDITION RI[8:LASER INPUT]=OFF    ;
  66:L PR[207:ORG CENTER] 1mm/sec FINE Skip,LBL[181]    ;
  67:  WAIT    .50(sec) ;
  68:  PR[207:ORG CENTER]=LPOS    ;
  69:  PR[207,1:ORG CENTER]=PR[207,1:ORG CENTER]+.8    ;
  70:J PR[207:ORG CENTER] 50% FINE    ;
  71:  WAIT    .50(sec) ;
  72:  R[180:CENTER X]=PR[207,1:ORG CENTER]    ;
  73:  PR[207,1:ORG CENTER]=PR[207,1:ORG CENTER]-15    ;
  74:  PR[207,6:ORG CENTER]=PR[207,6:ORG CENTER]+90    ;
  75:J PR[207:ORG CENTER] 50% FINE    ;
  76:  WAIT    .50(sec) ;
  77:  PR[207,2:ORG CENTER]=PR[207,2:ORG CENTER]+20    ;
  78:  SKIP CONDITION RI[8:LASER INPUT]=OFF    ;
  79:L PR[207:ORG CENTER] 1mm/sec FINE Skip,LBL[181]    ;
  80:  WAIT    .50(sec) ;
  81:  PR[207:ORG CENTER]=LPOS    ;
  82:  PR[207,2:ORG CENTER]=PR[207,2:ORG CENTER]+.8    ;
  83:J PR[207:ORG CENTER] 50% FINE    ;
  84:  WAIT    .50(sec) ;
  85:  PR[207,1:ORG CENTER]=R[180:CENTER X]    ;
  86:  PR[207,6:ORG CENTER]=0    ;
  87:J PR[207:ORG CENTER] 50% FINE    ;
  88:  WAIT    .50(sec) ;
  89:  PR[207,1:ORG CENTER]=PR[207,1:ORG CENTER]-10    ;
  90:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
  91:L PR[207:ORG CENTER] 1mm/sec FINE Skip,LBL[181]    ;
  92:  WAIT    .50(sec) ;
  93:  PR[207:ORG CENTER]=LPOS    ;
  94:  R[181:FINE X 1]=PR[207,1:ORG CENTER]    ;
  95:  WAIT    .50(sec) ;
  96:  PR[207,1:ORG CENTER]=PR[207,1:ORG CENTER]+.5    ;
  97:L PR[207:ORG CENTER] 1mm/sec FINE    ;
  98:  WAIT    .50(sec) ;
  99:  PR[207,1:ORG CENTER]=PR[207,1:ORG CENTER]+10    ;
 100:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
 101:L PR[207:ORG CENTER] 1mm/sec FINE Skip,LBL[181]    ;
 102:  WAIT    .50(sec) ;
 103:  PR[207:ORG CENTER]=LPOS    ;
 104:  R[182:FINE X 2]=PR[207,1:ORG CENTER]    ;
 105:  R[183:FINE X T]=((R[181:FINE X 1]+R[182:FINE X 2])/2) ;
 106:  PR[207,1:ORG CENTER]=R[183:FINE X T]    ;
 107:J PR[207:ORG CENTER] 50% FINE    ;
 108:  WAIT    .50(sec) ;
 109:  PR[207,2:ORG CENTER]=PR[207,2:ORG CENTER]-10    ;
 110:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
 111:L PR[207:ORG CENTER] 1mm/sec FINE Skip,LBL[181]    ;
 112:  WAIT    .50(sec) ;
 113:  PR[207:ORG CENTER]=LPOS    ;
 114:  R[184:FINE Y 1]=PR[207,2:ORG CENTER]    ;
 115:  WAIT    .50(sec) ;
 116:  PR[207,2:ORG CENTER]=PR[207,2:ORG CENTER]+.5    ;
 117:L PR[207:ORG CENTER] 1mm/sec FINE    ;
 118:  WAIT    .50(sec) ;
 119:  PR[207,2:ORG CENTER]=PR[207,2:ORG CENTER]+10    ;
 120:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
 121:L PR[207:ORG CENTER] 1mm/sec FINE Skip,LBL[181]    ;
 122:  WAIT    .50(sec) ;
 123:  PR[207:ORG CENTER]=LPOS    ;
 124:  R[185:FINE Y 2]=PR[207,2:ORG CENTER]    ;
 125:  R[188:FINE Y T]=((R[184:FINE Y 1]+R[185:FINE Y 2])/2) ;
 126:  PR[207,2:ORG CENTER]=R[188:FINE Y T]    ;
 127:  PR[207,6:ORG CENTER]=0    ;
 128:J PR[207:ORG CENTER] 50% FINE    ;
 129:  WAIT    .50(sec) ;
 130:  PR[224:TEACH SLOT]=LPOS    ;
 131:   ;
 132:  PR[224,4:TEACH SLOT]=2    ;
 133:  PR[224,5:TEACH SLOT]=(-2)    ;
 134:  PR[224,6:TEACH SLOT]=180    ;
 135:J PR[224:TEACH SLOT] 50% FINE    ;
 136:  WAIT    .50(sec) ;
 137:  PR[224,4:TEACH SLOT]=(-5)    ;
 138:  SKIP CONDITION RI[8:LASER INPUT]=OFF    ;
 139:L PR[224:TEACH SLOT] 2deg/sec FINE Skip,LBL[181]    ;
 140:  WAIT    .50(sec) ;
 141:  PR[47:LPOS 1]=LPOS    ;
 142:  R[189:LAS 2 4]=PR[47,4:LPOS 1]    ;
 143:  PR[224,4:TEACH SLOT]=2    ;
 144:  PR[224,5:TEACH SLOT]=(-2)    ;
 145:  PR[224,6:TEACH SLOT]=180    ;
 146:J PR[224:TEACH SLOT] 100% FINE    ;
 147:  WAIT    .50(sec) ;
 148:  PR[224,5:TEACH SLOT]=5    ;
 149:  SKIP CONDITION RI[8:LASER INPUT]=OFF    ;
 150:L PR[224:TEACH SLOT] 2deg/sec FINE Skip,LBL[181]    ;
 151:  WAIT    .50(sec) ;
 152:  PR[47:LPOS 1]=LPOS    ;
 153:  R[190:LAS 2 5]=PR[47,5:LPOS 1]    ;
 154:  PR[224,4:TEACH SLOT]=R[189:LAS 2 4]    ;
 155:  PR[224,5:TEACH SLOT]=R[190:LAS 2 5]    ;
 156:  PR[224,6:TEACH SLOT]=180    ;
 157:J PR[224:TEACH SLOT] 50% FINE    ;
 158:  PR[224,4:TEACH SLOT]=0    ;
 159:  PR[224,5:TEACH SLOT]=0    ;
 160:  PR[224,6:TEACH SLOT]=0    ;
 161:J PR[224:TEACH SLOT] 50% FINE    ;
 162:  R[816:ROBOT YAW]=((R[189:LAS 2 4]/2)+180) ;
 163:  R[818:ROBOT PITCH]=((R[190:LAS 2 5]/2)-30) ;
 164:  DO[123:CAL UTOOL]=PULSE,1.0sec ;
 165:  WAIT (DI[104:UTOOL CAL DONE]=ON)    ;
 166:  WAIT   2.00(sec) ;
 167:  PR[49,1:LASER U10]=R[830:NEW X]    ;
 168:  PR[49,2:LASER U10]=R[832:NEW Y]    ;
 169:  PR[49,3:LASER U10]=R[834:NEW Z]    ;
 170:  PR[49,4:LASER U10]=R[816:ROBOT YAW]    ;
 171:  PR[49,5:LASER U10]=R[818:ROBOT PITCH]    ;
 172:  PR[49,6:LASER U10]=R[820:ROBOT ROLL]    ;
 173:  UTOOL[9:LASER]=PR[49:LASER U10] ;
 174:  UTOOL_NUM=9 ;
 175:  PR[224:TEACH SLOT]=LPOS    ;
 176:  PR[224,4:TEACH SLOT]=0    ;
 177:  PR[224,5:TEACH SLOT]=0    ;
 178:  PR[224,6:TEACH SLOT]=0    ;
 179:J PR[224:TEACH SLOT] 50% FINE    ;
 180:   ;
 181:  PR[224,6:TEACH SLOT]=180    ;
 182:J PR[224:TEACH SLOT] 50% FINE    ;
 183:  PR[224,6:TEACH SLOT]=0    ;
 184:J PR[224:TEACH SLOT] 50% FINE    ;
 185:J PR[1:HOME POSITION] 100% FINE    ;
 186:  R[40:RECOVER START PR]=1    ;
 187:  RO[8:LASER ON]=OFF ;
/POS
P[3]{
   GP1:
	UF : 3, UT : 10,		CONFIG : 'N U T, 0, 0, 0',
	X =   350.805  mm,	Y =    71.740  mm,	Z =   242.400  mm,
	W =      .000 deg,	P =      .000 deg,	R =      .000 deg
};
/END
