/PROG  SETUP_NEW_PART
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 1347;
CREATE		= DATE 22-11-17  TIME 19:14:16;
MODIFIED	= DATE 23-04-01  TIME 00:07:50;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 53;
MEMORY_SIZE	= 1831;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !ROTATE TABLE FOR NEW PART IF     ;
   2:  !NEW PART IS NOT IN POSITION      ;
   3:  LBL[1] ;
   4:  IF (DI[1:TABLE AT A POSITION]=OFF AND DO[104:NEW PART ACTIVE]=ON AND DO[102:NEW PART A]=ON OR DI[2:TABLE AT B POSITION]=OFF AND DO[104:NEW PART ACTIVE]=ON AND DO[103:NEW PART B]=ON) THEN ;
   5:  CALL TABLE_MSG(14) ;
   6:  WAIT (DO[25:A SIDE LOAD COMPLETE]=ON OR DO[26:B SIDE LOAD COMPLETE]=ON)    ;
   7:  CALL TABLE_INDEX    ;
   8:  ENDIF ;
   9:  !LOGIC FOR DIRECT ENTRY SETUP     ;
  10:  !IF LASER IS NOT BEING USED       ;
  11:  ! ;
  12:  IF (DI[48:MEAS RAW DIA]=OFF AND R[800:RAW DIAMETER]>0) THEN ;
  13:  R[77:RAW DIAMETER]=R[800:RAW DIAMETER]    ;
  14:  DO[54:PART DIAMETER FOUND]=ON ;
  15:  ENDIF ;
  16:  ! ;
  17:  IF (DI[49:MEAS RAW LENGTH]=OFF AND R[802:RAW LENGTH]>0) THEN ;
  18:  R[67:MEAS PART LENGTH]=R[802:RAW LENGTH]    ;
  19:  DO[53:PART LENGTH FOUND]=ON ;
  20:  DO[115:FINE LENGTH CHECKED]=ON ;
  21:  ENDIF ;
  22:  ! ;
  23:  IF (DI[50:MEAS FINISHED DIA]=OFF) THEN ;
  24:  R[81:FINISHED DIA]=R[804:FINISHED DIA.]    ;
  25:  ELSE ;
  26:  R[81:FINISHED DIA]=R[77:RAW DIAMETER]    ;
  27:  ENDIF ;
  28:  ! ;
  29:  IF (DI[51:MEAS FINISHED LENGTH]=OFF) THEN ;
  30:  R[72:FIN PART LENGTH]=R[806:FINISHED LENGTH]    ;
  31:  ELSE ;
  32:  R[72:FIN PART LENGTH]=R[67:MEAS PART LENGTH]    ;
  33:  ENDIF ;
  34:  ! ;
  35:  IF (DI[52:MEAS TMPLT H]=OFF AND R[814:TMPLT HEIGHT]>0) THEN ;
  36:  R[66:TEMP Z TO TABLE]=R[814:TMPLT HEIGHT]    ;
  37:  DO[52:PALLET HEIGHT FOUND]=ON ;
  38:  ENDIF ;
  39:  ! ;
  40:  IF (DI[53:MEAS TMPLT NUM]=OFF AND R[812:TMPLT ID]>0) THEN ;
  41:  R[1:ACTIVE TEMPLATE]=R[812:TMPLT ID]    ;
  42:  SELECT R[1:ACTIVE TEMPLATE]=2,CALL TEMPLATE_2 ;
  43:         =3,CALL TEMPLATE_3 ;
  44:         =4,CALL TEMPLATE_4 ;
  45:         =5,CALL TEMPLATE_5 ;
  46:         =6,CALL TEMPLATE_6 ;
  47:  DO[51:PALLET TYPE FOUND]=ON ;
  48:  ENDIF ;
  49:  !MACHINE SETUP BASED ON MACHINE   ;
  50:  !TYPE SET ON HMI                  ;
  51:  IF (DO[55:OKUMA NO ANYBUS]=ON OR DO[56:OKUMA LATHE]=ON OR DO[58:HAAS LATHE]=ON OR DO[91:TEST RIG ENABLED]=ON OR DO[98:DEMO MODE]=ON OR DO[59:HARDINGE LATHE]=ON OR DO[93:MAZAK ON]=ON) THEN ;
  52:  CALL LATHE_SETUP    ;
  53:  ENDIF ;
/POS
/END
