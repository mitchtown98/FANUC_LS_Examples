/PROG  LATHE_UNLOAD_SUB
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 945;
CREATE		= DATE 23-11-21  TIME 02:54:28;
MODIFIED	= DATE 23-11-22  TIME 04:10:40;
FILE_NAME	= LATHE_UN;
VERSION		= 0;
LINE_COUNT	= 41;
MEMORY_SIZE	= 1305;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:J PR[80:MACHINE PERCH] 100% CNT20    ;
   2:  R[40:RECOVER START PR]=80    ;
   3:   ;
   4:J PR[81:MACH APPR] 100% CNT20    ;
   5:  R[40:RECOVER START PR]=81    ;
   6:  IF (DO[79:SUBSPINDLE AIR BLOW ON]=ON) THEN ;
   7:  DO[190:SUB AIRBLOW COM]=PULSE,3.0sec ;
   8:  ENDIF ;
   9:  TIMER[3]=STOP ;
  10:  TIMER[3]=RESET ;
  11:  TIMER[3]=START ;
  12:  UFRAME_NUM=5 ;
  13:  UTOOL_NUM=2 ;
  14:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  15:  PR[31:APP Z OFFS]=PR[30:CARTESIAN ZERO]    ;
  16:  PR[34,3:APP Z TOOL OFF]=PR[88,1:UNLD SUB]-R[284:MIN X SUB]    ;
  17:  PR[31,3:APP Z OFFS]=R[810:UNLOAD OFFSET]    ;
  18:L PR[88:UNLD SUB] 2000mm/sec CNT20 Tool_Offset,PR[34:APP Z TOOL OFF] Offset,PR[31:APP Z OFFS]    ;
  19:  R[40:RECOVER START PR]=88    ;
  20:  WAIT (DO[190:SUB AIRBLOW COM]=OFF)    ;
  21:  CALL LATHE_ORIENT2_SUB    ;
  22:L PR[88:UNLD SUB] 750mm/sec FINE Offset,PR[31:APP Z OFFS]    ;
  23:  CALL T2_GRIP    ;
  24:  R[75:T2 X DROP]=R[64:UNLOAD X DROP]    ;
  25:  R[76:T2 Y DROP]=R[65:UNLOAD Y DROP]    ;
  26:  R[64:UNLOAD X DROP]=0    ;
  27:  R[65:UNLOAD Y DROP]=0    ;
  28:  IF (R[4:CUR ROW DROP (X)]=1 AND R[5:CUR COL DROP (Y)]=1) THEN ;
  29:  R[75:T2 X DROP]=1    ;
  30:  R[76:T2 Y DROP]=1    ;
  31:  ENDIF ;
  32:  DO[137:T2 FINISHED PART]=ON ;
  33:  DO[106:MEAS UNLOAD ACTIVE]=OFF ;
  34:  CALL LATHE_UNCLAMP_SUB    ;
  35:L PR[88:UNLD SUB] 1000mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF] Offset,PR[31:APP Z OFFS]    ;
  36:  R[40:RECOVER START PR]=81    ;
  37:J PR[81:MACH APPR] 100% CNT10    ;
  38:  R[40:RECOVER START PR]=80    ;
  39:J PR[80:MACHINE PERCH] 100% CNT20    ;
  40:  TIMER[3]=STOP ;
  41:  R[122:SERVICE MACH TM]=TIMER[3]    ;
/POS
/END
