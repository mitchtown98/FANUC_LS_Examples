/PROG  PART_REGRIP
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "PART REGRIP";
PROG_SIZE	= 1929;
CREATE		= DATE 22-10-06  TIME 13:19:00;
MODIFIED	= DATE 23-11-21  TIME 05:38:18;
FILE_NAME	= PART_LEN;
VERSION		= 0;
LINE_COUNT	= 89;
MEMORY_SIZE	= 2373;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  --eg:REGRIP PART FOR CONSISTANT MACHINE LOAD. ;
   2:   ;
   3:  LBL[1:START] ;
   4:L PR[14:REGRIP POUNCE] 2000mm/sec CNT30    ;
   5:  R[40:RECOVER START PR]=14    ;
   6:  IF (R[67:MEAS PART LENGTH]=0),JMP LBL[10] ;
   7:  JMP LBL[100] ;
   8:   ;
   9:  LBL[10:FAULT] ;
  10:  CALL SETUP_MSG(1) ;
  11:  UALM[13] ;
  12:  JMP LBL[1] ;
  13:   ;
  14:  LBL[100:SELECT OP] ;
  15:  IF (DO[130:T1 REGRIP]=ON AND DO[49:PART AT REGRIP]=OFF),JMP LBL[200] ;
  16:  IF ((DO[129:T1 MACHINE]=ON AND DO[49:PART AT REGRIP]=OFF)),JMP LBL[200] ;
  17:  IF (DO[138:T2 REGRIP RECOV]=ON AND DO[49:PART AT REGRIP]=OFF OR DO[137:T2 FINISHED PART]=ON AND DO[49:PART AT REGRIP]=OFF),JMP LBL[200] ;
  18:  IF (DO[171:T1 UNGRIPPED]=ON AND DO[49:PART AT REGRIP]=ON),JMP LBL[300] ;
  19:  JMP LBL[9980] ;
  20:   ;
  21:  LBL[200:DROP TO REGRIP] ;
  22:  UFRAME_NUM=5 ;
  23:  IF (DO[130:T1 REGRIP]=ON OR DO[129:T1 MACHINE]=ON) THEN ;
  24:  UTOOL_NUM=1 ;
  25:  ENDIF ;
  26:  IF ((DO[138:T2 REGRIP RECOV]=ON OR DO[137:T2 FINISHED PART]=ON) AND DO[129:T1 MACHINE]=OFF AND DO[130:T1 REGRIP]=OFF)) THEN ;
  27:  UTOOL_NUM=2 ;
  28:J PR[4:T2 REGRIP PERCH] 100% FINE    ;
  29:  R[40:RECOVER START PR]=4    ;
  30:  ENDIF ;
  31:  PR[37,3:REGRIP Z OFFS]=R[67:MEAS PART LENGTH]+100    ;
  32:J PR[55:REGRIP DROP] 100% CNT20 Offset,PR[37:REGRIP Z OFFS]    ;
  33:  R[40:RECOVER START PR]=55    ;
  34:  PR[37,3:REGRIP Z OFFS]=R[67:MEAS PART LENGTH]    ;
  35:L PR[55:REGRIP DROP] 200mm/sec FINE Offset,PR[37:REGRIP Z OFFS]    ;
  36:  IF (DO[130:T1 REGRIP]=ON OR DO[129:T1 MACHINE]=ON) THEN ;
  37:  CALL T1_UNGRIP    ;
  38:  WAIT    .50(sec) ;
  39:  CALL T1_GRIP    ;
  40:  CALL T1_UNGRIP    ;
  41:  ENDIF ;
  42:  IF (DO[138:T2 REGRIP RECOV]=ON) THEN ;
  43:  CALL T1_GRIP    ;
  44:  ENDIF ;
  45:  DO[49:PART AT REGRIP]=ON ;
  46:  WAIT   1.00(sec) ;
  47:  IF (DO[31:CYCLE STOP ACTIVE]=OFF AND DO[202:RECOVER ACTIVE]=OFF AND DO[128:T1 REJECT]=OFF),JMP LBL[301] ;
  48:  PR[37,3:REGRIP Z OFFS]=R[67:MEAS PART LENGTH]+100    ;
  49:L PR[55:REGRIP DROP] 2000mm/sec CNT20 Offset,PR[37:REGRIP Z OFFS]    ;
  50:  JMP LBL[9999] ;
  51:   ;
  52:  LBL[300:PICK REGRIP] ;
  53:  UFRAME_NUM=5 ;
  54:  UTOOL_NUM=1 ;
  55:  PR[37,3:REGRIP Z OFFS]=R[67:MEAS PART LENGTH]+100    ;
  56:  R[40:RECOVER START PR]=55    ;
  57:J PR[55:REGRIP DROP] 100% CNT10 Offset,PR[37:REGRIP Z OFFS]    ;
  58:  IF DO[202:RECOVER ACTIVE]=ON,JMP LBL[9999] ;
  59:  LBL[301:PICK] ;
  60:  PR[37,3:REGRIP Z OFFS]=R[67:MEAS PART LENGTH]+5    ;
  61:L PR[55:REGRIP DROP] 15mm/sec FINE Offset,PR[37:REGRIP Z OFFS]    ;
  62:  SKIP CONDITION RI[7:SPRING COMPRESSED]=ON    ;
  63:  WAIT    .50(sec) ;
  64:  PR[37,3:REGRIP Z OFFS]=R[67:MEAS PART LENGTH]-25    ;
  65:L PR[55:REGRIP DROP] 15mm/sec FINE Offset,PR[37:REGRIP Z OFFS] Skip,LBL[380]    ;
  66:  DO[36:PART PRESENT CHECK ONLY]=ON ;
  67:  CALL T1_GRIP    ;
  68:  DO[49:PART AT REGRIP]=OFF ;
  69:  DO[129:T1 MACHINE]=ON ;
  70:  PR[37,3:REGRIP Z OFFS]=R[67:MEAS PART LENGTH]+100    ;
  71:L PR[55:REGRIP DROP] 500mm/sec CNT20 Offset,PR[37:REGRIP Z OFFS]    ;
  72:  JMP LBL[9999] ;
  73:   ;
  74:  LBL[380:PART NOT FOUND] ;
  75:  PR[37,3:REGRIP Z OFFS]=R[67:MEAS PART LENGTH]+100    ;
  76:  R[40:RECOVER START PR]=55    ;
  77:J PR[55:REGRIP DROP] 100% CNT10 Offset,PR[37:REGRIP Z OFFS]    ;
  78:  CALL LASER_MSG(9) ;
  79:  DO[49:PART AT REGRIP]=OFF ;
  80:  JMP LBL[9999] ;
  81:   ;
  82:  LBL[9980:FAULT] ;
  83:  CALL TABLE_MSG(15) ;
  84:  UALM[11] ;
  85:  JMP LBL[1] ;
  86:   ;
  87:  LBL[9999:END] ;
  88:  R[40:RECOVER START PR]=14    ;
  89:J PR[14:REGRIP POUNCE] 100% CNT40    ;
/POS
/END
