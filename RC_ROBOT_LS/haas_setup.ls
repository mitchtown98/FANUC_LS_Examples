/PROG  HAAS_SETUP
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "FIND CHUCK LOC";
PROG_SIZE	= 8081;
CREATE		= DATE 22-02-24  TIME 23:11:38;
MODIFIED	= DATE 22-10-31  TIME 15:07:18;
FILE_NAME	= OKUMA_SE;
VERSION		= 0;
LINE_COUNT	= 353;
MEMORY_SIZE	= 8741;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  --eg:FIND THE X LOCATION OF THE CHUCK LOAD/UNLOAD POSITION ON APPROPRIATE
    :  CHUCKS.  ;
   2:  IF DO[145:AT HOME]=OFF,CALL PNS0002 ;
   3:   ;
   4:  LBL[1:START] ;
   5:  R[88:NOM SLOW mm/sec]=10    ;
   6:  R[89:NOM FAST mm/sec]=35    ;
   7:  UFRAME_NUM=5 ;
   8:J PR[1:HOME POSITION] 100% CNT10    ;
   9:  R[40:RECOVER START PR]=1    ;
  10:  $WAITTMOUT=500 ;
  11:   ;
  12:  LBL[10:FAULT CHECKS] ;
  13:  --eg:CHECK FOR FAULTS AND DISPLAY ALARMS ;
  14:   ;
  15:  --eg:CHECK FOR SYSTEM LINK ;
  16:  IF (!DI[361:HAAS IN AUTO]) THEN ;
  17:  CALL MACH_MSG(8) ;
  18:  UALM[5] ;
  19:  JMP LBL[10] ;
  20:  ENDIF ;
  21:   ;
  22:  --eg:CHECK FOR HAAS ALARM ;
  23:  IF (DI[354:HAAS ALARMED]) THEN ;
  24:  CALL MACH_MSG(4) ;
  25:J PR[1:HOME POSITION] 100% CNT20    ;
  26:  R[40:RECOVER START PR]=1    ;
  27:  UALM[5] ;
  28:  JMP LBL[1] ;
  29:  ENDIF ;
  30:   ;
  31:  --eg:CHECK FOR READY. MAKE SURE ROBOT GRIPPERS ARE EMPTY. ;
  32:  IF (DO[174:PART IN T1] OR DO[175:PART IN T2]) THEN ;
  33:  CALL TOOL_MSG(30) ;
  34:  UALM[1] ;
  35:  JMP LBL[10] ;
  36:  ENDIF ;
  37:   ;
  38:  --eg:CHECK FOR READY. MAKE SURE MACHINE HAS RAW PART LOADED ;
  39:  IF (DO[105:MEAS LOAD ACTIVE] AND DO[61:LOAD MAINSPINDLE ON] AND !DI[355]) THEN ;
  40:  CALL MACH_MSG(9) ;
  41:  UALM[5] ;
  42:  JMP LBL[10] ;
  43:  ENDIF ;
  44:   ;
  45:  --eg:CHECK FOR READY. MAKE SURE MACHINE HAS RAW PART LOADED ;
  46:  IF (DO[105:MEAS LOAD ACTIVE] AND DO[62:LOAD SUBSPINDLE ON] AND !DI[358]) THEN ;
  47:  CALL MACH_MSG(9) ;
  48:  UALM[5] ;
  49:  JMP LBL[10] ;
  50:  ENDIF ;
  51:   ;
  52:  --eg:CHECK FOR READY. MAKE SURE MACHINE HAS FINISHED PART LOADED ;
  53:  IF (DO[106:MEAS UNLOAD ACTIVE] AND DO[67:UNLOAD SUBSPINDLE ON] AND !DI[358]) THEN ;
  54:  CALL MACH_MSG(10) ;
  55:  UALM[5] ;
  56:  JMP LBL[10] ;
  57:  ENDIF ;
  58:   ;
  59:  --eg:CHECK FOR READY. MAKE SURE MACHINE PART IS NOT IN MAIN SPINDLE WHILE
    :  MEASURING SUB SPINDLE ;
  60:  IF ((DO[105:MEAS LOAD ACTIVE] AND DO[62:LOAD SUBSPINDLE ON]) AND DI[355]) THEN ;
  61:  CALL MACH_MSG(13) ;
  62:  UALM[5] ;
  63:  JMP LBL[10] ;
  64:  ENDIF ;
  65:   ;
  66:  --eg:CHECK FOR READY. MAKE SURE ROBOT GRIPPER IS EMPTY AND MACHINE PART
    :  IS NOT IN SUB SPINDLE WHILE MEASURING MAIN SPINDLE ;
  67:  IF (DO[105:MEAS LOAD ACTIVE] AND DO[62:LOAD SUBSPINDLE ON] AND DI[355]) THEN ;
  68:  CALL MACH_MSG(13) ;
  69:  UALM[5] ;
  70:  JMP LBL[10] ;
  71:  ENDIF ;
  72:   ;
  73:  JMP LBL[100] ;
  74:   ;
  75:  LBL[100:CHOOSE OPERATION] ;
  76:  WAIT    .10(sec) ;
  77:  IF (DO[31:CYCLE STOP ACTIVE]),JMP LBL[9999] ;
  78:  !CHECKS TO OPEN DOOR MANUALLY ;
  79:  IF (!DO[210:MACH SAFE FOR ROBOT] AND DO[104:NEW PART ACTIVE] AND DI[355] AND !DI[354:HAAS ALARMED] AND DI[360:TURRET AT HOME] AND DI[361:HAAS IN AUTO] AND DO[50:PALLET SETUP COMPLETE] OR !DO[210:MACH SAFE FOR ROBOT] AND 
    :  DO[104:NEW PART ACTIVE] AND DI[358] AND !DI[354:HAAS ALARMED] AND DI[360:TURRET AT HOME] AND DI[361:HAAS IN AUTO] AND DO[50:PALLET SETUP COMPLETE]) THEN ;
  80:  CALL HAAS_DOOR_OPEN    ;
  81:  ENDIF ;
  82:   ;
  83:  --eg:GO TO MAINT TO REMOVE OR REPLACE T1 GRIPPER PINS ;
  84:  IF (DO[105:MEAS LOAD ACTIVE] AND !DO[107:MEAS LOAD COMP] AND !DO[108:MEAS UNLD COMP] AND !DO[124:PINS REMOVED]),JMP LBL[200] ;
  85:  IF (DO[63:NO LOAD ON] AND DO[107:MEAS LOAD COMP] AND !DO[108:MEAS UNLD COMP] AND !DO[124:PINS REMOVED]),JMP LBL[200] ;
  86:  IF ((DO[108:MEAS UNLD COMP] OR DO[68:NO UNLOAD ON]) AND (DO[124:PINS REMOVED])),JMP LBL[200] ;
  87:   ;
  88:  --eg:CHECK FOR NO LOAD AND START MACHINE ;
  89:  IF (DO[63:NO LOAD ON] AND DO[124:PINS REMOVED] AND !DO[107:MEAS LOAD COMP]),JMP LBL[800] ;
  90:   ;
  91:  --eg:CHECK FOR MAIN SPINDLE MEASURE RAW ;
  92:  IF (DO[105:MEAS LOAD ACTIVE] AND DO[171:T1 UNGRIPPED] AND DO[61:LOAD MAINSPINDLE ON] AND DO[209:MACH READY FOR SERVICE] AND DO[124:PINS REMOVED] AND DI[355]),JMP LBL[300] ;
  93:  --eg:CHECK FOR SUB SPINDLE MEASURE RAW ;
  94:  IF (DO[105:MEAS LOAD ACTIVE] AND DO[171:T1 UNGRIPPED] AND DO[62:LOAD SUBSPINDLE ON] AND DI[358] AND DO[209:MACH READY FOR SERVICE]),JMP LBL[400] ;
  95:   ;
  96:  --eg:CHECK FOR MAIN SPINDLE MEASURE FINISHED ;
  97:  IF (DO[106:MEAS UNLOAD ACTIVE] AND DO[171:T1 UNGRIPPED] AND DO[66:UNLOAD MAINSPINDLE ON] AND DI[355] AND DO[209:MACH READY FOR SERVICE] AND DO[50:PALLET SETUP COMPLETE]),JMP LBL[500] ;
  98:  --eg:CHECK FOR SUB SPINDLE MEASURE FINISHED ;
  99:  IF (DO[106:MEAS UNLOAD ACTIVE] AND !DO[174:PART IN T1] AND DO[67:UNLOAD SUBSPINDLE ON] AND DI[358] AND DO[209:MACH READY FOR SERVICE] AND DO[50:PALLET SETUP COMPLETE]),JMP LBL[600] ;
 100:   ;
 101:  --eg:TURN ON MEASURE UNLOAD COMPLETE IF IN NO UNLOAD MODE. ;
 102:  IF (DO[106:MEAS UNLOAD ACTIVE] AND DO[50:PALLET SETUP COMPLETE] AND DO[68:NO UNLOAD ON] AND DO[209:MACH READY FOR SERVICE]),DO[108:MEAS UNLD COMP]=(ON) ;
 103:   ;
 104:  !CHECK FOR PALLET SETUP COMPLETE ;
 105:  IF ((!DO[50:PALLET SETUP COMPLETE] AND DO[42:TABLE READY FOR SERVICE] AND DO[107:MEAS LOAD COMP] AND DO[108:MEAS UNLD COMP]) AND (DO[37:TABLE A READY] OR DO[38:TABLE B READY])),JMP LBL[700] ;
 106:  IF ((!DO[50:PALLET SETUP COMPLETE] AND DO[42:TABLE READY FOR SERVICE] AND DO[107:MEAS LOAD COMP]) AND (DO[66:UNLOAD MAINSPINDLE ON] OR DO[67:UNLOAD SUBSPINDLE ON])),JMP LBL[700] ;
 107:  IF (DO[50:PALLET SETUP COMPLETE] AND DO[42:TABLE READY FOR SERVICE] AND DO[107:MEAS LOAD COMP] AND DO[108:MEAS UNLD COMP]),JMP LBL[9999] ;
 108:   ;
 109:  JMP LBL[100] ;
 110:   ;
 111:  LBL[200:GOTO MAINT] ;
 112:  CALL PNS0003    ;
 113:   ;
 114:  IF (!DO[124:PINS REMOVED]) THEN ;
 115:  CALL TOOL_MSG(30) ;
 116:  PAUSE ;
 117:  DO[124:PINS REMOVED]=ON ;
 118:  ELSE ;
 119:  CALL TOOL_MSG(31) ;
 120:  PAUSE ;
 121:  DO[124:PINS REMOVED]=OFF ;
 122:  ENDIF ;
 123:   ;
 124:  JMP LBL[100] ;
 125:   ;
 126:  LBL[300:MEAS MAIN LOAD] ;
 127:  CALL FAULT_MESSAGE('Robot is measuring raw part','X location in machine.') ;
 128:J PR[23:MOVE TO MACH] 100% CNT10    ;
 129:  R[40:RECOVER START PR]=23    ;
 130:   ;
 131:J PR[3] 100% CNT30    ;
 132:  R[40:RECOVER START PR]=3    ;
 133:  UFRAME_NUM=5 ;
 134:  UTOOL_NUM=1 ;
 135:  WAIT (DO[209:MACH READY FOR SERVICE])    ;
 136:  $WAITTMOUT=500 ;
 137:  MONITOR MON_HAAS ;
 138:  DO[351]=OFF ;
 139:  CALL HAAS_ORIENT1_MAIN    ;
 140:J PR[11] 100% CNT50    ;
 141:  R[40:RECOVER START PR]=11    ;
 142:  UFRAME_NUM=5 ;
 143:  UTOOL_NUM=1 ;
 144:J PR[17:CLEAR] 100% CNT30    ;
 145:  R[40:RECOVER START PR]=17    ;
 146:  --eg:SET SEARCH START AND STOP POSITIONS ;
 147:  PR[71]=PR[77]    ;
 148:  R[140:J4 POS]=R[95]-PR[77,1]    ;
 149:  PR[34,3:APP Z TOOL OFF]=R[140:J4 POS]    ;
 150:  PR[72]=PR[77]    ;
 151:L PR[71] 2000mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 152:  R[40:RECOVER START PR]=72    ;
 153:  R[140:J4 POS]=R[96]-PR[77,1]    ;
 154:  PR[34,3:APP Z TOOL OFF]=R[140:J4 POS]    ;
 155:  SKIP CONDITION RI[7:SPRING COMPRESSED]=ON+    ;
 156:L PR[72] R[86:SEARCH FAST SPD]mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF] Skip,LBL[280],PR[77]=LPOS    ;
 157:  PR[34,3:APP Z TOOL OFF]=1    ;
 158:L PR[77] 100mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 159:  WAIT    .10(sec) ;
 160:  PR[77]=LPOS    ;
 161:  R[68:MAIN SPIN RAW X]=(PR[77,1]) ;
 162:  DO[107:MEAS LOAD COMP]=ON ;
 163:  PR[34,3:APP Z TOOL OFF]=75    ;
 164:L PR[77] 2000mm/sec CNT30 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 165:  R[40:RECOVER START PR]=17    ;
 166:J PR[17:CLEAR] 100% CNT40    ;
 167:  R[40:RECOVER START PR]=11    ;
 168:J PR[11] 100% CNT40    ;
 169:  R[40:RECOVER START PR]=3    ;
 170:J PR[3] 100% CNT20    ;
 171:  DO[351]=ON ;
 172:  MONITOR END MON_HAAS ;
 173:  IF (DI[345]) THEN ;
 174:  CALL HAAS_DOOR_CLOSE    ;
 175:  ENDIF ;
 176:  CALL HAAS_START    ;
 177:  R[40:RECOVER START PR]=23    ;
 178:J PR[23:MOVE TO MACH] 100% CNT50    ;
 179:  R[40:RECOVER START PR]=1    ;
 180:J PR[1:HOME POSITION] 100% CNT20    ;
 181:  JMP LBL[100] ;
 182:   ;
 183:  LBL[400:MEAS SUB LOAD] ;
 184:  CALL FAULT_MESSAGE('Robot is measuring raw part','X location in machine.') ;
 185:J PR[3] 100% FINE    ;
 186:  WAIT (DO[209:MACH READY FOR SERVICE])    ;
 187:  $WAITTMOUT=500 ;
 188:  MONITOR MON_HAAS ;
 189:  DO[351]=OFF ;
 190:J PR[12:JOINT OFFSET] 100% CNT50    ;
 191:  R[40:RECOVER START PR]=12    ;
 192:  UFRAME_NUM=5 ;
 193:  UTOOL_NUM=1 ;
 194:  CALL HAAS_ORIENT1_SUB    ;
 195:J PR[18:UT5] 100% CNT30    ;
 196:  --eg:SET SEARCH START AND STOP POSITIONS ;
 197:  PR[73]=PR[78]    ;
 198:  R[140:J4 POS]=R[97]-PR[78,1]    ;
 199:  PR[34,3:APP Z TOOL OFF]=R[140:J4 POS]    ;
 200:   ;
 201:  PR[74]=PR[78]    ;
 202:   ;
 203:  --eg:SET VALUES BASED ON ROOM INSIDE OF MACHINE ;
 204:  R[40:RECOVER START PR]=18    ;
 205:L PR[73] 2000mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 206:  R[140:J4 POS]=R[98]-PR[78,1]    ;
 207:  PR[34,3:APP Z TOOL OFF]=R[140:J4 POS]    ;
 208:  SKIP CONDITION RI[7:SPRING COMPRESSED]=ON+    ;
 209:L PR[74] R[86:SEARCH FAST SPD]mm/sec FINE Skip,LBL[580],PR[78]=LPOS Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 210:  PR[34,3:APP Z TOOL OFF]=1    ;
 211:L PR[78] 100mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 212:  PR[78]=LPOS    ;
 213:  WAIT    .10(sec) ;
 214:  R[70:SUB SPIN RAW X]=(PR[78,1]) ;
 215:  DO[107:MEAS LOAD COMP]=ON ;
 216:  PR[34,3:APP Z TOOL OFF]=75    ;
 217:L PR[78] 2000mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 218:  R[40:RECOVER START PR]=18    ;
 219:J PR[18:UT5] 100% CNT30    ;
 220:  R[40:RECOVER START PR]=12    ;
 221:J PR[12:JOINT OFFSET] 100% CNT60    ;
 222:  R[40:RECOVER START PR]=3    ;
 223:J PR[3] 100% CNT5    ;
 224:  DO[351]=ON ;
 225:  MONITOR END MON_HAAS ;
 226:  IF (!DO[87:AUTODOORS ENABLED]),CALL HAAS_DOOR_CLOSE ;
 227:  CALL HAAS_START    ;
 228:  R[40:RECOVER START PR]=23    ;
 229:J PR[23:MOVE TO MACH] 100% CNT50    ;
 230:  R[40:RECOVER START PR]=1    ;
 231:J PR[1:HOME POSITION] 100% CNT20    ;
 232:  JMP LBL[100] ;
 233:   ;
 234:  LBL[500:MEAS MAIN UNLOAD] ;
 235:  CALL FAULT_MESSAGE('Robot is measuring finished part','X location in machine.') ;
 236:  //J PR[23:MOVE TO MACH] 100% CNT50    ;
 237:  R[40:RECOVER START PR]=23    ;
 238:   ;
 239:J PR[3] 100% CNT30    ;
 240:  R[40:RECOVER START PR]=3    ;
 241:  UFRAME_NUM=5 ;
 242:  UTOOL_NUM=1 ;
 243:  WAIT (DO[209:MACH READY FOR SERVICE])    ;
 244:  $WAITTMOUT=500 ;
 245:  MONITOR MON_HAAS ;
 246:  DO[351]=OFF ;
 247:J PR[11] 100% CNT50    ;
 248:  R[40:RECOVER START PR]=11    ;
 249:  UFRAME_NUM=5 ;
 250:  UTOOL_NUM=1 ;
 251:  CALL HAAS_ORIENT1_MAIN    ;
 252:J PR[17:CLEAR] 100% CNT30    ;
 253:  R[40:RECOVER START PR]=17    ;
 254:  --eg:SET SEARCH START AND STOP POSITIONS ;
 255:  R[140:J4 POS]=R[95]-PR[75,1]    ;
 256:  PR[34,3:APP Z TOOL OFF]=R[140:J4 POS]    ;
 257:J PR[71] 100% CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 258:  R[140:J4 POS]=R[96]-PR[75,1]    ;
 259:  PR[34,3:APP Z TOOL OFF]=R[140:J4 POS]    ;
 260:  SKIP CONDITION RI[7:SPRING COMPRESSED]=ON+    ;
 261:L PR[72] R[86:SEARCH FAST SPD]mm/sec FINE Skip,LBL[580],PR[77]=LPOS Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 262:  PR[34,3:APP Z TOOL OFF]=1    ;
 263:L PR[77] 100mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 264:  WAIT    .10(sec) ;
 265:  PR[77]=LPOS    ;
 266:  PR[75,1]=PR[77,1]    ;
 267:  PR[75,2]=PR[77,2]    ;
 268:  PR[75,3]=PR[77,3]    ;
 269:  R[69:MAIN SPIN FIN X]=(PR[75,1]) ;
 270:  DO[108:MEAS UNLD COMP]=ON ;
 271:  PR[34,3:APP Z TOOL OFF]=75    ;
 272:L PR[77] 2000mm/sec CNT30 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 273:  R[40:RECOVER START PR]=17    ;
 274:J PR[17:CLEAR] 100% CNT40    ;
 275:  R[40:RECOVER START PR]=11    ;
 276:J PR[11] 100% CNT40    ;
 277:  R[40:RECOVER START PR]=3    ;
 278:J PR[3] 100% CNT20    ;
 279:  DO[351]=ON ;
 280:  MONITOR END MON_HAAS ;
 281:  JMP LBL[100] ;
 282:   ;
 283:  LBL[600:MEAS SUB UNLOAD] ;
 284:  CALL FAULT_MESSAGE('Robot is measuring finished part','X location in machine.') ;
 285:  //J PR[23:MOVE TO MACH] 100% CNT50    ;
 286:  R[40:RECOVER START PR]=23    ;
 287:J PR[3] 100% FINE    ;
 288:  R[40:RECOVER START PR]=3    ;
 289:  WAIT (DO[209:MACH READY FOR SERVICE])    ;
 290:  $WAITTMOUT=500 ;
 291:  MONITOR MON_HAAS ;
 292:  DO[351]=OFF ;
 293:J PR[12:JOINT OFFSET] 100% CNT50    ;
 294:  R[40:RECOVER START PR]=12    ;
 295:  UFRAME_NUM=5 ;
 296:  UTOOL_NUM=1 ;
 297:J PR[18:UT5] 100% CNT30    ;
 298:  --eg:SET SEARCH START AND STOP POSITIONS ;
 299:  PR[73]=PR[78]    ;
 300:  R[140:J4 POS]=R[97]-PR[78,1]    ;
 301:  PR[34,3:APP Z TOOL OFF]=R[140:J4 POS]    ;
 302:   ;
 303:  PR[74]=PR[78]    ;
 304:  --eg:SET VALUES BASED ON ROOM INSIDE OF MACHINE ;
 305:  R[40:RECOVER START PR]=18    ;
 306:L PR[73] 2000mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 307:  R[140:J4 POS]=R[98]-PR[78,1]    ;
 308:  PR[34,3:APP Z TOOL OFF]=R[140:J4 POS]    ;
 309:  SKIP CONDITION RI[7:SPRING COMPRESSED]=ON+    ;
 310:L PR[74] R[85:SEARCH SLOW SPD]mm/sec FINE Skip,LBL[580],PR[78]=LPOS Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 311:  PR[34,3:APP Z TOOL OFF]=1    ;
 312:L PR[78] 100mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 313:  WAIT    .10(sec) ;
 314:  PR[78]=LPOS    ;
 315:  PR[76,1]=PR[78,1]    ;
 316:  PR[76,2]=PR[78,2]    ;
 317:  PR[76,3]=PR[78,3]    ;
 318:  R[71:SUB SPIN FIN X]=(PR[76,1]) ;
 319:  DO[108:MEAS UNLD COMP]=ON ;
 320:  PR[34,3:APP Z TOOL OFF]=200    ;
 321:L PR[78] 2000mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
 322:  R[40:RECOVER START PR]=18    ;
 323:J PR[18:UT5] 100% CNT30    ;
 324:  R[40:RECOVER START PR]=12    ;
 325:J PR[12:JOINT OFFSET] 100% CNT60    ;
 326:  R[40:RECOVER START PR]=3    ;
 327:J PR[3] 100% CNT5    ;
 328:  DO[351]=ON ;
 329:  MONITOR END MON_HAAS ;
 330:  JMP LBL[100] ;
 331:   ;
 332:  LBL[700:SETUP TABLE] ;
 333:  --eg:IDENTIFIES TEMPLATE HEIGHT AND TYPE, TEMPLATE TYPE, AND PART
    :  DIMENSIONS ;
 334:  CALL PALLET_SETUP    ;
 335:  JMP LBL[100] ;
 336:   ;
 337:  LBL[800:HAAS START] ;
 338:  --eg:RUNS HAAS WHEN NO LOAD IS SELECTED. ;
 339:  DO[107:MEAS LOAD COMP]=ON ;
 340:  IF (!DO[87:AUTODOORS ENABLED]),CALL HAAS_DOOR_CLOSE ;
 341:  CALL HAAS_START    ;
 342:  JMP LBL[100] ;
 343:   ;
 344:  LBL[9999:END] ;
 345:  DO[102:NEW PART A]=OFF ;
 346:  DO[103:NEW PART B]=OFF ;
 347:  DO[105:MEAS LOAD ACTIVE]=OFF ;
 348:  DO[106:MEAS UNLOAD ACTIVE]=OFF ;
 349:  DO[107:MEAS LOAD COMP]=OFF ;
 350:  DO[108:MEAS UNLD COMP]=OFF ;
 351:   ;
 352:  R[40:RECOVER START PR]=1    ;
 353:J PR[1:HOME POSITION] 100% CNT5    ;
/POS
/END
