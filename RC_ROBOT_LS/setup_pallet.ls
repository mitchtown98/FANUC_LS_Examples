/PROG  SETUP_PALLET
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "SETUP PALLET";
PROG_SIZE	= 1391;
CREATE		= DATE 22-07-25  TIME 15:31:10;
MODIFIED	= DATE 23-01-07  TIME 06:34:10;
FILE_NAME	= PALLET_S;
VERSION		= 0;
LINE_COUNT	= 39;
MEMORY_SIZE	= 1955;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !PROGRAM WILL COMPLETE PALLET     ;
   2:  !SETUP MEASUREMENTS WITH LASER    ;
   3:  !AND SPRING PRESSURE PLATE        ;
   4:  LBL[10:PALLET SETUP] ;
   5:  !CHECK REGRIP IS CLEAR            ;
   6:  IF (DO[34:REGRIP CHECKED]=OFF) THEN ;
   7:  CALL REGRIP_CHECK    ;
   8:  ENDIF ;
   9:  !MEASURE PALLET HEIGHT            ;
  10:  IF (DO[52:PALLET HEIGHT FOUND]=OFF) THEN ;
  11:  CALL SETUP_TMPLT_HEIGHT    ;
  12:  ENDIF ;
  13:  !CHECK TEMPLATE TYPE              ;
  14:  IF (DO[51:PALLET TYPE FOUND]=OFF AND DO[52:PALLET HEIGHT FOUND]=ON) THEN ;
  15:  CALL SETUP_TMPLT_ID    ;
  16:  ENDIF ;
  17:  !TEMPLATE SETUP                   ;
  18:  IF (DO[51:PALLET TYPE FOUND]=ON AND DO[52:PALLET HEIGHT FOUND]=ON) THEN ;
  19:  SELECT R[1:ACTIVE TEMPLATE]=2,CALL TEMPLATE_2 ;
  20:         =3,CALL TEMPLATE_3 ;
  21:         =4,CALL TEMPLATE_4 ;
  22:         =5,CALL TEMPLATE_5 ;
  23:         =6,CALL TEMPLATE_6 ;
  24:  ENDIF ;
  25:  !MEASURE ROUGH PART LENGTH        ;
  26:  IF (DO[51:PALLET TYPE FOUND]=ON AND DO[52:PALLET HEIGHT FOUND]=ON AND DO[53:PART LENGTH FOUND]=OFF) THEN ;
  27:  CALL SETUP_LENGTH    ;
  28:  ENDIF ;
  29:  !MEASURE PART DIAMETER            ;
  30:  IF (DO[51:PALLET TYPE FOUND]=ON AND DO[52:PALLET HEIGHT FOUND]=ON AND DO[53:PART LENGTH FOUND]=ON AND DO[54:PART DIAMETER FOUND]=OFF AND DO[115:FINE LENGTH CHECKED]=OFF) THEN ;
  31:  CALL SETUP_DIAMETER    ;
  32:  ENDIF ;
  33:  !MEASURE FINE PART LENGTH         ;
  34:  IF (DO[51:PALLET TYPE FOUND]=ON AND DO[52:PALLET HEIGHT FOUND]=ON AND DO[53:PART LENGTH FOUND]=ON AND DO[54:PART DIAMETER FOUND]=ON AND DO[115:FINE LENGTH CHECKED]=OFF) THEN ;
  35:  CALL SETUP_FINE_LENGTH    ;
  36:  ENDIF ;
  37:  !GO BACK TO TOP IF SETUP          ;
  38:  !IS NOT COMPLETE                  ;
  39:  IF (DO[50:PALLET SETUP COMPLETE]=OFF),JMP LBL[10] ;
/POS
/END
