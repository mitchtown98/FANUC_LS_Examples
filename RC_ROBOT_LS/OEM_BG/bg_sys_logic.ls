/PROG  BG_SYS_LOGIC
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "BACKGROUND";
PROG_SIZE	= 4757;
CREATE		= DATE 21-03-09  TIME 10:58:32;
MODIFIED	= DATE 23-11-18  TIME 08:23:42;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 153;
MEMORY_SIZE	= 5125;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  !FAULT RESET                      ;
   2:  F[104:FAULT RESET]=(DI[43:ROBOT FAULT RESET]) ;
   3:  !HOME POSITION CHECK              ;
   4:  IF (DO[145:AT HOME]=ON OR DO[146:TABLE PERCH]=ON OR DO[147:MACHINE PERCH]=ON OR DO[148:PERCH SPARE]=ON OR DO[149:PERCH SPARE]=ON OR DO[150:SINGULARITY]=ON OR DO[151:PERCH SPARE]=ON OR 
    :  DO[152:AT MAINTENANCE]=ON) THEN ;
   5:  DO[160:ROBOT PERCHED]=ON ;
   6:  DO[217:ROBOT RETRACTED COM]=ON ;
   7:  IF (DO[145:AT HOME]=ON),R[40:RECOVER START PR]=(1) ;
   8:  ELSE ;
   9:  DO[160:ROBOT PERCHED]=OFF ;
  10:  ENDIF ;
  11:  !STEPMODE STATUS                  ;
  12:  DO[30:IN STEP MODE]=($SSR.$SINGLESTEP) ;
  13:  !MONITOR IF ROBOT IS JOGGED       ;
  14:  IF (DO[28:ROBOT JOGGED]=OFF AND $MOR_GRP[1].$JOGGED=1) THEN ;
  15:  DO[28:ROBOT JOGGED]=ON ;
  16:  ENDIF ;
  17:  IF (DO[28:ROBOT JOGGED]=ON AND DO[145:AT HOME]=ON) THEN ;
  18:  DO[28:ROBOT JOGGED]=OFF ;
  19:  ENDIF ;
  20:  IF (DO[31:CYCLE STOP ACTIVE]=ON),DO[32:PURGE ACTIVE]=(ON) ;
  21:  !CYCLE STOP AND PURGE CONTROL     ;
  22:  IF (DI[31:CYCLE STOP REQ]=ON OR DI[32:PURGE REQ]=ON OR DI[103:NEW PART HMI PB]=ON),F[1:I/O ONE SHOT 1]=PULSE   ;
  23:  IF (F[1:I/O ONE SHOT 1]=ON AND DI[31:CYCLE STOP REQ]=ON),DO[31:CYCLE STOP ACTIVE]=(!DO[31:CYCLE STOP ACTIVE]) ;
  24:  IF (F[1:I/O ONE SHOT 1]=ON AND DI[32:PURGE REQ]=ON),DO[32:PURGE ACTIVE]=(!DO[32:PURGE ACTIVE]) ;
  25:  !CONTROL LOAD COMPLETE SIGNALS    ;
  26:  IF (DI[47:LOAD COMPLETE HMI]=ON AND DO[42:TABLE READY FOR SERVICE]=ON),DO[41:TABLE READY TO ROTATE]=(ON) ;
  27:  IF (DI[47:LOAD COMPLETE HMI]=ON AND DO[42:TABLE READY FOR SERVICE]=ON AND DI[1:TABLE AT A POSITION]=ON),DO[26:B SIDE LOAD COMPLETE]=(ON) ;
  28:  IF (DI[47:LOAD COMPLETE HMI]=ON AND DO[42:TABLE READY FOR SERVICE]=ON AND DI[2:TABLE AT B POSITION]=ON),DO[25:A SIDE LOAD COMPLETE]=(ON) ;
  29:  IF (DI[45:ABORT LOAD SIDE]=ON AND DO[25:A SIDE LOAD COMPLETE]=ON AND DI[2:TABLE AT B POSITION]=ON),DO[25:A SIDE LOAD COMPLETE]=(OFF) ;
  30:  IF (DI[45:ABORT LOAD SIDE]=ON AND DO[26:B SIDE LOAD COMPLETE]=ON AND DI[1:TABLE AT A POSITION]=ON),DO[26:B SIDE LOAD COMPLETE]=(OFF) ;
  31:  IF (DI[45:ABORT LOAD SIDE]=ON AND DO[41:TABLE READY TO ROTATE]=ON),DO[41:TABLE READY TO ROTATE]=(OFF) ;
  32:  !WRITES ACTIVE FRAMES TO REGISTE  ;
  33:  R[60:CURRENT UFRAME]=($MNUFRAMENUM[1]) ;
  34:  R[61:CURRENT UTOOL]=($MNUTOOLNUM[1]) ;
  35:  !SPEED CONTROL FOR AUTO           ;
  36:  IF (R[41:GLOBAL SPEED]<10 OR R[41:GLOBAL SPEED]>100),R[41:GLOBAL SPEED]=(100) ;
  37:  IF (R[48:MACH SPEED]<10 OR R[48:MACH SPEED]>175),R[48:MACH SPEED]=(100) ;
  38:  IF (R[41:GLOBAL SPEED]>=10 AND R[41:GLOBAL SPEED]<=100 AND UO[8:TP enabled]=OFF AND DI[3:SCANNER WARNING CLEAR]=ON AND DI[4:SCANNER BLOCKED]=OFF AND GO[1:ACTIVE PNS NUM]<>2 AND DI[470:NOT IN MACHINE]=ON) THEN ;
  39:  R[42:REDUCED SPEED]=R[41:GLOBAL SPEED]/2.875    ;
  40:  ENDIF ;
  41:  IF (UO[8:TP enabled]=OFF AND DO[202:RECOVER ACTIVE]=ON AND DI[3:SCANNER WARNING CLEAR]=ON) THEN ;
  42:  R[42:REDUCED SPEED]=15    ;
  43:  ENDIF ;
  44:  IF (UO[8:TP enabled]=OFF AND DI[3:SCANNER WARNING CLEAR]=OFF) THEN ;
  45:  R[42:REDUCED SPEED]=10    ;
  46:  ENDIF ;
  47:  IF (UO[8:TP enabled]=OFF AND DO[150:SINGULARITY]=ON) THEN ;
  48:  R[42:REDUCED SPEED]=20    ;
  49:  ENDIF ;
  50:  !LOAD/UNLOAD MACHINE SPEED        ;
  51:  IF (UO[8:TP enabled]=OFF AND R[48:MACH SPEED]>=10 AND R[48:MACH SPEED]<=175 AND DO[104:NEW PART ACTIVE]=OFF AND DI[470:NOT IN MACHINE]=OFF AND DI[3:SCANNER WARNING CLEAR]=ON AND DI[4:SCANNER BLOCKED]=OFF AND 
    :  GO[1:ACTIVE PNS NUM]=1),R[42:REDUCED SPEED]=(R[48:MACH SPEED]/2.5) ;
  52:  IF (UO[8:TP enabled]=OFF),$MCR.$GENOVERRIDE=(R[42:REDUCED SPEED]) ;
  53:  R[45:CURRENT OVERRIDE]=($MCR.$GENOVERRIDE) ;
  54:  --eg:CALCULATE REGISTERS USED TO ADJUST MOTION SPEED. THESE CALCULATIONS
    :  ARE FOR LASER MOTION TO ENSURE SPEED IS CONSTANT REGARDLESS OF OVERRIDE
    :  DURING MEASUREMENTS ;
  55:  R[87:MAX SPD RATIO]=R[45:CURRENT OVERRIDE]/100    ;
  56:  R[90:CALC SLOW]=R[88:NOM SLOW mm/sec]/R[87:MAX SPD RATIO]    ;
  57:  R[91:CALC FAST]=R[89:NOM FAST mm/sec]/R[87:MAX SPD RATIO]    ;
  58:  R[85:SEARCH SLOW SPD]=R[90:CALC SLOW]    ;
  59:  R[86:SEARCH FAST SPD]=R[91:CALC FAST]    ;
  60:  !CONTROL STATUS LIGHTS            ;
  61:  IF ((DI[1:TABLE AT A POSITION]=ON AND DO[37:TABLE A READY]=OFF AND DO[41:TABLE READY TO ROTATE]=OFF AND DO[39:TABLE SIDE A COMPLETE]=ON AND DO[104:NEW PART ACTIVE]=OFF) OR (DI[2:TABLE AT B POSITION]=ON AND 
    :  DO[38:TABLE B READY]=OFF AND DO[41:TABLE READY TO ROTATE]=OFF AND DO[40:TABLE SIDE B COMPLETE]=OFF AND DO[104:NEW PART ACTIVE]=OFF)) THEN ;
  62:  DO[3:MATERIAL LAMP]=(DO[27:Heartbeat]) ;
  63:  ELSE ;
  64:  IF (DO[104:NEW PART ACTIVE]=ON) THEN ;
  65:  DO[3:MATERIAL LAMP]=ON ;
  66:  ELSE ;
  67:  DO[3:MATERIAL LAMP]=OFF ;
  68:  ENDIF ;
  69:  ENDIF ;
  70:  IF (UO[6:Fault]=ON AND DO[200:Input Simulated]=OFF AND DO[201:Output Simulated]=OFF AND DO[104:NEW PART ACTIVE]=OFF) THEN ;
  71:  DO[2:FAULT LAMP]=ON ;
  72:  ENDIF ;
  73:  IF (UO[6:Fault]=ON) AND (DO[200:Input Simulated]=ON OR DO[201:Output Simulated]=ON OR DO[104:NEW PART ACTIVE]=ON) THEN ;
  74:  DO[2:FAULT LAMP]=(DO[27:Heartbeat]) ;
  75:  ENDIF ;
  76:  IF (UO[6:Fault]=OFF AND DO[200:Input Simulated]=OFF AND DO[201:Output Simulated]=OFF) THEN ;
  77:  DO[2:FAULT LAMP]=OFF ;
  78:  ENDIF ;
  79:  DO[111:ROBOT FAULTED]=(UO[6:Fault]) ;
  80:  DO[110:ROBOT RUNNING]=(UO[10:Busy]) ;
  81:  IF (UO[10:Busy]=ON AND DO[3:MATERIAL LAMP]=OFF) THEN ;
  82:  DO[4:ROBOT RUNNING]=ON ;
  83:  ELSE ;
  84:  DO[4:ROBOT RUNNING]=OFF ;
  85:  ENDIF ;
  86:  --eg:CALCULATE CURRENT SLOT NUMBER BASED ON CURRENT ROW AND COLUMN VALUES ;
  87:  R[6:CUR PICK SLOT #]=(((R[2:CUR ROW PICK (X)]-1)*(R[11:NUMBER OF COLS]))+(R[3:CUR COL PICK (Y)])) ;
  88:  R[7:CUR DROP SLOT #]=(((R[4:CUR ROW DROP (X)]-1)*(R[11:NUMBER OF COLS]))+(R[5:CUR COL DROP (Y)])) ;
  89:  --eg:SET REGISTERS TO USE AS INDERCT VALUES FOR THE CURRENT ROW THE ROBOT
    :  IS WORKING ON TO DETERMINE START/STOP SLOT ON TABLE ;
  90:  R[36:PICK START REG]=R[2:CUR ROW PICK (X)]+R[2:CUR ROW PICK (X)]+12    ;
  91:  R[37:PICK STOP REG]=R[36:PICK START REG]+1    ;
  92:  R[38:DROP START REG]=R[4:CUR ROW DROP (X)]+R[4:CUR ROW DROP (X)]+12    ;
  93:  R[39:DROP STOP REG]=R[38:DROP START REG]+1    ;
  94:  !SET TABLE READY FOR SERVICE SIG  ;
  95:  IF (DI[6:TABLE LOCKED IN POSITION]=ON) AND ((DI[1:TABLE AT A POSITION]=ON AND DI[2:TABLE AT B POSITION]=OFF) OR (DI[1:TABLE AT A POSITION]=OFF AND DI[2:TABLE AT B POSITION]=ON)) THEN ;
  96:  DO[42:TABLE READY FOR SERVICE]=ON ;
  97:  ELSE ;
  98:  DO[42:TABLE READY FOR SERVICE]=OFF ;
  99:  ENDIF ;
 100:  IF (DI[1:TABLE AT A POSITION]=ON AND DO[43:TABLE PICK COMPLETE]=ON AND DO[44:TABLE DROP COMPLETE]=ON) THEN ;
 101:  DO[39:TABLE SIDE A COMPLETE]=ON ;
 102:  ELSE ;
 103:  DO[39:TABLE SIDE A COMPLETE]=OFF ;
 104:  ENDIF ;
 105:  IF (DI[2:TABLE AT B POSITION]=ON AND DO[43:TABLE PICK COMPLETE]=ON AND DO[44:TABLE DROP COMPLETE]=ON) THEN ;
 106:  DO[40:TABLE SIDE B COMPLETE]=ON ;
 107:  ELSE ;
 108:  DO[40:TABLE SIDE B COMPLETE]=OFF ;
 109:  ENDIF ;
 110:  IF (DI[1:TABLE AT A POSITION]=ON AND DO[39:TABLE SIDE A COMPLETE]=ON) THEN ;
 111:  DO[37:TABLE A READY]=OFF ;
 112:  ENDIF ;
 113:  IF (DI[2:TABLE AT B POSITION]=ON AND DO[40:TABLE SIDE B COMPLETE]=ON) THEN ;
 114:  DO[38:TABLE B READY]=OFF ;
 115:  ENDIF ;
 116:  !SET TABLE IN POSITION SIGNAL     ;
 117:  IF ((DI[1:TABLE AT A POSITION]=ON AND DI[2:TABLE AT B POSITION]=OFF) OR (DI[1:TABLE AT A POSITION]=OFF AND DI[2:TABLE AT B POSITION]=ON)) THEN ;
 118:  DO[35:TABLE IN POSITION]=ON ;
 119:  ELSE ;
 120:  DO[35:TABLE IN POSITION]=OFF ;
 121:  ENDIF ;
 122:  !UNLOCK TABLE IF NOT IN POSITION  ;
 123:  IF (DO[35:TABLE IN POSITION]=OFF) THEN ;
 124:  DO[5:TURN TABLE UNLOCK SOL]=ON ;
 125:  ENDIF ;
 126:  !MANUAL TABLE LOCK CONTROLS       ;
 127:  IF (DI[38:TABLE UNLOCK (HMI)]=ON AND UO[10:Busy]=OFF AND DO[5:TURN TABLE UNLOCK SOL]=OFF AND DO[145:AT HOME]=ON),DO[5:TURN TABLE UNLOCK SOL]=(ON) ;
 128:  IF (DI[39:TABLE LOCK (HMI)]=ON AND UO[10:Busy]=OFF AND DO[5:TURN TABLE UNLOCK SOL]=ON AND DO[145:AT HOME]=ON AND DO[35:TABLE IN POSITION]=ON),DO[5:TURN TABLE UNLOCK SOL]=(OFF) ;
 129:  !SET UI SIGNAL FROM DIN           ;
 130:  IF (DI[42:ROBOT HOLD]=OFF OR DI[7:MACH CNTRL SAFE]=OFF) THEN ;
 131:  F[101:*HOLD]=(OFF) ;
 132:  ELSE ;
 133:  F[101:*HOLD]=(ON) ;
 134:  ENDIF ;
 135:  F[100:*IMSTP]=(DI[8:SYSTEM AIR OK]) ;
 136:  F[102:*SFSPD]=(DI[8:SYSTEM AIR OK]) ;
 137:  F[107:ENABLE]=(DI[8:SYSTEM AIR OK]) ;
 138:  !AIR FILTER TIMER                 ;
 139:  IF (R[302:HOURS]>0 AND DO[27:Heartbeat]=ON),F[90:COUNT AIR FILTER]=PULSE   ;
 140:  IF (F[90:COUNT AIR FILTER]),R[303:SECONDS]=(R[303:SECONDS]-1) ;
 141:  IF (R[303:SECONDS]=0) THEN ;
 142:  R[302:HOURS]=R[302:HOURS]-1    ;
 143:  R[303:SECONDS]=3600    ;
 144:  ENDIF ;
 145:  --eg:COLLISION AT DROP ;
 146:  IF (R[40:RECOVER START PR]>=65 AND R[40:RECOVER START PR]<=69 AND DO[177:COLLISION]),DO[114:COLLISION AT DROP]=(ON) ;
 147:  --eg:SAVE CURRENT JOINT VALUES TO REGISTERS ;
 148:  R[137:J1 POS]=$SCR_GRP[1].$MCH_ANG[1] ;
 149:  R[138:J2 POS]=$SCR_GRP[1].$MCH_ANG[2] ;
 150:  R[139:J3 POS]=$SCR_GRP[1].$MCH_ANG[3] ;
 151:  R[140:J4 POS]=$SCR_GRP[1].$MCH_ANG[4] ;
 152:  R[141:J5 POS]=$SCR_GRP[1].$MCH_ANG[5] ;
 153:  R[142:J6 POS]=$SCR_GRP[1].$MCH_ANG[6] ;
/POS
/END
