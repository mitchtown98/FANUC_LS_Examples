/PROG  BG_GRIPPERS
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "BG GRIP CONTROL";
PROG_SIZE	= 3709;
CREATE		= DATE 22-08-17  TIME 14:25:52;
MODIFIED	= DATE 23-09-28  TIME 13:29:40;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 185;
MEMORY_SIZE	= 3981;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  --eg:CONTROLS ALL GRIPPER RELATED LOGIC. MONITORS PART DESTITION OF WHATS
    :  IN THE GRIPPERS, SENDS STATUS BITS FOR HMI DISPLAY, AND CONTAINS
    :  CONTROLS FOR HMI MANUAL OPERATION. ;
   2:   ;
   3:  --eg:SET ID/OD GRIPPING BASED ON INPUTS FROM HMI PB. ALLOW CHANGES ONLY
    :  WHEN NO SIDES ARE IN OPERATION.   ;
   4:   ;
   5:  IF (DO[145:AT HOME]=ON AND UO[3:Prg running]=OFF) THEN ;
   6:  DO[112:ROBOT OK TO CHANGEOVER]=ON ;
   7:  ELSE ;
   8:  DO[112:ROBOT OK TO CHANGEOVER]=OFF ;
   9:  ENDIF ;
  10:   ;
  11:  IF (DO[112:ROBOT OK TO CHANGEOVER]) THEN ;
  12:  IF (DO[63:NO LOAD ON]=ON) THEN ;
  13:  DO[117:T1 ID GRIP]=OFF ;
  14:  DO[116:T1 OD GRIP]=ON ;
  15:  RO[2:T1 CLOSE]=OFF ;
  16:  RO[1:T1 OPEN]=ON ;
  17:  ENDIF ;
  18:   ;
  19:  IF (DO[68:NO UNLOAD ON]=ON) THEN ;
  20:  DO[119:T2 ID GRIP]=OFF ;
  21:  DO[118:T2 OD GRIP]=ON ;
  22:  RO[4:T2 CLOSE]=OFF ;
  23:  RO[3:T2 OPEN]=ON ;
  24:  ENDIF ;
  25:   ;
  26:  IF (DI[116:T1 OD GRIP] AND !DO[116:T1 OD GRIP]) THEN ;
  27:  DO[117:T1 ID GRIP]=OFF ;
  28:  DO[116:T1 OD GRIP]=ON ;
  29:  RO[2:T1 CLOSE]=OFF ;
  30:  RO[1:T1 OPEN]=ON ;
  31:  ENDIF ;
  32:   ;
  33:  IF (DI[117:T1 ID GRIP] AND !DO[117:T1 ID GRIP]) THEN ;
  34:  DO[116:T1 OD GRIP]=OFF ;
  35:  DO[117:T1 ID GRIP]=ON ;
  36:  RO[1:T1 OPEN]=OFF ;
  37:  RO[2:T1 CLOSE]=ON ;
  38:  ENDIF ;
  39:   ;
  40:  IF (DI[118:T2 OD GRIP] AND !DO[118:T2 OD GRIP]) THEN ;
  41:  DO[119:T2 ID GRIP]=OFF ;
  42:  DO[118:T2 OD GRIP]=ON ;
  43:  RO[4:T2 CLOSE]=OFF ;
  44:  RO[3:T2 OPEN]=ON ;
  45:  ENDIF ;
  46:   ;
  47:  IF (DI[119:T2 ID GRIP] AND !DO[119:T2 ID GRIP]) THEN ;
  48:  DO[118:T2 OD GRIP]=OFF ;
  49:  DO[119:T2 ID GRIP]=ON ;
  50:  RO[3:T2 OPEN]=OFF ;
  51:  RO[4:T2 CLOSE]=ON ;
  52:  ENDIF ;
  53:  ENDIF ;
  54:   ;
  55:  --eg:SET GRIPPER STATUS BASED ON ID/OD GRIPPING ;
  56:  IF ((DO[116:T1 OD GRIP] AND !DO[117:T1 ID GRIP] AND !RO[1:T1 OPEN] AND RO[2:T1 CLOSE] AND !RI[1:T1 FULLY OPENED] AND !RI[2:T1 FULLY CLOSED]) OR (!DO[116:T1 OD GRIP] AND DO[117:T1 ID GRIP] AND RO[1:T1 OPEN] AND !RO[2:T1 CLOSE] 
    :  AND !RI[1:T1 FULLY OPENED] AND !RI[2:T1 FULLY CLOSED])) THEN ;
  57:  DO[174:PART IN T1]=ON ;
  58:  ELSE ;
  59:  DO[174:PART IN T1]=OFF ;
  60:  ENDIF ;
  61:   ;
  62:  IF ((DO[118:T2 OD GRIP] AND !DO[119:T2 ID GRIP] AND !RO[3:T2 OPEN] AND RO[4:T2 CLOSE] AND !RI[3:T2 FULLY OPENED] AND !RI[4:T2 FULLY CLOSED]) OR (!DO[118:T2 OD GRIP] AND DO[119:T2 ID GRIP] AND RO[3:T2 OPEN] AND !RO[4:T2 CLOSE] 
    :  AND !RI[3:T2 FULLY OPENED] AND !RI[4:T2 FULLY CLOSED])) THEN ;
  63:  DO[175:PART IN T2]=ON ;
  64:  ELSE ;
  65:  DO[175:PART IN T2]=OFF ;
  66:  ENDIF ;
  67:   ;
  68:  IF ((DO[116:T1 OD GRIP] AND !DO[117:T1 ID GRIP] AND !RO[1:T1 OPEN] AND RO[2:T1 CLOSE] AND !RI[1:T1 FULLY OPENED] AND RI[2:T1 FULLY CLOSED]) OR (!DO[116:T1 OD GRIP] AND DO[117:T1 ID GRIP] AND RO[1:T1 OPEN] AND !RO[2:T1 CLOSE] 
    :  AND RI[1:T1 FULLY OPENED] AND !RI[2:T1 FULLY CLOSED])) THEN ;
  69:  DO[170:T1 OVERTRAVEL]=ON ;
  70:  ELSE ;
  71:  DO[170:T1 OVERTRAVEL]=OFF ;
  72:  ENDIF ;
  73:   ;
  74:  IF ((DO[116:T1 OD GRIP] AND !DO[117:T1 ID GRIP] AND RO[1:T1 OPEN] AND !RO[2:T1 CLOSE] AND RI[1:T1 FULLY OPENED] AND !RI[2:T1 FULLY CLOSED]) OR (!DO[116:T1 OD GRIP] AND DO[117:T1 ID GRIP] AND !RO[1:T1 OPEN] AND RO[2:T1 CLOSE] 
    :  AND !RI[1:T1 FULLY OPENED] AND RI[2:T1 FULLY CLOSED])) THEN ;
  75:  DO[171:T1 UNGRIPPED]=ON ;
  76:  ELSE ;
  77:  DO[171:T1 UNGRIPPED]=OFF ;
  78:  ENDIF ;
  79:   ;
  80:  IF ((DO[118:T2 OD GRIP] AND !DO[119:T2 ID GRIP] AND !RO[3:T2 OPEN] AND RO[4:T2 CLOSE] AND !RI[3:T2 FULLY OPENED] AND RI[4:T2 FULLY CLOSED]) OR (!DO[118:T2 OD GRIP] AND DO[119:T2 ID GRIP] AND RO[3:T2 OPEN] AND !RO[4:T2 CLOSE] 
    :  AND RI[3:T2 FULLY OPENED] AND !RI[4:T2 FULLY CLOSED])) THEN ;
  81:  DO[172:T2 OVERTRAVEL]=ON ;
  82:  ELSE ;
  83:  DO[172:T2 OVERTRAVEL]=OFF ;
  84:  ENDIF ;
  85:   ;
  86:  IF ((DO[118:T2 OD GRIP] AND !DO[119:T2 ID GRIP] AND RO[3:T2 OPEN] AND !RO[4:T2 CLOSE] AND RI[3:T2 FULLY OPENED] AND !RI[4:T2 FULLY CLOSED]) OR (!DO[118:T2 OD GRIP] AND DO[119:T2 ID GRIP] AND !RO[3:T2 OPEN] AND RO[4:T2 CLOSE] 
    :  AND !RI[3:T2 FULLY OPENED] AND RI[4:T2 FULLY CLOSED])) THEN ;
  87:  DO[173:T2 UNGRIPPED]=ON ;
  88:  ELSE ;
  89:  DO[173:T2 UNGRIPPED]=OFF ;
  90:  ENDIF ;
  91:   ;
  92:  IF (DO[171:T1 UNGRIPPED] AND DO[173:T2 UNGRIPPED]) THEN ;
  93:  DO[176:ALL TOOLS UNGRIPPED]=ON ;
  94:  ELSE ;
  95:  DO[176:ALL TOOLS UNGRIPPED]=OFF ;
  96:  ENDIF ;
  97:   ;
  98:  --eg:CLEAR PART TRACKING BITS WHEN TOOL IS UNGRIPPED. ;
  99:  IF (UO[10:Busy]) THEN ;
 100:  IF (DO[171:T1 UNGRIPPED]) THEN ;
 101:  GO[10:PART TRACK TOOL 1]=0 ;
 102:  ENDIF ;
 103:   ;
 104:  IF (DO[173:T2 UNGRIPPED]) THEN ;
 105:  GO[11:PART TRACK TOOL 2]=0 ;
 106:  ENDIF ;
 107:  ENDIF ;
 108:   ;
 109:  --eg:MANUAL HMI CONTROLS ;
 110:  IF (DI[120:T1 OPEN HMI PB] OR DI[121:T1 CLOSE HMI PB] OR DI[122:T2 OPEN HMI PB] OR DI[123:T2 CLOSE HMI PB]),F[2:I/O ONE SHOT]=PULSE   ;
 111:   ;
 112:  IF (DI[120:T1 OPEN HMI PB] AND !UO[10:Busy] AND F[2:I/O ONE SHOT]) THEN ;
 113:  RO[2:T1 CLOSE]=OFF ;
 114:  RO[1:T1 OPEN]=ON ;
 115:  ENDIF ;
 116:   ;
 117:  IF (DI[121:T1 CLOSE HMI PB] AND !UO[10:Busy] AND !DO[174:PART IN T1] AND F[2:I/O ONE SHOT]) THEN ;
 118:  RO[1:T1 OPEN]=OFF ;
 119:  RO[2:T1 CLOSE]=ON ;
 120:  ENDIF ;
 121:   ;
 122:  IF (DI[122:T2 OPEN HMI PB] AND !UO[10:Busy] AND F[2:I/O ONE SHOT]) THEN ;
 123:  RO[4:T2 CLOSE]=OFF ;
 124:  RO[3:T2 OPEN]=ON ;
 125:  ENDIF ;
 126:   ;
 127:  IF (DI[123:T2 CLOSE HMI PB] AND !UO[10:Busy] AND F[2:I/O ONE SHOT] AND !DO[175:PART IN T2]) THEN ;
 128:  RO[3:T2 OPEN]=OFF ;
 129:  RO[4:T2 CLOSE]=ON ;
 130:  ENDIF ;
 131:   ;
 132:  --eg:STATUS BITS FOR HMI DISPLAY ;
 133:  DO[161:T1 OPEN SOL STATUS]=(RO[1:T1 OPEN]) ;
 134:  DO[162:T1 CLOSE SOL STATUS]=(RO[2:T1 CLOSE]) ;
 135:  DO[163:T2 OPEN SOL STATUS]=(RO[3:T2 OPEN]) ;
 136:  DO[164:T2 CLOSE SOL STATUS]=(RO[4:T2 CLOSE]) ;
 137:  DO[165:T1 OPEN SENSOR STATUS]=(RI[1:T1 FULLY OPENED]) ;
 138:  DO[166:T1 CLOSE SENSOR STATUS]=(RI[2:T1 FULLY CLOSED]) ;
 139:  DO[167:T2 OPEN SENSOR STATUS]=(RI[3:T2 FULLY OPENED]) ;
 140:  DO[168:T2 CLOSE SENSOR STATUS]=(RI[4:T2 FULLY CLOSED]) ;
 141:   ;
 142:  --eg:MANUAL PB LOGIC TO OPEN/CLOSE GRIPPERS AT MAINT POSITION. ;
 143:  IF (DO[152:AT MAINTENANCE]) THEN ;
 144:   ;
 145:  IF (RI[1:T1 FULLY OPENED] AND RI[5:MAN TOOL 1 PB]) THEN ;
 146:  RO[1:T1 OPEN]=OFF ;
 147:  RO[2:T1 CLOSE]=ON ;
 148:  ENDIF ;
 149:   ;
 150:  IF (DO[174:PART IN T1] AND RI[5:MAN TOOL 1 PB] AND DO[116:T1 OD GRIP]) THEN ;
 151:  RO[2:T1 CLOSE]=OFF ;
 152:  RO[1:T1 OPEN]=ON ;
 153:  ENDIF ;
 154:   ;
 155:  IF (DO[174:PART IN T1] AND RI[5:MAN TOOL 1 PB] AND DO[117:T1 ID GRIP]) THEN ;
 156:  RO[1:T1 OPEN]=OFF ;
 157:  RO[2:T1 CLOSE]=ON ;
 158:  ENDIF ;
 159:   ;
 160:  IF (RI[2:T1 FULLY CLOSED] AND RI[5:MAN TOOL 1 PB]) THEN ;
 161:  RO[2:T1 CLOSE]=OFF ;
 162:  RO[1:T1 OPEN]=ON ;
 163:  ENDIF ;
 164:   ;
 165:  IF (RI[3:T2 FULLY OPENED] AND RI[6:MAN TOOL 2 PB]) THEN ;
 166:  RO[3:T2 OPEN]=OFF ;
 167:  RO[4:T2 CLOSE]=ON ;
 168:  ENDIF ;
 169:   ;
 170:  IF (DO[175:PART IN T2] AND RI[6:MAN TOOL 2 PB] AND DO[118:T2 OD GRIP]) THEN ;
 171:  RO[4:T2 CLOSE]=OFF ;
 172:  RO[3:T2 OPEN]=ON ;
 173:  ENDIF ;
 174:   ;
 175:  IF (DO[175:PART IN T2] AND RI[6:MAN TOOL 2 PB] AND DO[119:T2 ID GRIP]) THEN ;
 176:  RO[3:T2 OPEN]=OFF ;
 177:  RO[4:T2 CLOSE]=ON ;
 178:  ENDIF ;
 179:   ;
 180:  IF (RI[4:T2 FULLY CLOSED] AND RI[6:MAN TOOL 2 PB]) THEN ;
 181:  RO[4:T2 CLOSE]=OFF ;
 182:  RO[3:T2 OPEN]=ON ;
 183:  ENDIF ;
 184:  ENDIF ;
 185:   ;
/POS
/END
