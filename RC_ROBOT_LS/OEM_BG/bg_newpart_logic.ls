/PROG  BG_NEWPART_LOGIC
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 3385;
CREATE		= DATE 22-06-24  TIME 14:11:42;
MODIFIED	= DATE 23-11-21  TIME 01:37:46;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 142;
MEMORY_SIZE	= 3701;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  !TURN ON NEW PART ACTIVE IF       ;
   2:  !EITHER SIDE IS IN NEW PART MODE  ;
   3:  IF (DI[103:NEW PART HMI PB]=ON AND UO[3:Prg running]=OFF) THEN ;
   4:  IF (DI[1:TABLE AT A POSITION]=ON OR DI[2:TABLE AT B POSITION]=ON) THEN ;
   5:  DO[104:NEW PART ACTIVE]=ON ;
   6:  IF (DI[1:TABLE AT A POSITION]=OFF AND DO[104:NEW PART ACTIVE]=ON),DO[102:NEW PART A]=(ON) ;
   7:  IF (DI[1:TABLE AT A POSITION]=OFF AND DO[104:NEW PART ACTIVE]=ON),DO[103:NEW PART B]=(OFF) ;
   8:  IF (DI[2:TABLE AT B POSITION]=OFF AND DO[104:NEW PART ACTIVE]=ON),DO[103:NEW PART B]=(ON) ;
   9:  IF (DI[2:TABLE AT B POSITION]=OFF AND DO[104:NEW PART ACTIVE]=ON),DO[102:NEW PART A]=(OFF) ;
  10:  DO[51:PALLET TYPE FOUND]=OFF ;
  11:  DO[52:PALLET HEIGHT FOUND]=OFF ;
  12:  DO[53:PART LENGTH FOUND]=OFF ;
  13:  DO[54:PART DIAMETER FOUND]=OFF ;
  14:  DO[115:FINE LENGTH CHECKED]=OFF ;
  15:  ENDIF ;
  16:  ENDIF ;
  17:  --eg:CLEAR NEW PART ACTIVE IF BOTH ON ;
  18:  IF (DO[104:NEW PART ACTIVE]=ON AND DO[102:NEW PART A]=ON AND DO[103:NEW PART B]=ON) THEN ;
  19:  DO[25:A SIDE LOAD COMPLETE]=OFF ;
  20:  DO[26:B SIDE LOAD COMPLETE]=OFF ;
  21:  DO[102:NEW PART A]=OFF ;
  22:  DO[103:NEW PART B]=OFF ;
  23:  ENDIF ;
  24:  !PALLET SETUP IS COMPLETE         ;
  25:  IF (DO[51:PALLET TYPE FOUND]=ON AND DO[52:PALLET HEIGHT FOUND]=ON AND DO[53:PART LENGTH FOUND]=ON AND DO[54:PART DIAMETER FOUND]=ON AND DO[115:FINE LENGTH CHECKED]=ON) THEN ;
  26:  DO[50:PALLET SETUP COMPLETE]=ON ;
  27:  ELSE ;
  28:  DO[50:PALLET SETUP COMPLETE]=OFF ;
  29:  ENDIF ;
  30:  !RESET PALLET IF TABLE UNLOCKED   ;
  31:  IF (DO[42:TABLE READY FOR SERVICE]=OFF) THEN ;
  32:  DO[31:CYCLE STOP ACTIVE]=OFF ;
  33:  DO[32:PURGE ACTIVE]=OFF ;
  34:  DO[34:REGRIP CHECKED]=OFF ;
  35:  DO[37:TABLE A READY]=OFF ;
  36:  DO[38:TABLE B READY]=OFF ;
  37:  DO[39:TABLE SIDE A COMPLETE]=OFF ;
  38:  DO[40:TABLE SIDE B COMPLETE]=OFF ;
  39:  DO[41:TABLE READY TO ROTATE]=OFF ;
  40:  DO[43:TABLE PICK COMPLETE]=OFF ;
  41:  DO[44:TABLE DROP COMPLETE]=OFF ;
  42:  DO[51:PALLET TYPE FOUND]=OFF ;
  43:  DO[52:PALLET HEIGHT FOUND]=OFF ;
  44:  DO[53:PART LENGTH FOUND]=OFF ;
  45:  DO[54:PART DIAMETER FOUND]=OFF ;
  46:  DO[105:MEAS LOAD ACTIVE]=OFF ;
  47:  DO[106:MEAS UNLOAD ACTIVE]=OFF ;
  48:  DO[115:FINE LENGTH CHECKED]=OFF ;
  49:  DO[124:PINS REMOVED]=OFF ;
  50:  R[2:CUR ROW PICK (X)]=1    ;
  51:  R[3:CUR COL PICK (Y)]=1    ;
  52:  R[4:CUR ROW DROP (X)]=1    ;
  53:  R[5:CUR COL DROP (Y)]=1    ;
  54:  !RESET NEW PART LOGIC IF TABLE    ;
  55:  !UNLOCKED AND NEW PART ACTIVE     ;
  56:  IF (DO[104:NEW PART ACTIVE]=ON) THEN ;
  57:  DO[107:MEAS LOAD COMP]=OFF ;
  58:  DO[108:MEAS UNLD COMP]=OFF ;
  59:  DO[125:PART INSIDE SLOT]=OFF ;
  60:  R[1:ACTIVE TEMPLATE]=0    ;
  61:  R[66:TEMP Z TO TABLE]=0    ;
  62:  R[77:RAW DIAMETER]=0    ;
  63:  R[80:RAW CENTER X OFF]=0    ;
  64:  R[81:FINISHED DIA]=0    ;
  65:  ENDIF ;
  66:  ENDIF ;
  67:  !TURN ON MEASURE LOAD SIGNAL      ;
  68:  !IF STARTING NEW PART SETUP       ;
  69:  IF (R[2:CUR ROW PICK (X)]=1 AND R[3:CUR COL PICK (Y)]=1 AND R[4:CUR ROW DROP (X)]=1 AND R[5:CUR COL DROP (Y)]=1 AND DO[107:MEAS LOAD COMP]=OFF AND DO[63:NO LOAD ON]=OFF AND DO[104:NEW PART ACTIVE]=ON) THEN ;
  70:  DO[105:MEAS LOAD ACTIVE]=ON ;
  71:  ELSE ;
  72:  DO[105:MEAS LOAD ACTIVE]=OFF ;
  73:  ENDIF ;
  74:  !TURN ON MEASURE UNLOAD SIGNAL    ;
  75:  !IF STARTING NEW PART SETUP       ;
  76:  IF (R[2:CUR ROW PICK (X)]=1 AND R[3:CUR COL PICK (Y)]=1 AND R[4:CUR ROW DROP (X)]=1 AND R[5:CUR COL DROP (Y)]=1 AND DO[107:MEAS LOAD COMP]=ON AND DO[108:MEAS UNLD COMP]=OFF AND DO[68:NO UNLOAD ON]=OFF AND 
    :  DO[104:NEW PART ACTIVE]=ON) THEN ;
  77:  DO[106:MEAS UNLOAD ACTIVE]=ON ;
  78:  ELSE ;
  79:  DO[106:MEAS UNLOAD ACTIVE]=OFF ;
  80:  ENDIF ;
  81:  !TURN ON MEASURE COMPLETE IF NO   ;
  82:  !LOAD OR NO UNLOAD IS ACTIVE      ;
  83:  IF (DO[63:NO LOAD ON]=ON) THEN ;
  84:  DO[107:MEAS LOAD COMP]=ON ;
  85:  DO[53:PART LENGTH FOUND]=ON ;
  86:  DO[54:PART DIAMETER FOUND]=ON ;
  87:  DO[115:FINE LENGTH CHECKED]=ON ;
  88:  R[77:RAW DIAMETER]=R[804:FINISHED DIA.]    ;
  89:  R[67:MEAS PART LENGTH]=R[806:FINISHED LENGTH]    ;
  90:  ENDIF ;
  91:  IF (DO[68:NO UNLOAD ON]=ON),DO[108:MEAS UNLD COMP]=(ON) ;
  92:  !TURN OFF MEASURE COMPLETE        ;
  93:  IF (DI[66:UNLOAD MAINSPINDLE ON]=ON AND DO[108:MEAS UNLD COMP]=ON),DO[108:MEAS UNLD COMP]=(OFF) ;
  94:  IF (DI[67:UNLOAD SUBSPINDLE ON]=ON AND DO[108:MEAS UNLD COMP]=ON),DO[108:MEAS UNLD COMP]=(OFF) ;
  95:  !IF SETUP BUTTON IS PRESSED       ;
  96:  IF (DI[56:OKUMA LATHE]=ON OR DI[57:OKUMA MILL]=ON OR DI[58:HAAS LATHE]=ON OR DI[61:LOAD MAINSPINDLE ON]=ON OR DI[62:LOAD SUBSPINDLE ON]=ON OR DI[63:NO LOAD ON]=ON OR DI[66:UNLOAD MAINSPINDLE ON]=ON OR 
    :  DI[67:UNLOAD SUBSPINDLE ON]=ON OR DI[68:NO UNLOAD ON]=ON OR DI[116:T1 OD GRIP]=ON OR DI[117:T1 ID GRIP]=ON OR DI[118:T2 OD GRIP]=ON OR DI[119:T2 ID GRIP]=ON OR DI[55:OKUMA NO ANYBUS]=ON) THEN ;
  97:  DO[113:SETUP BUTTON PRESSED]=ON ;
  98:  ELSE ;
  99:  DO[113:SETUP BUTTON PRESSED]=OFF ;
 100:  ENDIF ;
 101:  !LOGIC FOR DIRECT ENTRY SETUP     ;
 102:  !LASER IS NOT BEING USED          ;
 103:  IF (DI[48:MEAS RAW DIA]=OFF AND R[800:RAW DIAMETER]>0) THEN ;
 104:  R[77:RAW DIAMETER]=R[800:RAW DIAMETER]    ;
 105:  DO[54:PART DIAMETER FOUND]=ON ;
 106:  ENDIF ;
 107:  IF (DI[49:MEAS RAW LENGTH]=OFF AND R[802:RAW LENGTH]>0) THEN ;
 108:  R[67:MEAS PART LENGTH]=R[802:RAW LENGTH]    ;
 109:  DO[53:PART LENGTH FOUND]=ON ;
 110:  DO[115:FINE LENGTH CHECKED]=ON ;
 111:  ENDIF ;
 112:  IF (DI[50:MEAS FINISHED DIA]=OFF) THEN ;
 113:  R[81:FINISHED DIA]=R[804:FINISHED DIA.]    ;
 114:  ELSE ;
 115:  R[81:FINISHED DIA]=R[77:RAW DIAMETER]    ;
 116:  ENDIF ;
 117:  IF (DI[51:MEAS FINISHED LENGTH]=OFF) THEN ;
 118:  R[72:FIN PART LENGTH]=R[806:FINISHED LENGTH]    ;
 119:  ELSE ;
 120:  R[72:FIN PART LENGTH]=R[67:MEAS PART LENGTH]    ;
 121:  ENDIF ;
 122:  IF (DI[52:MEAS TMPLT H]=OFF AND R[814:TMPLT HEIGHT]>0) THEN ;
 123:  R[66:TEMP Z TO TABLE]=R[814:TMPLT HEIGHT]    ;
 124:  DO[52:PALLET HEIGHT FOUND]=ON ;
 125:  ENDIF ;
 126:  IF (DI[53:MEAS TMPLT NUM]=OFF AND R[812:TMPLT ID]>0) THEN ;
 127:  R[1:ACTIVE TEMPLATE]=R[812:TMPLT ID]    ;
 128:  DO[51:PALLET TYPE FOUND]=ON ;
 129:  ENDIF ;
 130:  !SET HMI SETUP COMPLETE SIGNAL    ;
 131:  IF (DI[105:LOAD SETUP DONE]=ON AND DI[106:UNLOAD SETUP DONE]=ON AND DI[107:ADVANCED SETUP DONE]=ON AND DI[110:HMI PART SETUP COMPLETE]=ON) THEN ;
 132:  DO[109:PART HMI SETUP COMPLETE]=ON ;
 133:  ELSE ;
 134:  DO[109:PART HMI SETUP COMPLETE]=OFF ;
 135:  ENDIF ;
 136:  !RESET OUTPUTS IF DIMENSION IS    ;
 137:  !SET TO ZERO                      ;
 138:  IF (R[67:MEAS PART LENGTH]=0),DO[53:PART LENGTH FOUND]=(OFF) ;
 139:  IF (R[67:MEAS PART LENGTH]=0),DO[115:FINE LENGTH CHECKED]=(OFF) ;
 140:  IF (R[77:RAW DIAMETER]=0),DO[54:PART DIAMETER FOUND]=(OFF) ;
 141:  IF (R[66:TEMP Z TO TABLE]=0),DO[52:PALLET HEIGHT FOUND]=(OFF) ;
 142:  IF (R[1:ACTIVE TEMPLATE]=0),DO[51:PALLET TYPE FOUND]=(OFF) ;
/POS
/END
