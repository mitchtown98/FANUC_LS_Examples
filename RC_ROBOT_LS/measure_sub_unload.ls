/PROG  MEASURE_SUB_UNLOAD
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 1521;
CREATE		= DATE 23-11-18  TIME 09:26:12;
MODIFIED	= DATE 23-11-22  TIME 04:12:56;
FILE_NAME	= MEASURE_;
VERSION		= 0;
LINE_COUNT	= 63;
MEMORY_SIZE	= 1973;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  DO[217:ROBOT RETRACTED COM]=OFF ;
   2:  MONITOR MON_LATHE ;
   3:  CALL FAULT_MESSAGE('Robot is measuring finished part','X location in machine.') ;
   4:  $WAITTMOUT=500 ;
   5:  UFRAME_NUM=5 ;
   6:  UTOOL_NUM=1 ;
   7:  PR[12:JOINT OFFSET]=PR[12:JOINT OFFSET]-PR[12:JOINT OFFSET]    ;
   8:  IF (PR[81,6:MACH APPR]<0),PR[12,6:JOINT OFFSET]=(180) ;
   9:  IF (PR[81,6:MACH APPR]>=0),PR[12,6:JOINT OFFSET]=((-180)) ;
  10:J PR[80:MACHINE PERCH] 100% CNT20 Offset,PR[12:JOINT OFFSET]    ;
  11:  R[40:RECOVER START PR]=80    ;
  12:J PR[81:MACH APPR] 100% CNT20 Offset,PR[12:JOINT OFFSET]    ;
  13:  R[40:RECOVER START PR]=81    ;
  14:  LBL[601:SEARCH] ;
  15:  --eg:SET SEARCH START AND STOP POSITIONS ;
  16:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  17:  PR[31:APP Z OFFS]=PR[30:CARTESIAN ZERO]    ;
  18:  PR[95:SUB SRCH START]=PR[91:MASTER LOAD SUB]    ;
  19:  PR[96:SUB SRCH END]=PR[91:MASTER LOAD SUB]    ;
  20:  R[280:START SEARCH X]=PR[91,1:MASTER LOAD SUB]-R[284:MIN X SUB]    ;
  21:  R[286:END SEARCH X]=R[285:PART\JAW LENGTH]*(-1)    ;
  22:  PR[34,3:APP Z TOOL OFF]=R[280:START SEARCH X]    ;
  23:J PR[95:SUB SRCH START] 100% CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  24:  R[40:RECOVER START PR]=95    ;
  25:   ;
  26:   ;
  27:   ;
  28:   ;
  29:   ;
  30:  PR[34,3:APP Z TOOL OFF]=R[286:END SEARCH X]    ;
  31:  SKIP CONDITION RI[7:SPRING COMPRESSED]=ON+    ;
  32:L PR[96:SUB SRCH END] R[86:SEARCH FAST SPD]mm/sec FINE Skip,LBL[680],PR[88:UNLD SUB]=LPOS Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  33:  R[40:RECOVER START PR]=96    ;
  34:  --eg:BACK OFF TO PREVENT COLLISION. ;
  35:  PR[34,3:APP Z TOOL OFF]=1.5    ;
  36:L PR[88:UNLD SUB] 100mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  37:  WAIT    .10(sec) ;
  38:  PR[88:UNLD SUB]=LPOS    ;
  39:  R[71:SUB SPIN FIN X]=(PR[88,1:UNLD SUB]) ;
  40:  DO[108:MEAS UNLD COMP]=ON ;
  41:  PR[34,3:APP Z TOOL OFF]=75    ;
  42:L PR[88:UNLD SUB] 2000mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  43:  PR[12:JOINT OFFSET]=PR[12:JOINT OFFSET]-PR[12:JOINT OFFSET]    ;
  44:  IF (PR[81,6:MACH APPR]<0),PR[12,6:JOINT OFFSET]=(180) ;
  45:  IF (PR[81,6:MACH APPR]>=0),PR[12,6:JOINT OFFSET]=((-180)) ;
  46:  R[40:RECOVER START PR]=81    ;
  47:J PR[81:MACH APPR] 100% CNT20 Offset,PR[12:JOINT OFFSET]    ;
  48:  R[40:RECOVER START PR]=80    ;
  49:J PR[80:MACHINE PERCH] 100% CNT5    ;
  50:  DO[217:ROBOT RETRACTED COM]=ON ;
  51:  MONITOR END MON_LATHE ;
  52:  R[40:RECOVER START PR]=23    ;
  53:J PR[23:MOVE TO MACH] 100% CNT50    ;
  54:  R[40:RECOVER START PR]=1    ;
  55:J PR[1:HOME POSITION] 100% CNT20    ;
  56:  JMP LBL[9999] ;
  57:  LBL[680:SKIP FAULT] ;
  58:  CALL MACH_MSG(20) ;
  59:  UALM[6] ;
  60:  JMP LBL[601] ;
  61:  LBL[9999] ;
  62:  CALL MESSAGE19    ;
  63:  CALL MESSAGE20    ;
/POS
/END
