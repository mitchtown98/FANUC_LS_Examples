/PROG  TABLE_DROP
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "LOAD FINISH PART";
PROG_SIZE	= 2577;
CREATE		= DATE 23-11-18  TIME 07:36:26;
MODIFIED	= DATE 23-11-21  TIME 03:02:22;
FILE_NAME	= TABLE_PI;
VERSION		= 0;
LINE_COUNT	= 107;
MEMORY_SIZE	= 3125;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  --eg:DROP FINISHED PART INTO NEXT AVAILABLE TEMPLATE SLOT ;
   2:  LBL[1:START] ;
   3:  IF (DO[63:NO LOAD ON]=OFF) THEN ;
   4:  R[4:CUR ROW DROP (X)]=R[75:T2 X DROP]    ;
   5:  R[5:CUR COL DROP (Y)]=R[76:T2 Y DROP]    ;
   6:  WAIT    .10(sec) ;
   7:  ENDIF ;
   8:J PR[1:HOME POSITION] 100% CNT10    ;
   9:  R[40:RECOVER START PR]=1    ;
  10:  IF (DO[31:CYCLE STOP ACTIVE]=ON AND DO[32:PURGE ACTIVE]=ON AND DO[137:T2 FINISHED PART]=ON AND DO[211:MACHINE EMPTY]=ON AND R[6:CUR PICK SLOT #]=R[7:CUR DROP SLOT #]) THEN ;
  11:  DO[137:T2 FINISHED PART]=OFF ;
  12:  DO[138:T2 REGRIP RECOV]=ON ;
  13:  CALL PART_REGRIP    ;
  14:  JMP LBL[9999] ;
  15:  ENDIF ;
  16:  IF (DO[98:DEMO MODE]=ON) THEN ;
  17:J PR[19:T1 TABLE PNCE] 100% FINE    ;
  18:  R[40:RECOVER START PR]=13    ;
  19:  UTOOL_NUM=1 ;
  20:  ENDIF ;
  21:  IF (DO[98:DEMO MODE]=OFF) THEN ;
  22:J PR[13:TABLE DP POUNCE] 100% CNT50    ;
  23:  R[40:RECOVER START PR]=13    ;
  24:  UTOOL_NUM=2 ;
  25:  ENDIF ;
  26:   ;
  27:  --eg:SET R151 EQUAL TO THE POSITION REGISTER USED FOR THE ACTIVE TEMPLATE
    :  TYPE ;
  28:  IF (R[1:ACTIVE TEMPLATE]=2),R[151:TABLE DROP PR]=(65) ;
  29:  IF (R[1:ACTIVE TEMPLATE]=3),R[151:TABLE DROP PR]=(66) ;
  30:  IF (R[1:ACTIVE TEMPLATE]=4),R[151:TABLE DROP PR]=(67) ;
  31:  IF (R[1:ACTIVE TEMPLATE]=5),R[151:TABLE DROP PR]=(68) ;
  32:  IF (R[1:ACTIVE TEMPLATE]=6),R[151:TABLE DROP PR]=(69) ;
  33:   ;
  34:  LBL[10:CALCULATE DROP] ;
  35:  PR[40:TABLE DROP OFFS]=PR[30:CARTESIAN ZERO]    ;
  36:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  37:  --eg:WHEN DROPPING THE FIRST PART TO THE TABLE CALL THIS PROGRAM TO
    :  DETERMINE STARTING LOCATION BASED ON TEMPLATE TYPE ;
  38:  IF (R[5:CUR COL DROP (Y)]=1),CALL FIRST_DROP_SLOT ;
  39:  --eg:CALCULATE XYZ OF THE CURRENT SLOT ;
  40:  CALL CALC_DROP_OFFSET    ;
  41:  --eg:CALCULATE XYZ OF THE CURRENT SLOT ;
  42:  CALL CALC_DROP_R    ;
  43:  JMP LBL[100] ;
  44:   ;
  45:  LBL[100:DROP TO TABLE] ;
  46:  IF (DI[1:TABLE AT A POSITION]=ON) THEN ;
  47:  UFRAME_NUM=1 ;
  48:  ELSE ;
  49:  UFRAME_NUM=2 ;
  50:  ENDIF ;
  51:  WAIT DO[42:TABLE READY FOR SERVICE]=ON TIMEOUT,LBL[9980] ;
  52:  MONITOR MON_TABLE ;
  53:  PR[56:TEMP POSITION]=PR[R[151]]    ;
  54:  PR[56,1:TEMP POSITION]=PR[56,1:TEMP POSITION]+PR[40,1:TABLE DROP OFFS]+R[80:RAW CENTER X OFF]    ;
  55:  PR[56,2:TEMP POSITION]=PR[56,2:TEMP POSITION]+PR[40,2:TABLE DROP OFFS]    ;
  56:  PR[56,3:TEMP POSITION]=PR[56,3:TEMP POSITION]+R[72:FIN PART LENGTH]+R[72:FIN PART LENGTH]+50    ;
  57:  PR[56,6:TEMP POSITION]=PR[56,6:TEMP POSITION]+PR[40,6:TABLE DROP OFFS]    ;
  58:  CALL CHK_CFG((-1),56,0,54) ;
  59:  IF (R[54:CONFIG STATUS]<>0) THEN ;
  60:  JMP LBL[1] ;
  61:  ENDIF ;
  62:J PR[56:TEMP POSITION] 100% CNT50    ;
  63:  PR[56,3:TEMP POSITION]=PR[56,3:TEMP POSITION]-R[72:FIN PART LENGTH]-45    ;
  64:  CALL CHK_CFG((-1),56,0,54) ;
  65:L PR[56:TEMP POSITION] 1000mm/sec CNT15    ;
  66:  PR[56,1:TEMP POSITION]=PR[56,1:TEMP POSITION]-R[80:RAW CENTER X OFF]    ;
  67:  CALL CHK_CFG((-1),56,0,54) ;
  68:L PR[56:TEMP POSITION] 500mm/sec FINE    ;
  69:  DO[47:FINISHED PART DROPPED]=PULSE,0.7sec ;
  70:  IF (DO[98:DEMO MODE]=OFF) THEN ;
  71:  CALL T2_UNGRIP    ;
  72:  ENDIF ;
  73:  IF (DO[98:DEMO MODE]=ON) THEN ;
  74:  CALL T1_UNGRIP    ;
  75:  ENDIF ;
  76:  R[75:T2 X DROP]=0    ;
  77:  R[76:T2 Y DROP]=0    ;
  78:  CALL INC_DROP    ;
  79:  --eg:PART TO PART TIMER ;
  80:  TIMER[1]=STOP ;
  81:  R[120:PART-PART TIME]=TIMER[1]    ;
  82:  TIMER[1]=RESET ;
  83:  TIMER[1]=START ;
  84:  R[999:LIFETIME COUNT]=R[999:LIFETIME COUNT]+1    ;
  85:  PR[56,3:TEMP POSITION]=PR[56,3:TEMP POSITION]+100    ;
  86:  CALL CHK_CFG((-1),56,0,54) ;
  87:L PR[56:TEMP POSITION] 1000mm/sec CNT50    ;
  88:  MONITOR END MON_TABLE ;
  89:  JMP LBL[9999] ;
  90:   ;
  91:  LBL[9980:TABLE FAULT] ;
  92:  CALL TABLE_MSG(2) ;
  93:  UALM[11] ;
  94:  JMP LBL[10] ;
  95:   ;
  96:  LBL[9999:END] ;
  97:  R[40:RECOVER START PR]=13    ;
  98:J PR[13:TABLE DP POUNCE] 100% CNT30    ;
  99:  RO[2:T1 CLOSE]=OFF ;
 100:  RO[1:T1 OPEN]=ON ;
 101:  IF (DO[31:CYCLE STOP ACTIVE] OR DO[32:PURGE ACTIVE] OR DO[43:TABLE PICK COMPLETE]) THEN ;
 102:  R[40:RECOVER START PR]=1    ;
 103:J PR[1:HOME POSITION] 100% FINE    ;
 104:  ENDIF ;
 105:  IF (DO[63:NO LOAD ON]=ON) THEN ;
 106:  CALL GRIP_CHECK    ;
 107:  ENDIF ;
/POS
/END
