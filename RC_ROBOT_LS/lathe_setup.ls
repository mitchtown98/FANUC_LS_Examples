/PROG  LATHE_SETUP
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "FIND CHUCK LOC";
PROG_SIZE	= 4735;
CREATE		= DATE 23-11-18  TIME 09:14:20;
MODIFIED	= DATE 23-11-28  TIME 11:36:50;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 187;
MEMORY_SIZE	= 5299;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !MEASURE FOR LOAD AND UNLOAD      ;
   2:  !SETUP PALLET                     ;
   3:  IF DO[145:AT HOME]=OFF,CALL PNS0002 ;
   4:  LBL[1:START] ;
   5:  R[88:NOM SLOW mm/sec]=10    ;
   6:  R[89:NOM FAST mm/sec]=35    ;
   7:  UFRAME_NUM=5 ;
   8:J PR[1:HOME POSITION] 100% CNT10    ;
   9:  R[40:RECOVER START PR]=1    ;
  10:  $WAITTMOUT=500 ;
  11:  LBL[10:FAULT CHECKS] ;
  12:  !CHECK PROGRAM IS SET ;
  13:  IF ((R[410:CUT PRGM]=0 AND DO[58:HAAS LATHE]=ON) OR (R[411:WASH PRGM]=0 AND DO[221:RUN WASH COM]=ON AND DO[58:HAAS LATHE]=ON)) THEN ;
  14:  CALL MACH_MSG(22) ;
  15:  UALM[5] ;
  16:  JMP LBL[10] ;
  17:  ENDIF ;
  18:  !CHECK MACHINE IS READY STATE     ;
  19:  !SYSTEM LINK IS ON                ;
  20:  IF (DO[203:SYSTEM LINK COM]=OFF) THEN ;
  21:  CALL MACH_MSG(8) ;
  22:  UALM[5] ;
  23:  JMP LBL[10] ;
  24:  ENDIF ;
  25:  !NO ALARMS PRESENT                ;
  26:  IF (DO[218:NC ALARM COM]=OFF) THEN ;
  27:  CALL MACH_MSG(4) ;
  28:J PR[1:HOME POSITION] 100% CNT20    ;
  29:  R[40:RECOVER START PR]=1    ;
  30:  UALM[5] ;
  31:  JMP LBL[1] ;
  32:  ENDIF ;
  33:  !CHECK GRIPPERS CORRECT           ;
  34:  IF (DO[174:PART IN T1]=ON OR DO[175:PART IN T2]=ON) THEN ;
  35:  CALL TOOL_MSG(30) ;
  36:  UALM[1] ;
  37:  JMP LBL[10] ;
  38:  ENDIF ;
  39:  !CHECK MACHINE IS EMPTY           ;
  40:  IF (DO[105:MEAS LOAD ACTIVE]=ON AND DO[61:LOAD MAINSPINDLE ON]=ON AND DO[192:MAIN CLAMPED COM]=ON AND DO[124:PINS REMOVED]=OFF OR DO[105:MEAS LOAD ACTIVE]=ON AND DO[62:LOAD SUBSPINDLE ON]=ON AND 
    :  DO[194:SUB CLAMPED COM]=ON AND DO[124:PINS REMOVED]=OFF) THEN ;
  41:  CALL MACH_MSG(21) ;
  42:  UALM[16] ;
  43:  JMP LBL[10] ;
  44:  ENDIF ;
  45:  !CHECK PART IS IN LOAD SPINDLE    ;
  46:  IF (DO[105:MEAS LOAD ACTIVE]=ON AND DO[61:LOAD MAINSPINDLE ON]=ON AND DO[192:MAIN CLAMPED COM]=OFF OR DO[105:MEAS LOAD ACTIVE]=ON AND DO[62:LOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=OFF) THEN ;
  47:  CALL PNS0003    ;
  48:  CALL MACH_MSG(9) ;
  49:  PAUSE ;
  50:  DO[124:PINS REMOVED]=ON ;
  51:  JMP LBL[10] ;
  52:  ENDIF ;
  53:  !CHECK PART IS IN UNLOAD SPINDLE  ;
  54:  IF (DO[106:MEAS UNLOAD ACTIVE]=ON AND DO[67:UNLOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=OFF) THEN ;
  55:  CALL MACH_MSG(10) ;
  56:  UALM[5] ;
  57:  JMP LBL[10] ;
  58:  ENDIF ;
  59:  !CHECK MAIN EMPTY IF MEASURE SUB  ;
  60:  IF (DO[105:MEAS LOAD ACTIVE]=ON AND DO[62:LOAD SUBSPINDLE ON]=ON AND DO[192:MAIN CLAMPED COM]=ON) THEN ;
  61:  CALL MACH_MSG(13) ;
  62:  UALM[5] ;
  63:  JMP LBL[10] ;
  64:  ENDIF ;
  65:  !CHECK SUB EMPTY IF MEASURE MAIN  ;
  66:  IF (DO[105:MEAS LOAD ACTIVE]=ON AND DO[61:LOAD MAINSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=ON) THEN ;
  67:  CALL MACH_MSG(13) ;
  68:  UALM[5] ;
  69:  JMP LBL[10] ;
  70:  ENDIF ;
  71:  !MAIN PROGRAM LOOP                ;
  72:  LBL[100:CHOOSE OPERATION] ;
  73:  WAIT    .10(sec) ;
  74:  IF (DO[31:CYCLE STOP ACTIVE]=ON),JMP LBL[9999] ;
  75:J PR[23:MOVE TO MACH] 100% CNT100    ;
  76:  R[40:RECOVER START PR]=23    ;
  77:J PR[80:MACHINE PERCH] 100% FINE    ;
  78:  R[40:RECOVER START PR]=80    ;
  79:  --eg:WAIT UNTIL MACHINE IS READY FOR SERVICE ;
  80:  DO[224:ROBOT WAITING]=ON ;
  81:  WAIT (DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON)    ;
  82:  --eg:OPEN DOOR IF NOT IN CNC PROGAM ;
  83:  IF (DO[196:DOOR OPEN 1 COM]=OFF AND DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON) THEN ;
  84:  CALL LATHE_DOOR_OPEN    ;
  85:  ENDIF ;
  86:  WAIT (DO[209:MACH READY FOR SERVICE]=ON AND DO[210:MACH SAFE FOR ROBOT]=ON)    ;
  87:  DO[224:ROBOT WAITING]=OFF ;
  88:  CALL MESSAGE19    ;
  89:  CALL MESSAGE20    ;
  90:  !IF NO LOAD IS ON, SKIP LOAD      ;
  91:  !MEASURE AND START MACHINE        ;
  92:  IF (DO[63:NO LOAD ON]=ON AND DO[124:PINS REMOVED]=ON AND DO[107:MEAS LOAD COMP]=OFF) THEN ;
  93:  DO[107:MEAS LOAD COMP]=ON ;
  94:  CALL LATHE_DOOR_CLOSE    ;
  95:  IF (DO[58:HAAS LATHE]=ON) THEN ;
  96:  DO[223:CALL CUT COM]=ON ;
  97:  DO[222:CALL WASH COM]=OFF ;
  98:  WAIT R[300:PRGM STATUS]=1    ;
  99:  ENDIF ;
 100:  CALL LATHE_START    ;
 101:  WAIT DO[209:MACH READY FOR SERVICE]=OFF    ;
 102:  DO[223:CALL CUT COM]=OFF ;
 103:  JMP LBL[2] ;
 104:  ENDIF ;
 105:  !MEASURE MAIN CHUCK LOAD          ;
 106:  IF (DO[105:MEAS LOAD ACTIVE]=ON AND DO[176:ALL TOOLS UNGRIPPED]=ON AND DO[61:LOAD MAINSPINDLE ON]=ON AND DO[124:PINS REMOVED]=ON AND DO[192:MAIN CLAMPED COM]=ON) THEN ;
 107:  CALL MEASURE_MAIN_LOAD    ;
 108:  ENDIF ;
 109:  !MEASURE SUB CHUCK LOAD           ;
 110:  IF (DO[105:MEAS LOAD ACTIVE]=ON AND DO[176:ALL TOOLS UNGRIPPED]=ON AND DO[62:LOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=ON) THEN ;
 111:  CALL MEASURE_SUB_LOAD    ;
 112:  ENDIF ;
 113:  R[40:RECOVER START PR]=80    ;
 114:L PR[80:MACHINE PERCH] 2000mm/sec CNT20    ;
 115:  MONITOR END MON_LATHE ;
 116:  IF (DO[89:MANUAL DOOR]=ON) THEN ;
 117:  CALL LATHE_DOOR_CLOSE    ;
 118:  ENDIF ;
 119:  IF (DO[58:HAAS LATHE]=ON) THEN ;
 120:  DO[223:CALL CUT COM]=ON ;
 121:  DO[222:CALL WASH COM]=OFF ;
 122:  WAIT R[300:PRGM STATUS]=1    ;
 123:  ENDIF ;
 124:  IF (DO[91:TEST RIG ENABLED]=OFF) THEN ;
 125:  CALL LATHE_START    ;
 126:  WAIT DO[209:MACH READY FOR SERVICE]=OFF    ;
 127:  DO[223:CALL CUT COM]=OFF ;
 128:  R[40:RECOVER START PR]=23    ;
 129:J PR[23:MOVE TO MACH] 100% CNT50    ;
 130:  R[40:RECOVER START PR]=1    ;
 131:J PR[1:HOME POSITION] 100% CNT20    ;
 132:  ENDIF ;
 133:   ;
 134:  LBL[2:PALLET SETUP] ;
 135:  !PALLET SETUP                     ;
 136:  IF (DO[107:MEAS LOAD COMP]=ON AND DO[176:ALL TOOLS UNGRIPPED]=ON AND DO[50:PALLET SETUP COMPLETE]=OFF) THEN ;
 137:  CALL SETUP_PALLET    ;
 138:  ENDIF ;
 139:  !CHECK REGRIP IS CLEAR            ;
 140:  IF (DO[34:REGRIP CHECKED]=OFF AND DO[50:PALLET SETUP COMPLETE]=ON) THEN ;
 141:  CALL REGRIP_CHECK    ;
 142:  ENDIF ;
 143:J PR[23:MOVE TO MACH] 100% CNT50    ;
 144:  R[40:RECOVER START PR]=23    ;
 145:J PR[80:MACHINE PERCH] 100% CNT30    ;
 146:  R[40:RECOVER START PR]=80    ;
 147:  --eg:WAIT UNTIL MACHINE IS READY FOR SERVICE ;
 148:  DO[224:ROBOT WAITING]=ON ;
 149:  WAIT (DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON)    ;
 150:  --eg:OPEN DOOR IF NOT IN CNC PROGAM ;
 151:  IF (DO[196:DOOR OPEN 1 COM]=OFF AND DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON) THEN ;
 152:  CALL LATHE_DOOR_OPEN    ;
 153:  ENDIF ;
 154:  WAIT (DO[209:MACH READY FOR SERVICE]=ON AND DO[210:MACH SAFE FOR ROBOT]=ON)    ;
 155:  DO[224:ROBOT WAITING]=OFF ;
 156:  !MEASURE MAIN CHUCK UNLOAD        ;
 157:  IF (DO[106:MEAS UNLOAD ACTIVE]=ON AND DO[176:ALL TOOLS UNGRIPPED]=ON AND DO[66:UNLOAD MAINSPINDLE ON]=ON AND DO[192:MAIN CLAMPED COM]=ON AND DO[50:PALLET SETUP COMPLETE]=ON) THEN ;
 158:  CALL MEASURE_MAIN_UNLOAD    ;
 159:  ENDIF ;
 160:  !MEASURE SUB CHUCK UNLOAD         ;
 161:  IF (DO[106:MEAS UNLOAD ACTIVE]=ON AND DO[176:ALL TOOLS UNGRIPPED]=ON AND DO[67:UNLOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=ON AND DO[50:PALLET SETUP COMPLETE]=ON) THEN ;
 162:  CALL MEASURE_SUB_UNLOAD    ;
 163:  ENDIF ;
 164:  !SKIP UNLOAD MEASURE IF NO UNLOA  ;
 165:  IF (DO[106:MEAS UNLOAD ACTIVE]=ON AND DO[50:PALLET SETUP COMPLETE]=ON AND DO[68:NO UNLOAD ON]=ON AND DO[209:MACH READY FOR SERVICE]=ON),DO[108:MEAS UNLD COMP]=(ON) ;
 166:  !MOVE TO MAINTENACE TO SET PINS   ;
 167:  IF (DO[50:PALLET SETUP COMPLETE]=ON AND DO[107:MEAS LOAD COMP]=ON AND DO[108:MEAS UNLD COMP]=ON AND DO[124:PINS REMOVED]=ON) THEN ;
 168:  CALL PNS0003    ;
 169:  CALL TOOL_MSG(31) ;
 170:  PAUSE ;
 171:  DO[124:PINS REMOVED]=OFF ;
 172:  ENDIF ;
 173:  !IF COMPLETE, JUMP TO END SETUP   ;
 174:  IF (DO[50:PALLET SETUP COMPLETE]=ON AND DO[107:MEAS LOAD COMP]=ON AND DO[108:MEAS UNLD COMP]=ON AND DO[124:PINS REMOVED]=OFF),JMP LBL[9999] ;
 175:  JMP LBL[100] ;
 176:  LBL[9999:END] ;
 177:  DO[102:NEW PART A]=OFF ;
 178:  DO[103:NEW PART B]=OFF ;
 179:  DO[104:NEW PART ACTIVE]=OFF ;
 180:  DO[105:MEAS LOAD ACTIVE]=OFF ;
 181:  DO[106:MEAS UNLOAD ACTIVE]=OFF ;
 182:  DO[107:MEAS LOAD COMP]=OFF ;
 183:  DO[108:MEAS UNLD COMP]=OFF ;
 184:  R[40:RECOVER START PR]=1    ;
 185:J PR[1:HOME POSITION] 100% CNT5    ;
 186:  --eg:CHECK FIRST SLOT EMPTY ;
 187:  CALL CHECK_EMPTY_SLOT    ;
/POS
/END
