/PROG  SETUP_DIAMETER
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "LASER TEACH";
PROG_SIZE	= 4137;
CREATE		= DATE 22-07-25  TIME 15:31:44;
MODIFIED	= DATE 23-11-21  TIME 01:12:18;
FILE_NAME	= FIND_CIR;
VERSION		= 0;
LINE_COUNT	= 203;
MEMORY_SIZE	= 4637;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  --eg:USES LASER TO FIND 3 EDGES OF THE PART AND CALCULATES THE CENTER
    :  LOCATION OF THE PART AND ITS DIAMETER. ;
   2:   ;
   3:  LBL[1:START] ;
   4:  CALL FAULT_MESSAGE('Robot is measuring the part ','diameter.') ;
   5:   ;
   6:  R[40:RECOVER START PR]=1    ;
   7:  RO[8:LASER ON]=ON ;
   8:   ;
   9:  --eg:SPEED CONTROL FOR LASER SEARCH. SPEED IS CALCULATED TO ALWAYS MOVE
    :  AT SAME RATE BASED ON MODE AND OVERRIDE ;
  10:  R[43:ACTION RETRIES]=0    ;
  11:  R[88:NOM SLOW mm/sec]=10    ;
  12:  R[89:NOM FAST mm/sec]=40    ;
  13:  R[94:Z ADJUST]=15    ;
  14:   ;
  15:  --eg:SET R155 - R160  EQUAL TO THE POSITION REGISTER USED FOR THE ACTIVE
    :  TEMPLATE TYPE  ;
  16:   ;
  17:  IF (R[1:ACTIVE TEMPLATE]=2) THEN ;
  18:  R[155:EDGE SEARCH 1 PR]=100    ;
  19:  R[156:EDGE SEARCH 2 PR]=101    ;
  20:  R[158:EDGE FOUND 1 PR]=103    ;
  21:  R[159:EDGE FOUND 2 PR]=104    ;
  22:  ENDIF ;
  23:   ;
  24:  IF (R[1:ACTIVE TEMPLATE]=3) THEN ;
  25:  R[155:EDGE SEARCH 1 PR]=110    ;
  26:  R[156:EDGE SEARCH 2 PR]=111    ;
  27:  R[158:EDGE FOUND 1 PR]=113    ;
  28:  R[159:EDGE FOUND 2 PR]=114    ;
  29:  ENDIF ;
  30:   ;
  31:  IF (R[1:ACTIVE TEMPLATE]=4) THEN ;
  32:  R[155:EDGE SEARCH 1 PR]=120    ;
  33:  R[156:EDGE SEARCH 2 PR]=121    ;
  34:  R[158:EDGE FOUND 1 PR]=123    ;
  35:  R[159:EDGE FOUND 2 PR]=124    ;
  36:  ENDIF ;
  37:   ;
  38:  IF (R[1:ACTIVE TEMPLATE]=5) THEN ;
  39:  R[155:EDGE SEARCH 1 PR]=130    ;
  40:  R[156:EDGE SEARCH 2 PR]=131    ;
  41:  R[158:EDGE FOUND 1 PR]=133    ;
  42:  R[159:EDGE FOUND 2 PR]=134    ;
  43:  ENDIF ;
  44:   ;
  45:  IF (R[1:ACTIVE TEMPLATE]=6) THEN ;
  46:  R[155:EDGE SEARCH 1 PR]=140    ;
  47:  R[156:EDGE SEARCH 2 PR]=141    ;
  48:  R[158:EDGE FOUND 1 PR]=143    ;
  49:  R[159:EDGE FOUND 2 PR]=144    ;
  50:  ENDIF ;
  51:   ;
  52:  JMP LBL[100] ;
  53:   ;
  54:  LBL[100:CIRCLE EDGE 1] ;
  55:  UTOOL_NUM=9 ;
  56:  IF (DI[1:TABLE AT A POSITION]=ON) THEN ;
  57:  UFRAME_NUM=1 ;
  58:  ELSE ;
  59:  UFRAME_NUM=2 ;
  60:  ENDIF ;
  61:   ;
  62:  !SET Z TO TEMPLATE HEIGHT PLUS    ;
  63:  !5MM FOR LASER FUNCTION           ;
  64:  PR[R[155],3]=R[66:TEMP Z TO TABLE]+50    ;
  65:  PR[R[156],3]=R[66:TEMP Z TO TABLE]+50    ;
  66:   ;
  67:  --eg:FIND FIRST OUTSIDE EDGE OF PART ;
  68:J PR[R[155]] 100% FINE    ;
  69:  PR[R[155],3]=R[66:TEMP Z TO TABLE]-100    ;
  70:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
  71:L PR[R[155]] 50mm/sec FINE Skip,LBL[11]    ;
  72:  WAIT    .10(sec) ;
  73:  SKIP CONDITION RI[8:LASER INPUT]=OFF    ;
  74:  PR[R[155],3]=R[66:TEMP Z TO TABLE]+100    ;
  75:L PR[R[155]] 3mm/sec FINE Skip,LBL[11]    ;
  76:  PR[R[155],3]=R[66:TEMP Z TO TABLE]+100    ;
  77:  PR[47:LPOS 1]=LPOS    ;
  78:  PR[R[155],3]=PR[47,3:LPOS 1]+R[94:Z ADJUST]    ;
  79:J PR[R[155]] 100% FINE    ;
  80:  PR[33,1:APP X OFFS]=R[79:SLOT X CENTER]    ;
  81:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
  82:L PR[R[155]] R[85:SEARCH SLOW SPD]mm/sec FINE Offset,PR[33:APP X OFFS] Skip,LBL[180],PR[R[158]]=LPOS    ;
  83:  WAIT    .25(sec) ;
  84:  SKIP CONDITION RI[8:LASER INPUT]=OFF-    ;
  85:  PR[33,1:APP X OFFS]=(-25)    ;
  86:L PR[R[158]] R[85:SEARCH SLOW SPD]mm/sec FINE Offset,PR[33:APP X OFFS] Skip,LBL[180],PR[R[158]]=LPOS    ;
  87:  WAIT    .10(sec) ;
  88:  PR[R[158]]=LPOS    ;
  89:   ;
  90:  JMP LBL[110] ;
  91:   ;
  92:  LBL[110:CIRCLE EDGE 2] ;
  93:  PR[36,1:APP X TOOL OFFS]=0    ;
  94:  R[43:ACTION RETRIES]=0    ;
  95:   ;
  96:  --eg:FIND SECOND OUTSIDE EDGE OF PART ;
  97:  LBL[111:CONTINUE] ;
  98:  UTOOL_NUM=9 ;
  99:  IF (DI[1:TABLE AT A POSITION]=ON) THEN ;
 100:  UFRAME_NUM=1 ;
 101:  ELSE ;
 102:  UFRAME_NUM=2 ;
 103:  ENDIF ;
 104:  PR[33,1:APP X OFFS]=R[79:SLOT X CENTER]*(-2)    ;
 105:J PR[R[156]] 100% FINE Tool_Offset,PR[36:APP X TOOL OFF]    ;
 106:  PR[R[156],3]=R[66:TEMP Z TO TABLE]-100    ;
 107:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
 108:L PR[R[156]] 50mm/sec FINE Skip,LBL[11]    ;
 109:  WAIT    .10(sec) ;
 110:  SKIP CONDITION RI[8:LASER INPUT]=OFF    ;
 111:  PR[R[156],3]=R[66:TEMP Z TO TABLE]+100    ;
 112:L PR[R[156]] 5mm/sec FINE Skip,LBL[11]    ;
 113:  PR[R[156],3]=R[66:TEMP Z TO TABLE]+100    ;
 114:  PR[47:LPOS 1]=LPOS    ;
 115:  PR[R[156],3]=PR[47,3:LPOS 1]+R[94:Z ADJUST]    ;
 116:J PR[R[156]] 100% FINE Tool_Offset,PR[36:APP X TOOL OFF]    ;
 117:  WAIT    .10(sec) ;
 118:  SKIP CONDITION RI[8:LASER INPUT]=ON    ;
 119:L PR[R[156]] R[85:SEARCH SLOW SPD]mm/sec FINE Offset,PR[33:APP X OFFS] Tool_Offset,PR[36:APP X TOOL OFF] Skip,LBL[180],PR[R[159]]=LPOS    ;
 120:  WAIT    .10(sec) ;
 121:  SKIP CONDITION RI[8:LASER INPUT]=OFF-    ;
 122:  PR[33,1:APP X OFFS]=20    ;
 123:L PR[R[159]] R[85:SEARCH SLOW SPD]mm/sec FINE Offset,PR[33:APP X OFFS] Skip,LBL[180],PR[R[159]]=LPOS    ;
 124:  WAIT    .10(sec) ;
 125:  PR[R[159]]=LPOS    ;
 126:   ;
 127:  --eg:CALCULATE CIRCLE DIAMETER AND CENTER ;
 128:  WAIT    .25(sec) ;
 129:  R[77:RAW DIAMETER]=(PR[R[159],1]-PR[R[158],1]) ;
 130:  R[82:X CENTER]=R[77:RAW DIAMETER]/2    ;
 131:  R[82:X CENTER]=PR[R[158],1]+R[82:X CENTER]    ;
 132:   ;
 133:   ;
 134:   ;
 135:  JMP LBL[200] ;
 136:   ;
 137:  LBL[180:PART NOT FOUND] ;
 138:  R[43:ACTION RETRIES]=R[43:ACTION RETRIES]+1    ;
 139:  IF (R[43:ACTION RETRIES]=1),R[94:Z ADJUST]=(10) ;
 140:  IF (R[43:ACTION RETRIES]=2),R[94:Z ADJUST]=(5) ;
 141:  IF (R[43:ACTION RETRIES]>=3) THEN ;
 142:  CALL LASER_MSG(1) ;
 143:  R[40:RECOVER START PR]=1    ;
 144:J PR[1:HOME POSITION] 100% FINE    ;
 145:  UALM[19] ;
 146:  R[43:ACTION RETRIES]=0    ;
 147:  ENDIF ;
 148:  JMP LBL[100] ;
 149:   ;
 150:  LBL[200:CHECK VALUES] ;
 151:  IF (R[77:RAW DIAMETER]<R[50:MIN DIAMETER]),JMP LBL[280] ;
 152:  IF (R[77:RAW DIAMETER]>R[51:MAX DIAMETER]),JMP LBL[281] ;
 153:   ;
 154:  IF (DO[104:NEW PART ACTIVE]=OFF) THEN ;
 155:  R[136:TOLERANCE]=R[131:LAST DIAMETER]*R[133:TOLERANCE %]    ;
 156:  R[134:MIN TOLERANCE]=R[131:LAST DIAMETER]-R[136:TOLERANCE]    ;
 157:  R[135:MAX TOLERANCE]=R[131:LAST DIAMETER]+R[136:TOLERANCE]    ;
 158:  IF (R[77:RAW DIAMETER]<R[134:MIN TOLERANCE] OR R[77:RAW DIAMETER]>R[135:MAX TOLERANCE]) THEN ;
 159:  R[40:RECOVER START PR]=1    ;
 160:J PR[1:HOME POSITION] 100% FINE    ;
 161:  CALL TABLE_MSG(23) ;
 162:  UALM[11] ;
 163:  JMP LBL[1] ;
 164:  ENDIF ;
 165:  ENDIF ;
 166:   ;
 167:   ;
 168:  IF (DO[104:NEW PART ACTIVE]=ON) THEN ;
 169:  R[131:LAST DIAMETER]=R[77:RAW DIAMETER]    ;
 170:  ENDIF ;
 171:  DO[54:PART DIAMETER FOUND]=ON ;
 172:  JMP LBL[9999] ;
 173:   ;
 174:  LBL[280:MEASURE FAULT] ;
 175:  R[40:RECOVER START PR]=1    ;
 176:J PR[1:HOME POSITION] 100% FINE    ;
 177:  CALL TABLE_MSG(12) ;
 178:  UALM[18] ;
 179:  JMP LBL[1] ;
 180:   ;
 181:  LBL[281:DIAMTER FAULT] ;
 182:  R[40:RECOVER START PR]=1    ;
 183:J PR[1:HOME POSITION] 100% FINE    ;
 184:  CALL TABLE_MSG(13) ;
 185:  UALM[18] ;
 186:  JMP LBL[1] ;
 187:   ;
 188:  LBL[282:PART TOO SHORT] ;
 189:  R[40:RECOVER START PR]=1    ;
 190:J PR[1:HOME POSITION] 100% FINE    ;
 191:  CALL TABLE_MSG(11) ;
 192:  UALM[18] ;
 193:  JMP LBL[1] ;
 194:   ;
 195:  LBL[9980:SETUP NOT DONE] ;
 196:  CALL TABLE_MSG(1) ;
 197:  UALM[18] ;
 198:  JMP LBL[1] ;
 199:   ;
 200:  LBL[9999:END] ;
 201:  RO[8:LASER ON]=OFF ;
 202:  CALL MESSAGE19    ;
 203:  CALL MESSAGE20    ;
/POS
/END
