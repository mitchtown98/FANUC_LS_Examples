/PROG  LATHE_SERVICE_DUAL
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "DUAL SERVICE";
PROG_SIZE	= 3871;
CREATE		= DATE 23-11-21  TIME 05:53:32;
MODIFIED	= DATE 23-11-22  TIME 04:09:04;
FILE_NAME	= LATHE_SE;
VERSION		= 0;
LINE_COUNT	= 147;
MEMORY_SIZE	= 4363;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !PROGRAM TO SERVICE LATHE ;
   2:  LBL[1:START] ;
   3:  UFRAME_NUM=5 ;
   4:J PR[23:MOVE TO MACH] 100% CNT100    ;
   5:  R[40:RECOVER START PR]=23    ;
   6:  LBL[10:STATUS CHECK] ;
   7:J PR[80:MACHINE PERCH] 100% CNT10    ;
   8:  R[40:RECOVER START PR]=80    ;
   9:  !CHECK PROGRAM IS SET ;
  10:  IF ((R[410:CUT PRGM]=0 AND DO[58:HAAS LATHE]=ON) OR (R[411:WASH PRGM]=0 AND DO[221:RUN WASH COM]=ON AND DO[58:HAAS LATHE]=ON)) THEN ;
  11:  CALL MACH_MSG(22) ;
  12:  UALM[5] ;
  13:  JMP LBL[10] ;
  14:  ENDIF ;
  15:  !CHECK MACHINE IS READY STATE ;
  16:  !SYSTEM LINK IS ON ;
  17:  IF (DO[203:SYSTEM LINK COM]=OFF) THEN ;
  18:  IF (DO[145:AT HOME]=OFF) THEN ;
  19:  R[40:RECOVER START PR]=23    ;
  20:J PR[23:MOVE TO MACH] 100% CNT60    ;
  21:  R[40:RECOVER START PR]=1    ;
  22:J PR[1:HOME POSITION] 100% FINE    ;
  23:  ENDIF ;
  24:  CALL MACH_MSG(8) ;
  25:  UALM[5] ;
  26:  JMP LBL[10] ;
  27:  ENDIF ;
  28:  !CHECK MEASURE NOT ACTIVE ;
  29:  IF (DO[105:MEAS LOAD ACTIVE]=ON OR DO[106:MEAS UNLOAD ACTIVE]=ON) THEN ;
  30:  CALL MACH_MSG(10) ;
  31:  UALM[5] ;
  32:  JMP LBL[9999] ;
  33:  ENDIF ;
  34:  !CHECK MACHINE ALARM CLEAR ;
  35:  IF (DO[218:NC ALARM COM]=OFF) THEN ;
  36:  CALL MACH_MSG(4) ;
  37:  UALM[5] ;
  38:  JMP LBL[10] ;
  39:  ENDIF ;
  40:  !CHECK DIAMETER SET ;
  41:  IF (DI[50:MEAS FINISHED DIA]=OFF AND R[804:FINISHED DIA.]=0) THEN ;
  42:  CALL MACH_MSG(11) ;
  43:  UALM[5] ;
  44:  JMP LBL[1] ;
  45:  ENDIF ;
  46:  !CHECK LENGTH SET ;
  47:  IF (DI[51:MEAS FINISHED LENGTH]=OFF AND R[806:FINISHED LENGTH]=0) THEN ;
  48:  CALL MACH_MSG(12) ;
  49:  UALM[5] ;
  50:  JMP LBL[1] ;
  51:  ENDIF ;
  52:  JMP LBL[100] ;
  53:  !MAIN LOOP ;
  54:  LBL[100:CHOOSE OPERATION] ;
  55:  --eg:WAIT UNTIL MACHINE IS READY FOR SERVICE ;
  56:  DO[224:ROBOT WAITING]=ON ;
  57:  WAIT (DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON)    ;
  58:  --eg:OPEN DOOR IF NOT IN CNC PROGAM ;
  59:  IF (DO[196:DOOR OPEN 1 COM]=OFF AND DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON) THEN ;
  60:  CALL LATHE_DOOR_OPEN    ;
  61:  ENDIF ;
  62:  WAIT (DO[209:MACH READY FOR SERVICE]=ON AND DO[210:MACH SAFE FOR ROBOT]=ON)    ;
  63:  DO[224:ROBOT WAITING]=OFF ;
  64:  CALL MESSAGE19    ;
  65:  CALL MESSAGE20    ;
  66:  DO[217:ROBOT RETRACTED COM]=OFF ;
  67:  MONITOR MON_LATHE ;
  68:  !LOAD MAIN SPINDLE FIRST PART ;
  69:  IF (DO[129:T1 MACHINE]=ON AND DO[61:LOAD MAINSPINDLE ON]=ON AND DO[193:MAIN UNCLAMPED COM]=ON AND DO[67:UNLOAD SUBSPINDLE ON]=ON AND DO[195:SUB UNCLAMPED COM]=ON AND DO[173:T2 UNGRIPPED]=ON) THEN ;
  70:  CALL LATHE_LOAD_MAIN    ;
  71:  ENDIF ;
  72:  !UNLOAD SUB SPINDLE ;
  73:  IF (DO[173:T2 UNGRIPPED]=ON AND DO[67:UNLOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=ON AND DO[129:T1 MACHINE]=ON AND DO[61:LOAD MAINSPINDLE ON]=ON AND DO[192:MAIN CLAMPED COM]=ON) THEN ;
  74:  CALL LATHE_UNLOAD_SUB    ;
  75:  ENDIF ;
  76:  !UNLOAD SUB SPINDLE PURGE ;
  77:  IF (DO[173:T2 UNGRIPPED]=ON AND DO[67:UNLOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=ON AND DO[171:T1 UNGRIPPED]=ON AND DO[192:MAIN CLAMPED COM]=ON AND DO[32:PURGE ACTIVE]=ON) THEN ;
  78:  CALL LATHE_UNLOAD_SUB    ;
  79:  ENDIF ;
  80:  !UNLOAD SUB SPINDLE PURGE LAST PT ;
  81:  IF (DO[173:T2 UNGRIPPED]=ON AND DO[67:UNLOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=ON AND DO[171:T1 UNGRIPPED]=ON AND DO[193:MAIN UNCLAMPED COM]=ON AND DO[32:PURGE ACTIVE]=ON) THEN ;
  82:  CALL LATHE_UNLOAD_SUB    ;
  83:  ENDIF ;
  84:  !LOAD MAIN SPINDLE AFTER TRANSFER ;
  85:  IF (DO[129:T1 MACHINE]=ON AND DO[61:LOAD MAINSPINDLE ON]=ON AND DO[193:MAIN UNCLAMPED COM]=ON AND DO[67:UNLOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=ON AND DO[173:T2 UNGRIPPED]=ON) THEN ;
  86:  CALL LATHE_LOAD_MAIN    ;
  87:  ENDIF ;
  88:  --eg:CYCLE START TRANSFER ;
  89:  IF ((DO[176:ALL TOOLS UNGRIPPED]=ON AND DO[192:MAIN CLAMPED COM]=ON AND DO[195:SUB UNCLAMPED COM]=ON) OR (DO[129:T1 MACHINE]=ON AND DO[137:T2 FINISHED PART]=ON AND DO[192:MAIN CLAMPED COM]=ON AND 
    :  DO[195:SUB UNCLAMPED COM]=ON) OR (DO[32:PURGE ACTIVE]=ON AND DO[195:SUB UNCLAMPED COM]=ON AND DO[192:MAIN CLAMPED COM]=ON AND DO[137:T2 FINISHED PART]=ON)) THEN ;
  90:  DO[223:CALL CUT COM]=ON ;
  91:  DO[222:CALL WASH COM]=OFF ;
  92:  WAIT R[300:PRGM STATUS]=1    ;
  93:  DO[217:ROBOT RETRACTED COM]=ON ;
  94:  MONITOR END MON_LATHE ;
  95:  CALL LATHE_START    ;
  96:  WAIT DO[209:MACH READY FOR SERVICE]=OFF    ;
  97:  DO[223:CALL CUT COM]=OFF ;
  98:  R[300:PRGM STATUS]=0    ;
  99:  R[64:UNLOAD X DROP]=R[57:LOAD X DROP]    ;
 100:  R[65:UNLOAD Y DROP]=R[58:LOAD Y DROP]    ;
 101:  ENDIF ;
 102:  IF (DO[176:ALL TOOLS UNGRIPPED]=ON AND DO[195:SUB UNCLAMPED COM]=ON),JMP LBL[9999] ;
 103:  IF (DO[32:PURGE ACTIVE]=ON AND DO[137:T2 FINISHED PART]=ON AND DO[171:T1 UNGRIPPED]=ON),JMP LBL[9999] ;
 104:  --eg:WAIT UNTIL MACHINE IS READY FOR SERVICE ;
 105:  DO[224:ROBOT WAITING]=ON ;
 106:  WAIT (DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON)    ;
 107:  --eg:OPEN DOOR IF NOT IN CNC PROGAM ;
 108:  IF (DO[196:DOOR OPEN 1 COM]=OFF AND DO[203:SYSTEM LINK COM]=ON AND DO[204:MACHINE IN AUTO COM]=ON AND DO[205:CYCLE COMPLETE COM]=ON) THEN ;
 109:  CALL LATHE_DOOR_OPEN    ;
 110:  ENDIF ;
 111:  WAIT (DO[209:MACH READY FOR SERVICE]=ON AND DO[210:MACH SAFE FOR ROBOT]=ON)    ;
 112:  DO[224:ROBOT WAITING]=OFF ;
 113:  CALL MESSAGE19    ;
 114:  CALL MESSAGE20    ;
 115:  DO[217:ROBOT RETRACTED COM]=OFF ;
 116:  MONITOR MON_LATHE ;
 117:  !LOAD MAIN SPINDLE AFTER TRANSFER ;
 118:  IF (DO[129:T1 MACHINE]=ON AND DO[61:LOAD MAINSPINDLE ON]=ON AND DO[193:MAIN UNCLAMPED COM]=ON AND DO[67:UNLOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=ON AND DO[137:T2 FINISHED PART]=ON) THEN ;
 119:  CALL LATHE_LOAD_MAIN    ;
 120:  ENDIF ;
 121:  DO[217:ROBOT RETRACTED COM]=ON ;
 122:  MONITOR END MON_LATHE ;
 123:  R[40:RECOVER START PR]=80    ;
 124:J PR[80:MACHINE PERCH] 100% FINE    ;
 125:  --eg:CYCLE START CUTTING PROGRAM ;
 126:  IF ((DO[61:LOAD MAINSPINDLE ON]=ON AND DO[192:MAIN CLAMPED COM]=ON) OR (DO[62:LOAD SUBSPINDLE ON]=ON AND DO[194:SUB CLAMPED COM]=ON) OR (DO[63:NO LOAD ON]=ON)) THEN ;
 127:  DO[222:CALL WASH COM]=ON ;
 128:  DO[223:CALL CUT COM]=OFF ;
 129:  WAIT R[300:PRGM STATUS]=2    ;
 130:  ENDIF ;
 131:  IF (DO[91:TEST RIG ENABLED]=OFF) THEN ;
 132:  CALL LATHE_START    ;
 133:  WAIT DO[209:MACH READY FOR SERVICE]=OFF    ;
 134:  DO[222:CALL WASH COM]=OFF ;
 135:  ENDIF ;
 136:  --eg:EXIT PROGRAM IF FINISHED PART IN T2 AND T1 EMPTY OR IF BOTH GRIPS
    :  EMPTY ;
 137:  IF ((DO[137:T2 FINISHED PART]=ON AND DO[129:T1 MACHINE]=OFF) OR (DO[176:ALL TOOLS UNGRIPPED]=ON)),JMP LBL[9999] ;
 138:  WAIT    .10(sec) ;
 139:  JMP LBL[100] ;
 140:  !PROGRAM END ;
 141:  LBL[9999:END] ;
 142:  IF (DO[145:AT HOME]=OFF) THEN ;
 143:  R[40:RECOVER START PR]=23    ;
 144:J PR[23:MOVE TO MACH] 100% CNT100    ;
 145:  ENDIF ;
 146:  R[40:RECOVER START PR]=1    ;
 147:J PR[1:HOME POSITION] 100% CNT5    ;
/POS
/END
