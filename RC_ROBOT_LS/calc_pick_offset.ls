/PROG  CALC_PICK_OFFSET
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "TABLE PICK OFFS";
PROG_SIZE	= 1301;
CREATE		= DATE 22-10-04  TIME 17:20:38;
MODIFIED	= DATE 22-10-06  TIME 13:16:50;
FILE_NAME	= CALC_PIC;
VERSION		= 0;
LINE_COUNT	= 41;
MEMORY_SIZE	= 1769;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  --eg:CALCULATE XYZ OFFSET OF RAW PART BASED ON CURRENT SLOT ;
   2:   ;
   3:  LBL[1:START] ;
   4:  --eg:MOVE ALL ZEROS IN PICK OFFSETS POSITION REGISTER ;
   5:  PR[38:TABLE PCK OFFSET]=PR[30:CARTESIAN ZERO]    ;
   6:  --eg:CALCULATE ROW OFFSET (X) ;
   7:  PR[38,1:TABLE PCK OFFSET]=((R[2:CUR ROW PICK (X)]*R[12:ROW SPACING (X)])-R[12:ROW SPACING (X)]) ;
   8:  !CALC X OFFSET FOR DIAMETER       ;
   9:  R[80:RAW CENTER X OFF]=(.7071*(R[84:SIDE LENGTH]-R[77:RAW DIAMETER])) ;
  10:  PR[38,1:TABLE PCK OFFSET]=PR[38,1:TABLE PCK OFFSET]-R[80:RAW CENTER X OFF]    ;
  11:   ;
  12:  --eg:CALCULATE COLUMN OFFSET (Y) ;
  13:  PR[38,2:TABLE PCK OFFSET]=((R[3:CUR COL PICK (Y)]*R[13:COL SPACING (Y)])-R[13:COL SPACING (Y)]) ;
  14:  --eg:GET PART LENGTH AND CALCULATE Z OFFSET ;
  15:  IF (DI[1:TABLE AT A POSITION]=ON) THEN ;
  16:  PR[38,3:TABLE PCK OFFSET]=R[67:MEAS PART LENGTH]-R[74:SPRING DIST A]+2    ;
  17:  ENDIF ;
  18:  IF (DI[2:TABLE AT B POSITION]=ON) THEN ;
  19:  PR[38,3:TABLE PCK OFFSET]=R[67:MEAS PART LENGTH]-R[83:SPRING DIST B]+2    ;
  20:  ENDIF ;
  21:  --eg:OFFSETS FOR TWO OFFSET SLOTS ON TEMPLATE 5. ;
  22:   ;
  23:  IF (R[1:ACTIVE TEMPLATE]=5) THEN ;
  24:  IF (R[6:CUR PICK SLOT #]=20) THEN ;
  25:  PR[38,1:TABLE PCK OFFSET]=202.66    ;
  26:  !CALC X OFFSET FOR DIAMETER       ;
  27:  R[80:RAW CENTER X OFF]=(.7071*(R[84:SIDE LENGTH]-R[77:RAW DIAMETER])) ;
  28:  PR[38,1:TABLE PCK OFFSET]=PR[38,1:TABLE PCK OFFSET]-R[80:RAW CENTER X OFF]    ;
  29:  PR[38,2:TABLE PCK OFFSET]=86.66    ;
  30:  ENDIF ;
  31:   ;
  32:  IF (R[6:CUR PICK SLOT #]=26) THEN ;
  33:  PR[38,1:TABLE PCK OFFSET]=202.66    ;
  34:  !CALC X OFFSET FOR DIAMETER       ;
  35:  R[80:RAW CENTER X OFF]=(.7071*(R[84:SIDE LENGTH]-R[77:RAW DIAMETER])) ;
  36:  PR[38,1:TABLE PCK OFFSET]=PR[38,1:TABLE PCK OFFSET]-R[80:RAW CENTER X OFF]    ;
  37:  PR[38,2:TABLE PCK OFFSET]=793.44    ;
  38:  ENDIF ;
  39:  ENDIF ;
  40:   ;
  41:  LBL[9999:END] ;
/POS
/END
