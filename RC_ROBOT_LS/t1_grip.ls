/PROG  T1_GRIP
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "TOOL GRIP";
PROG_SIZE	= 1587;
CREATE		= DATE 21-10-21  TIME 10:41:28;
MODIFIED	= DATE 22-05-23  TIME 15:38:20;
FILE_NAME	= OPEN_T1;
VERSION		= 0;
LINE_COUNT	= 72;
MEMORY_SIZE	= 2115;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;

  AUTOZONE_INTERFERENCE_AVOID;
    AUTOZONE_ENABLED         : FALSE;
    AUTOZONE_SCHEDULE_NUMBER : 1;
/MN
   1:  --eg:TURN ON DO36 BEFORE PROGRAM CALL WHEN YOU DO NOT WANT A GRIPPER
    :  OVERTRAVEL TO TRIGGER AN ALARM. WHEN OVERTRAVEL OCCURS GRIPPER WILL
    :  UNGRIP AND MOVE TO NEXT SLOT. ;
   2:   ;
   3:  LBL[1:START] ;
   4:  IF DO[116:T1 OD GRIP]=ON AND DO[117:T1 ID GRIP]=OFF,JMP LBL[100] ;
   5:  IF DO[117:T1 ID GRIP]=ON AND DO[116:T1 OD GRIP]=OFF,JMP LBL[200] ;
   6:  JMP LBL[9990] ;
   7:   ;
   8:  LBL[100:OD GRIP] ;
   9:  $WAITTMOUT=100 ;
  10:  RO[1:T1 OPEN]=OFF ;
  11:  RO[2:T1 CLOSE]=ON ;
  12:  WAIT RI[1:T1 FULLY OPENED]=OFF TIMEOUT,LBL[9991] ;
  13:  WAIT    .75(sec) ;
  14:  WAIT RI[2:T1 FULLY CLOSED]=OFF TIMEOUT,LBL[9992] ;
  15:  JMP LBL[9999] ;
  16:   ;
  17:  LBL[200:ID GRIP] ;
  18:  $WAITTMOUT=200 ;
  19:  RO[2:T1 CLOSE]=OFF ;
  20:  RO[1:T1 OPEN]=ON ;
  21:  WAIT RI[2:T1 FULLY CLOSED]=OFF TIMEOUT,LBL[9993] ;
  22:  WAIT    .75(sec) ;
  23:  WAIT RI[1:T1 FULLY OPENED]=OFF TIMEOUT,LBL[9994] ;
  24:  JMP LBL[9999] ;
  25:   ;
  26:  LBL[9990:GRIPPER FAULT] ;
  27:  CALL TOOL_MSG(1) ;
  28:  UALM[1] ;
  29:  JMP LBL[1] ;
  30:   ;
  31:  LBL[9991:GRIPPER FAULT] ;
  32:  RO[2:T1 CLOSE]=OFF ;
  33:  RO[1:T1 OPEN]=ON ;
  34:  WAIT    .25(sec) ;
  35:  CALL TOOL_MSG(4) ;
  36:  UALM[1] ;
  37:  JMP LBL[1] ;
  38:   ;
  39:  LBL[9992:GRIPPER FAULT] ;
  40:  RO[2:T1 CLOSE]=OFF ;
  41:  RO[1:T1 OPEN]=ON ;
  42:  WAIT    .25(sec) ;
  43:  IF DO[36:PART PRESENT CHECK ONLY]=ON,JMP LBL[9999] ;
  44:  CALL TOOL_MSG(5) ;
  45:  UALM[1] ;
  46:  JMP LBL[1] ;
  47:   ;
  48:  LBL[9993:GRIPPER FAULT] ;
  49:  RO[1:T1 OPEN]=OFF ;
  50:  RO[2:T1 CLOSE]=ON ;
  51:  WAIT    .25(sec) ;
  52:  CALL TOOL_MSG(6) ;
  53:  UALM[1] ;
  54:  JMP LBL[1] ;
  55:   ;
  56:  LBL[9994:GRIPPER FAULT] ;
  57:  RO[1:T1 OPEN]=OFF ;
  58:  RO[2:T1 CLOSE]=ON ;
  59:  WAIT    .25(sec) ;
  60:  IF DO[36:PART PRESENT CHECK ONLY]=ON,JMP LBL[9999] ;
  61:  CALL TOOL_MSG(7) ;
  62:  UALM[1] ;
  63:  JMP LBL[1] ;
  64:   ;
  65:  LBL[9999:END] ;
  66:  IF (DO[174:PART IN T1]) THEN ;
  67:  MONITOR MON_T1 ;
  68:  ENDIF ;
  69:   ;
  70:  CALL PAYLOAD    ;
  71:  R[900:TOOL 1 COUNT]=R[900:TOOL 1 COUNT]-1    ;
  72:  DO[36:PART PRESENT CHECK ONLY]=OFF ;
/POS
/END
