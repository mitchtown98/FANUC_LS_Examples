/PROG  MEASURE_SUB_LOAD
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 1193;
CREATE		= DATE 23-11-18  TIME 09:22:34;
MODIFIED	= DATE 23-11-22  TIME 04:12:20;
FILE_NAME	= MEASURE_;
VERSION		= 0;
LINE_COUNT	= 45;
MEMORY_SIZE	= 1717;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  DO[217:ROBOT RETRACTED COM]=OFF ;
   2:  MONITOR MON_LATHE ;
   3:  CALL FAULT_MESSAGE('Robot is measuring raw part','X location in machine.') ;
   4:  $WAITTMOUT=500 ;
   5:  UFRAME_NUM=5 ;
   6:  UTOOL_NUM=1 ;
   7:  PR[12:JOINT OFFSET]=PR[12:JOINT OFFSET]-PR[12:JOINT OFFSET]    ;
   8:  IF (PR[81,6:MACH APPR]<0),PR[12,6:JOINT OFFSET]=(180) ;
   9:  IF (PR[81,6:MACH APPR]>=0),PR[12,6:JOINT OFFSET]=((-180)) ;
  10:J PR[81:MACH APPR] 100% CNT20 Offset,PR[12:JOINT OFFSET]    ;
  11:  R[40:RECOVER START PR]=81    ;
  12:  LBL[401] ;
  13:  --eg:SET SEARCH START AND STOP POSITIONS ;
  14:  PR[34:APP Z TOOL OFF]=PR[30:CARTESIAN ZERO]    ;
  15:  PR[31:APP Z OFFS]=PR[30:CARTESIAN ZERO]    ;
  16:  PR[95:SUB SRCH START]=PR[91:MASTER LOAD SUB]    ;
  17:  PR[96:SUB SRCH END]=PR[91:MASTER LOAD SUB]    ;
  18:  R[280:START SEARCH X]=PR[91,1:MASTER LOAD SUB]-R[284:MIN X SUB]    ;
  19:  R[286:END SEARCH X]=R[285:PART\JAW LENGTH]*(-1)    ;
  20:  PR[34,3:APP Z TOOL OFF]=R[280:START SEARCH X]    ;
  21:J PR[95:SUB SRCH START] 100% CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  22:  R[40:RECOVER START PR]=95    ;
  23:  SKIP CONDITION RI[7:SPRING COMPRESSED]=ON+    ;
  24:  PR[34,3:APP Z TOOL OFF]=R[286:END SEARCH X]    ;
  25:L PR[96:SUB SRCH END] R[86:SEARCH FAST SPD]mm/sec FINE Skip,LBL[580],PR[87:LOAD SUB]=LPOS Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  26:  R[40:RECOVER START PR]=96    ;
  27:  --eg:BACK OFF TO PREVENT COLLISION. ;
  28:  PR[34,3:APP Z TOOL OFF]=1    ;
  29:L PR[87:LOAD SUB] 100mm/sec FINE Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  30:  WAIT    .10(sec) ;
  31:  PR[87:LOAD SUB]=LPOS    ;
  32:  R[70:SUB SPIN RAW X]=(PR[87,1:LOAD SUB]) ;
  33:  DO[107:MEAS LOAD COMP]=ON ;
  34:  PR[34,3:APP Z TOOL OFF]=75    ;
  35:L PR[87:LOAD SUB] 2000mm/sec CNT10 Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  36:  R[40:RECOVER START PR]=81    ;
  37:J PR[81:MACH APPR] 100% CNT20 Offset,PR[12:JOINT OFFSET]    ;
  38:  JMP LBL[9999] ;
  39:  LBL[480:SKIP FAULT] ;
  40:  CALL MACH_MSG(20) ;
  41:  UALM[6] ;
  42:  JMP LBL[401] ;
  43:  LBL[9999] ;
  44:  CALL MESSAGE19    ;
  45:  CALL MESSAGE20    ;
/POS
/END
