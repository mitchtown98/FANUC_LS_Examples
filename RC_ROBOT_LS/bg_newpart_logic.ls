/PROG  BG_NEWPART_LOGIC
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 3745;
CREATE		= DATE 22-06-24  TIME 14:11:42;
MODIFIED	= DATE 24-03-05  TIME 14:01:56;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 187;
MEMORY_SIZE	= 4009;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  !TURN ON NEW PART ACTIVE IF       ;
   2:  !EITHER SIDE IS IN NEW PART MODE  ;
   3:  IF (DI[103:NEW PART HMI PB]=ON AND UO[3:Prg running]=OFF) THEN ;
   4:   ;
   5:  IF (DI[1:TABLE AT A POSITION]=ON OR DI[2:TABLE AT B POSITION]=ON) THEN ;
   6:   ;
   7:  DO[104:NEW PART ACTIVE]=ON ;
   8:   ;
   9:  IF (DI[1:TABLE AT A POSITION]=OFF AND DO[104:NEW PART ACTIVE]=ON),DO[102:NEW PART A]=(ON) ;
  10:   ;
  11:  IF (DI[1:TABLE AT A POSITION]=OFF AND DO[104:NEW PART ACTIVE]=ON),DO[103:NEW PART B]=(OFF) ;
  12:   ;
  13:  IF (DI[2:TABLE AT B POSITION]=OFF AND DO[104:NEW PART ACTIVE]=ON),DO[103:NEW PART B]=(ON) ;
  14:   ;
  15:  IF (DI[2:TABLE AT B POSITION]=OFF AND DO[104:NEW PART ACTIVE]=ON),DO[102:NEW PART A]=(OFF) ;
  16:   ;
  17:  DO[51:PALLET TYPE FOUND]=OFF ;
  18:  DO[52:PALLET HEIGHT FOUND]=OFF ;
  19:  DO[53:PART LENGTH FOUND]=OFF ;
  20:  DO[54:PART DIAMETER FOUND]=OFF ;
  21:  DO[115:FINE LENGTH CHECKED]=OFF ;
  22:  ENDIF ;
  23:  ENDIF ;
  24:   ;
  25:  --eg:CLEAR NEW PART ACTIVE IF BOTH ON ;
  26:  IF (DO[104:NEW PART ACTIVE]=ON AND DO[102:NEW PART A]=ON AND DO[103:NEW PART B]=ON) THEN ;
  27:   ;
  28:  DO[25:A SIDE LOAD COMPLETE]=OFF ;
  29:  DO[26:B SIDE LOAD COMPLETE]=OFF ;
  30:  DO[102:NEW PART A]=OFF ;
  31:  DO[103:NEW PART B]=OFF ;
  32:  ENDIF ;
  33:   ;
  34:  !PALLET SETUP IS COMPLETE         ;
  35:  IF (DO[51:PALLET TYPE FOUND]=ON AND DO[52:PALLET HEIGHT FOUND]=ON AND DO[53:PART LENGTH FOUND]=ON AND DO[54:PART DIAMETER FOUND]=ON AND DO[115:FINE LENGTH CHECKED]=ON) THEN ;
  36:   ;
  37:  DO[50:PALLET SETUP COMPLETE]=ON ;
  38:   ;
  39:  ELSE ;
  40:  DO[50:PALLET SETUP COMPLETE]=OFF ;
  41:  ENDIF ;
  42:   ;
  43:  !RESET PALLET IF TABLE UNLOCKED   ;
  44:  IF (DO[42:TABLE READY FOR SERVICE]=OFF) THEN ;
  45:   ;
  46:  DO[31:CYCLE STOP ACTIVE]=OFF ;
  47:  DO[32:PURGE ACTIVE]=OFF ;
  48:  DO[34:REGRIP CHECKED]=OFF ;
  49:  DO[37:TABLE A READY]=OFF ;
  50:  DO[38:TABLE B READY]=OFF ;
  51:  DO[39:TABLE SIDE A COMPLETE]=OFF ;
  52:  DO[40:TABLE SIDE B COMPLETE]=OFF ;
  53:  DO[41:TABLE READY TO ROTATE]=OFF ;
  54:  DO[43:TABLE PICK COMPLETE]=OFF ;
  55:  DO[44:TABLE DROP COMPLETE]=OFF ;
  56:  DO[51:PALLET TYPE FOUND]=OFF ;
  57:  DO[52:PALLET HEIGHT FOUND]=OFF ;
  58:  DO[53:PART LENGTH FOUND]=OFF ;
  59:  DO[54:PART DIAMETER FOUND]=OFF ;
  60:  DO[105:MEAS LOAD ACTIVE]=OFF ;
  61:  DO[106:MEAS UNLOAD ACTIVE]=OFF ;
  62:  DO[115:FINE LENGTH CHECKED]=OFF ;
  63:  DO[124:PINS REMOVED]=OFF ;
  64:  R[2:CUR ROW PICK (X)]=1    ;
  65:  R[3:CUR COL PICK (Y)]=1    ;
  66:  R[4:CUR ROW DROP (X)]=1    ;
  67:  R[5:CUR COL DROP (Y)]=1    ;
  68:   ;
  69:  !RESET NEW PART LOGIC IF TABLE    ;
  70:  !UNLOCKED AND NEW PART ACTIVE     ;
  71:   ;
  72:  IF (DO[104:NEW PART ACTIVE]=ON) THEN ;
  73:  DO[107:MEAS LOAD COMP]=OFF ;
  74:  DO[108:MEAS UNLD COMP]=OFF ;
  75:  DO[125:PART INSIDE SLOT]=OFF ;
  76:  R[1:ACTIVE TEMPLATE]=0    ;
  77:  R[66:TEMP Z TO TABLE]=0    ;
  78:  R[77:RAW DIAMETER]=0    ;
  79:  R[80:RAW CENTER X OFF]=0    ;
  80:  R[81:FINISHED DIA]=0    ;
  81:  ENDIF ;
  82:  ENDIF ;
  83:   ;
  84:  !TURN ON MEASURE LOAD SIGNAL      ;
  85:  !IF STARTING NEW PART SETUP       ;
  86:  IF (R[2:CUR ROW PICK (X)]=1 AND R[3:CUR COL PICK (Y)]=1 AND R[4:CUR ROW DROP (X)]=1 AND R[5:CUR COL DROP (Y)]=1 AND DO[107:MEAS LOAD COMP]=OFF AND DO[63:NO LOAD ON]=OFF AND DO[104:NEW PART ACTIVE]=ON) THEN ;
  87:  DO[105:MEAS LOAD ACTIVE]=ON ;
  88:   ;
  89:  ELSE ;
  90:  DO[105:MEAS LOAD ACTIVE]=OFF ;
  91:  ENDIF ;
  92:   ;
  93:  !TURN ON MEASURE UNLOAD SIGNAL    ;
  94:  !IF STARTING NEW PART SETUP       ;
  95:  IF (R[2:CUR ROW PICK (X)]=1 AND R[3:CUR COL PICK (Y)]=1 AND R[4:CUR ROW DROP (X)]=1 AND R[5:CUR COL DROP (Y)]=1 AND DO[107:MEAS LOAD COMP]=ON AND DO[108:MEAS UNLD COMP]=OFF AND DO[68:NO UNLOAD ON]=OFF AND 
    :  DO[104:NEW PART ACTIVE]=ON) THEN ;
  96:   ;
  97:  DO[106:MEAS UNLOAD ACTIVE]=ON ;
  98:   ;
  99:  ELSE ;
 100:  DO[106:MEAS UNLOAD ACTIVE]=OFF ;
 101:  ENDIF ;
 102:   ;
 103:  !TURN ON MEASURE COMPLETE IF NO   ;
 104:  !LOAD OR NO UNLOAD IS ACTIVE      ;
 105:  IF (DO[63:NO LOAD ON]=ON) THEN ;
 106:  DO[107:MEAS LOAD COMP]=ON ;
 107:  DO[53:PART LENGTH FOUND]=ON ;
 108:  DO[54:PART DIAMETER FOUND]=ON ;
 109:  DO[115:FINE LENGTH CHECKED]=ON ;
 110:  R[77:RAW DIAMETER]=R[804:FINISHED DIA.]    ;
 111:  R[67:MEAS PART LENGTH]=R[806:FINISHED LENGTH]    ;
 112:  ENDIF ;
 113:   ;
 114:  IF (DO[68:NO UNLOAD ON]=ON),DO[108:MEAS UNLD COMP]=(ON) ;
 115:   ;
 116:  !TURN OFF MEASURE COMPLETE        ;
 117:  IF (DI[66:UNLOAD MAINSPINDLE ON]=ON AND DO[108:MEAS UNLD COMP]=ON),DO[108:MEAS UNLD COMP]=(OFF) ;
 118:   ;
 119:  IF (DI[67:UNLOAD SUBSPINDLE ON]=ON AND DO[108:MEAS UNLD COMP]=ON),DO[108:MEAS UNLD COMP]=(OFF) ;
 120:   ;
 121:  !IF SETUP BUTTON IS PRESSED       ;
 122:  IF (DI[56:OKUMA LATHE]=ON OR DI[57:OKUMA MILL]=ON OR DI[58:HAAS LATHE]=ON OR DI[61:LOAD MAINSPINDLE ON]=ON OR DI[62:LOAD SUBSPINDLE ON]=ON OR DI[63:NO LOAD ON]=ON OR DI[66:UNLOAD MAINSPINDLE ON]=ON OR 
    :  DI[67:UNLOAD SUBSPINDLE ON]=ON OR DI[68:NO UNLOAD ON]=ON OR DI[116:T1 OD GRIP]=ON OR DI[117:T1 ID GRIP]=ON OR DI[118:T2 OD GRIP]=ON OR DI[119:T2 ID GRIP]=ON OR DI[55:OKUMA NO ANYBUS]=ON) THEN ;
 123:   ;
 124:  DO[113:SETUP BUTTON PRESSED]=ON ;
 125:   ;
 126:  ELSE ;
 127:  DO[113:SETUP BUTTON PRESSED]=OFF ;
 128:  ENDIF ;
 129:  !LOGIC FOR DIRECT ENTRY SETUP     ;
 130:  !LASER IS NOT BEING USED          ;
 131:  IF (DI[48:MEAS RAW DIA]=OFF AND R[800:RAW DIAMETER]>0) THEN ;
 132:  R[77:RAW DIAMETER]=R[800:RAW DIAMETER]    ;
 133:  DO[54:PART DIAMETER FOUND]=ON ;
 134:  ENDIF ;
 135:   ;
 136:  IF (DI[49:MEAS RAW LENGTH]=OFF AND R[802:RAW LENGTH]>0) THEN ;
 137:  R[67:MEAS PART LENGTH]=R[802:RAW LENGTH]    ;
 138:  DO[53:PART LENGTH FOUND]=ON ;
 139:  DO[115:FINE LENGTH CHECKED]=ON ;
 140:  ENDIF ;
 141:   ;
 142:  IF (DI[50:MEAS FINISHED DIA]=OFF) THEN ;
 143:   ;
 144:  R[81:FINISHED DIA]=R[804:FINISHED DIA.]    ;
 145:   ;
 146:  ELSE ;
 147:  R[81:FINISHED DIA]=R[77:RAW DIAMETER]    ;
 148:  ENDIF ;
 149:   ;
 150:  IF (DI[51:MEAS FINISHED LENGTH]=OFF) THEN ;
 151:   ;
 152:  R[72:FIN PART LENGTH]=R[806:FINISHED LENGTH]    ;
 153:   ;
 154:  ELSE ;
 155:  R[72:FIN PART LENGTH]=R[67:MEAS PART LENGTH]    ;
 156:  ENDIF ;
 157:   ;
 158:  IF (DI[52:MEAS TMPLT H]=OFF AND R[814:TMPLT HEIGHT]>0) THEN ;
 159:  R[66:TEMP Z TO TABLE]=R[814:TMPLT HEIGHT]    ;
 160:  DO[52:PALLET HEIGHT FOUND]=ON ;
 161:  ENDIF ;
 162:   ;
 163:  IF (DI[53:MEAS TMPLT NUM]=OFF AND R[812:TMPLT ID]>0) THEN ;
 164:  R[1:ACTIVE TEMPLATE]=R[812:TMPLT ID]    ;
 165:  DO[51:PALLET TYPE FOUND]=ON ;
 166:  ENDIF ;
 167:   ;
 168:  !SET HMI SETUP COMPLETE SIGNAL    ;
 169:  IF (DI[105:LOAD SETUP DONE]=ON AND DI[106:UNLOAD SETUP DONE]=ON AND DI[107:ADVANCED SETUP DONE]=ON AND DI[110:HMI PART SETUP COMPLETE]=ON) THEN ;
 170:   ;
 171:  DO[109:PART HMI SETUP COMPLETE]=ON ;
 172:   ;
 173:  ELSE ;
 174:  DO[109:PART HMI SETUP COMPLETE]=OFF ;
 175:  ENDIF ;
 176:   ;
 177:  !RESET OUTPUTS IF DIMENSION IS    ;
 178:  !SET TO ZERO                      ;
 179:  IF (R[67:MEAS PART LENGTH]=0),DO[53:PART LENGTH FOUND]=(OFF) ;
 180:   ;
 181:  IF (R[67:MEAS PART LENGTH]=0),DO[115:FINE LENGTH CHECKED]=(OFF) ;
 182:   ;
 183:  IF (R[77:RAW DIAMETER]=0),DO[54:PART DIAMETER FOUND]=(OFF) ;
 184:   ;
 185:  IF (R[66:TEMP Z TO TABLE]=0),DO[52:PALLET HEIGHT FOUND]=(OFF) ;
 186:   ;
 187:  IF (R[1:ACTIVE TEMPLATE]=0),DO[51:PALLET TYPE FOUND]=(OFF) ;
/POS
/END
