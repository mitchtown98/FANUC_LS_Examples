/PROG  BG_SYS_LOGIC
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "BACKGROUND";
PROG_SIZE	= 5261;
CREATE		= DATE 21-03-09  TIME 10:58:32;
MODIFIED	= DATE 24-03-05  TIME 11:13:42;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 205;
MEMORY_SIZE	= 5677;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  !FAULT RESET                      ;
   2:  F[104:FAULT RESET]=(DI[43:ROBOT FAULT RESET]) ;
   3:   ;
   4:  !HOME POSITION CHECK              ;
   5:  IF (DO[145:AT HOME]=ON OR DO[146:TABLE PERCH]=ON OR DO[147:MACHINE PERCH]=ON OR DO[148:PERCH SPARE]=ON OR DO[149:PERCH SPARE]=ON OR DO[150:SINGULARITY]=ON OR DO[151:PERCH SPARE]=ON OR 
    :  DO[152:AT MAINTENANCE]=ON) THEN ;
   6:   ;
   7:  DO[160:ROBOT PERCHED]=ON ;
   8:  DO[217:ROBOT RETRACTED COM]=ON ;
   9:   ;
  10:  IF (DO[145:AT HOME]=ON),R[40:RECOVER START PR]=(1) ;
  11:  ELSE ;
  12:  DO[160:ROBOT PERCHED]=OFF ;
  13:  ENDIF ;
  14:   ;
  15:  !STEPMODE STATUS                  ;
  16:  DO[30:IN STEP MODE]=($SSR.$SINGLESTEP) ;
  17:   ;
  18:  !MONITOR IF ROBOT IS JOGGED       ;
  19:  IF (DO[28:ROBOT JOGGED]=OFF AND $MOR_GRP[1].$JOGGED=1) THEN ;
  20:  DO[28:ROBOT JOGGED]=ON ;
  21:  ENDIF ;
  22:   ;
  23:  IF (DO[28:ROBOT JOGGED]=ON AND DO[145:AT HOME]=ON) THEN ;
  24:  DO[28:ROBOT JOGGED]=OFF ;
  25:  ENDIF ;
  26:   ;
  27:  IF (DO[31:CYCLE STOP ACTIVE]=ON),DO[32:PURGE ACTIVE]=(ON) ;
  28:   ;
  29:  !CYCLE STOP AND PURGE CONTROL     ;
  30:  IF (DI[31:CYCLE STOP REQ]=ON OR DI[32:PURGE REQ]=ON OR DI[103:NEW PART HMI PB]=ON),F[1:I/O ONE SHOT 1]=PULSE   ;
  31:   ;
  32:  IF (F[1:I/O ONE SHOT 1]=ON AND DI[31:CYCLE STOP REQ]=ON),DO[31:CYCLE STOP ACTIVE]=(!DO[31:CYCLE STOP ACTIVE]) ;
  33:   ;
  34:  IF (F[1:I/O ONE SHOT 1]=ON AND DI[32:PURGE REQ]=ON),DO[32:PURGE ACTIVE]=(!DO[32:PURGE ACTIVE]) ;
  35:   ;
  36:  !CONTROL LOAD COMPLETE SIGNALS    ;
  37:  IF (DI[47:LOAD COMPLETE HMI]=ON AND DO[42:TABLE READY FOR SERVICE]=ON),DO[41:TABLE READY TO ROTATE]=(ON) ;
  38:   ;
  39:  IF (DI[47:LOAD COMPLETE HMI]=ON AND DO[42:TABLE READY FOR SERVICE]=ON AND DI[1:TABLE AT A POSITION]=ON),DO[26:B SIDE LOAD COMPLETE]=(ON) ;
  40:   ;
  41:  IF (DI[47:LOAD COMPLETE HMI]=ON AND DO[42:TABLE READY FOR SERVICE]=ON AND DI[2:TABLE AT B POSITION]=ON),DO[25:A SIDE LOAD COMPLETE]=(ON) ;
  42:   ;
  43:  IF (DI[45:ABORT LOAD SIDE]=ON AND DO[25:A SIDE LOAD COMPLETE]=ON AND DI[2:TABLE AT B POSITION]=ON),DO[25:A SIDE LOAD COMPLETE]=(OFF) ;
  44:   ;
  45:  IF (DI[45:ABORT LOAD SIDE]=ON AND DO[26:B SIDE LOAD COMPLETE]=ON AND DI[1:TABLE AT A POSITION]=ON),DO[26:B SIDE LOAD COMPLETE]=(OFF) ;
  46:   ;
  47:  IF (DI[45:ABORT LOAD SIDE]=ON AND DO[41:TABLE READY TO ROTATE]=ON),DO[41:TABLE READY TO ROTATE]=(OFF) ;
  48:   ;
  49:  !WRITES ACTIVE FRAMES TO REGISTE  ;
  50:  R[60:CURRENT UFRAME]=($MNUFRAMENUM[1]) ;
  51:  R[61:CURRENT UTOOL]=($MNUTOOLNUM[1]) ;
  52:   ;
  53:  !SPEED CONTROL FOR AUTO           ;
  54:  IF (R[41:GLOBAL SPEED]<10 OR R[41:GLOBAL SPEED]>100),R[41:GLOBAL SPEED]=(100) ;
  55:   ;
  56:  IF (R[48:MACH SPEED]<10 OR R[48:MACH SPEED]>175),R[48:MACH SPEED]=(100) ;
  57:   ;
  58:  IF (R[41:GLOBAL SPEED]>=10 AND R[41:GLOBAL SPEED]<=100 AND UO[8:TP enabled]=OFF AND DI[3:SCANNER WARNING CLEAR]=ON AND DI[4:SCANNER BLOCKED]=OFF AND GO[1:ACTIVE PNS NUM]<>2 AND DI[470:NOT IN MACHINE]=ON) THEN ;
  59:  R[42:REDUCED SPEED]=R[41:GLOBAL SPEED]/2.875    ;
  60:  ENDIF ;
  61:   ;
  62:  IF (UO[8:TP enabled]=OFF AND DO[202:RECOVER ACTIVE]=ON AND DI[3:SCANNER WARNING CLEAR]=ON) THEN ;
  63:  R[42:REDUCED SPEED]=15    ;
  64:  ENDIF ;
  65:   ;
  66:  IF (UO[8:TP enabled]=OFF AND DI[3:SCANNER WARNING CLEAR]=OFF) THEN ;
  67:  R[42:REDUCED SPEED]=10    ;
  68:  ENDIF ;
  69:   ;
  70:  IF (UO[8:TP enabled]=OFF AND DO[150:SINGULARITY]=ON) THEN ;
  71:  R[42:REDUCED SPEED]=20    ;
  72:  ENDIF ;
  73:   ;
  74:  !LOAD/UNLOAD MACHINE SPEED        ;
  75:  IF (UO[8:TP enabled]=OFF AND R[48:MACH SPEED]>=10 AND R[48:MACH SPEED]<=175 AND DO[104:NEW PART ACTIVE]=OFF AND DI[470:NOT IN MACHINE]=OFF AND DI[3:SCANNER WARNING CLEAR]=ON AND DI[4:SCANNER BLOCKED]=OFF AND 
    :  GO[1:ACTIVE PNS NUM]=1),R[42:REDUCED SPEED]=(R[48:MACH SPEED]/2.5) ;
  76:   ;
  77:  IF (UO[8:TP enabled]=OFF),$MCR.$GENOVERRIDE=(R[42:REDUCED SPEED]) ;
  78:  R[45:CURRENT OVERRIDE]=($MCR.$GENOVERRIDE) ;
  79:   ;
  80:  --eg:CALCULATE REGISTERS USED TO ADJUST MOTION SPEED. THESE CALCULATIONS
    :  ARE FOR LASER MOTION TO ENSURE SPEED IS CONSTANT REGARDLESS OF OVERRIDE
    :  DURING MEASUREMENTS ;
  81:  R[87:MAX SPD RATIO]=R[45:CURRENT OVERRIDE]/100    ;
  82:  R[90:CALC SLOW]=R[88:NOM SLOW mm/sec]/R[87:MAX SPD RATIO]    ;
  83:  R[91:CALC FAST]=R[89:NOM FAST mm/sec]/R[87:MAX SPD RATIO]    ;
  84:  R[85:SEARCH SLOW SPD]=R[90:CALC SLOW]    ;
  85:  R[86:SEARCH FAST SPD]=R[91:CALC FAST]    ;
  86:   ;
  87:  !CONTROL STATUS LIGHTS            ;
  88:  IF ((DI[1:TABLE AT A POSITION]=ON AND DO[37:TABLE A READY]=OFF AND DO[41:TABLE READY TO ROTATE]=OFF AND DO[39:TABLE SIDE A COMPLETE]=ON AND DO[104:NEW PART ACTIVE]=OFF) OR (DI[2:TABLE AT B POSITION]=ON AND 
    :  DO[38:TABLE B READY]=OFF AND DO[41:TABLE READY TO ROTATE]=OFF AND DO[40:TABLE SIDE B COMPLETE]=OFF AND DO[104:NEW PART ACTIVE]=OFF)) THEN ;
  89:  DO[3:MATERIAL LAMP]=(DO[27:Heartbeat]) ;
  90:  ELSE ;
  91:  IF (DO[104:NEW PART ACTIVE]=ON) THEN ;
  92:  DO[3:MATERIAL LAMP]=ON ;
  93:  ELSE ;
  94:  DO[3:MATERIAL LAMP]=OFF ;
  95:  ENDIF ;
  96:  ENDIF ;
  97:   ;
  98:  IF (UO[6:Fault]=ON AND DO[200:Input Simulated]=OFF AND DO[201:Output Simulated]=OFF AND DO[104:NEW PART ACTIVE]=OFF) THEN ;
  99:  DO[2:FAULT LAMP]=ON ;
 100:  ENDIF ;
 101:   ;
 102:  IF (UO[6:Fault]=ON) AND (DO[200:Input Simulated]=ON OR DO[201:Output Simulated]=ON OR DO[104:NEW PART ACTIVE]=ON) THEN ;
 103:  DO[2:FAULT LAMP]=(DO[27:Heartbeat]) ;
 104:  ENDIF ;
 105:   ;
 106:  IF (UO[6:Fault]=OFF AND DO[200:Input Simulated]=OFF AND DO[201:Output Simulated]=OFF) THEN ;
 107:  DO[2:FAULT LAMP]=OFF ;
 108:  ENDIF ;
 109:   ;
 110:  DO[111:ROBOT FAULTED]=(UO[6:Fault]) ;
 111:  DO[110:ROBOT RUNNING]=(UO[10:Busy]) ;
 112:   ;
 113:  IF (UO[10:Busy]=ON AND DO[3:MATERIAL LAMP]=OFF) THEN ;
 114:  DO[4:ROBOT RUNNING]=ON ;
 115:  ELSE ;
 116:  DO[4:ROBOT RUNNING]=OFF ;
 117:  ENDIF ;
 118:   ;
 119:  --eg:CALCULATE CURRENT SLOT NUMBER BASED ON CURRENT ROW AND COLUMN VALUES ;
 120:  R[6:CUR PICK SLOT #]=(((R[2:CUR ROW PICK (X)]-1)*(R[11:NUMBER OF COLS]))+(R[3:CUR COL PICK (Y)])) ;
 121:  R[7:CUR DROP SLOT #]=(((R[4:CUR ROW DROP (X)]-1)*(R[11:NUMBER OF COLS]))+(R[5:CUR COL DROP (Y)])) ;
 122:   ;
 123:  --eg:SET REGISTERS TO USE AS INDERCT VALUES FOR THE CURRENT ROW THE ROBOT
    :  IS WORKING ON TO DETERMINE START/STOP SLOT ON TABLE ;
 124:  R[36:PICK START REG]=R[2:CUR ROW PICK (X)]+R[2:CUR ROW PICK (X)]+12    ;
 125:  R[37:PICK STOP REG]=R[36:PICK START REG]+1    ;
 126:  R[38:DROP START REG]=R[4:CUR ROW DROP (X)]+R[4:CUR ROW DROP (X)]+12    ;
 127:  R[39:DROP STOP REG]=R[38:DROP START REG]+1    ;
 128:   ;
 129:  !SET TABLE READY FOR SERVICE SIG  ;
 130:  IF (DI[6:TABLE LOCKED IN POSITION]=ON) AND ((DI[1:TABLE AT A POSITION]=ON AND DI[2:TABLE AT B POSITION]=OFF) OR (DI[1:TABLE AT A POSITION]=OFF AND DI[2:TABLE AT B POSITION]=ON)) THEN ;
 131:  DO[42:TABLE READY FOR SERVICE]=ON ;
 132:  ELSE ;
 133:  DO[42:TABLE READY FOR SERVICE]=OFF ;
 134:  ENDIF ;
 135:   ;
 136:  !  Side A Complete Logic ;
 137:  IF (DI[1:TABLE AT A POSITION]=ON AND DO[43:TABLE PICK COMPLETE]=ON AND DO[44:TABLE DROP COMPLETE]=ON) THEN ;
 138:  DO[39:TABLE SIDE A COMPLETE]=ON ;
 139:  ELSE ;
 140:  DO[39:TABLE SIDE A COMPLETE]=OFF ;
 141:  ENDIF ;
 142:   ;
 143:  !  Side B Complete Logic ;
 144:  IF (DI[2:TABLE AT B POSITION]=ON AND DO[43:TABLE PICK COMPLETE]=ON AND DO[44:TABLE DROP COMPLETE]=ON) THEN ;
 145:  DO[40:TABLE SIDE B COMPLETE]=ON ;
 146:  ELSE ;
 147:  DO[40:TABLE SIDE B COMPLETE]=OFF ;
 148:  ENDIF ;
 149:   ;
 150:  !  Side A NOT Ready ;
 151:  IF (DI[1:TABLE AT A POSITION]=ON AND DO[39:TABLE SIDE A COMPLETE]=ON) THEN ;
 152:  DO[37:TABLE A READY]=OFF ;
 153:  ENDIF ;
 154:   ;
 155:  !  Side B NOT Ready ;
 156:  IF (DI[2:TABLE AT B POSITION]=ON AND DO[40:TABLE SIDE B COMPLETE]=ON) THEN ;
 157:  DO[38:TABLE B READY]=OFF ;
 158:  ENDIF ;
 159:   ;
 160:  !SET TABLE IN POSITION SIGNAL     ;
 161:  IF ((DI[1:TABLE AT A POSITION]=ON AND DI[2:TABLE AT B POSITION]=OFF) OR (DI[1:TABLE AT A POSITION]=OFF AND DI[2:TABLE AT B POSITION]=ON)) THEN ;
 162:  DO[35:TABLE IN POSITION]=ON ;
 163:  ELSE ;
 164:  DO[35:TABLE IN POSITION]=OFF ;
 165:  ENDIF ;
 166:   ;
 167:  !UNLOCK TABLE IF NOT IN POSITION  ;
 168:  IF (DO[35:TABLE IN POSITION]=OFF) THEN ;
 169:  DO[5:TURN TABLE UNLOCK SOL]=ON ;
 170:  ENDIF ;
 171:   ;
 172:  !MANUAL TABLE LOCK CONTROLS       ;
 173:  IF (DI[38:TABLE UNLOCK (HMI)]=ON AND UO[10:Busy]=OFF AND DO[5:TURN TABLE UNLOCK SOL]=OFF AND DO[145:AT HOME]=ON),DO[5:TURN TABLE UNLOCK SOL]=(ON) ;
 174:  IF (DI[39:TABLE LOCK (HMI)]=ON AND UO[10:Busy]=OFF AND DO[5:TURN TABLE UNLOCK SOL]=ON AND DO[145:AT HOME]=ON AND DO[35:TABLE IN POSITION]=ON),DO[5:TURN TABLE UNLOCK SOL]=(OFF) ;
 175:   ;
 176:  !SET UI SIGNAL FROM DIN           ;
 177:  IF (DI[42:ROBOT HOLD]=OFF OR DI[7:MACH CNTRL SAFE]=OFF) THEN ;
 178:  F[101:*HOLD]=(OFF) ;
 179:  ELSE ;
 180:  F[101:*HOLD]=(ON) ;
 181:  ENDIF ;
 182:  F[100:*IMSTP]=(DI[8:SYSTEM AIR OK]) ;
 183:  F[102:*SFSPD]=(DI[8:SYSTEM AIR OK]) ;
 184:  F[107:ENABLE]=(DI[8:SYSTEM AIR OK]) ;
 185:   ;
 186:  !AIR FILTER TIMER                 ;
 187:  IF (R[302:HOURS]>0 AND DO[27:Heartbeat]=ON),F[90:COUNT AIR FILTER]=PULSE   ;
 188:   ;
 189:  IF (F[90:COUNT AIR FILTER]),R[303:SECONDS]=(R[303:SECONDS]-1) ;
 190:   ;
 191:  IF (R[303:SECONDS]=0) THEN ;
 192:  R[302:HOURS]=R[302:HOURS]-1    ;
 193:  R[303:SECONDS]=3600    ;
 194:  ENDIF ;
 195:   ;
 196:  --eg:COLLISION AT DROP ;
 197:  IF (R[40:RECOVER START PR]>=65 AND R[40:RECOVER START PR]<=69 AND DO[177:COLLISION]),DO[114:COLLISION AT DROP]=(ON) ;
 198:   ;
 199:  --eg:SAVE CURRENT JOINT VALUES TO REGISTERS ;
 200:  R[137:J1 POS]=$SCR_GRP[1].$MCH_ANG[1] ;
 201:  R[138:J2 POS]=$SCR_GRP[1].$MCH_ANG[2] ;
 202:  R[139:J3 POS]=$SCR_GRP[1].$MCH_ANG[3] ;
 203:  R[140:J4 POS]=$SCR_GRP[1].$MCH_ANG[4] ;
 204:  R[141:J5 POS]=$SCR_GRP[1].$MCH_ANG[5] ;
 205:  R[142:J6 POS]=$SCR_GRP[1].$MCH_ANG[6] ;
/POS
/END
