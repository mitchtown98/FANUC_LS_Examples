/PROG  SETUP_TMPLT_HEIGHT
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "TEMPLATE Z";
PROG_SIZE	= 1387;
CREATE		= DATE 22-07-25  TIME 15:33:54;
MODIFIED	= DATE 23-11-16  TIME 05:12:56;
FILE_NAME	= TEACH_UT;
VERSION		= 0;
LINE_COUNT	= 58;
MEMORY_SIZE	= 1803;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;
/MN
   1:  --eg:FIND THE Z HEIGHT OF THE TOP TEMPLATE TO SET LIMITS FOR LASER
    :  SEARCH. ;
   2:   ;
   3:  LBL[1:START] ;
   4:  CALL FAULT_MESSAGE('Robot is measuring the template','height.') ;
   5:J PR[1:HOME POSITION] 100% CNT10    ;
   6:  R[40:RECOVER START PR]=1    ;
   7:J PR[15:LEN MEAS POUNCE] 100% CNT20    ;
   8:  R[40:RECOVER START PR]=15    ;
   9:  PR[31:APP Z OFFS]=PR[30:CARTESIAN ZERO]    ;
  10:  RO[8:LASER ON]=ON ;
  11:  --eg:SPEED CONTROL FOR LASER SEARCH. SPEED IS CALCULATED TO ALWAYS MOVE
    :  AT SAME RATE BASED ON MODE AND OVERRIDE. MAX IS 100. ;
  12:  R[88:NOM SLOW mm/sec]=15    ;
  13:  R[89:NOM FAST mm/sec]=40    ;
  14:   ;
  15:  LBL[100:FAST SEARCH] ;
  16:  IF (DI[1:TABLE AT A POSITION]=ON) THEN ;
  17:  UFRAME_NUM=1 ;
  18:  ELSE ;
  19:  UFRAME_NUM=2 ;
  20:  ENDIF ;
  21:  UTOOL_NUM=9 ;
  22:  RO[8:LASER ON]=ON ;
  23:  PR[31,3:APP Z OFFS]=300    ;
  24:J PR[45:TEMP Z SEARCH] 100% FINE Offset,PR[31:APP Z OFFS]    ;
  25:  WAIT    .25(sec) ;
  26:  SKIP CONDITION RI[8:LASER INPUT]=ON+    ;
  27:L PR[45:TEMP Z SEARCH] R[86:SEARCH FAST SPD]mm/sec FINE Skip,LBL[180],PR[47:LPOS 1]=LPOS    ;
  28:  JMP LBL[200] ;
  29:   ;
  30:  LBL[180:NO TEMPLATE] ;
  31:  CALL LASER_MSG(3) ;
  32:  UALM[12] ;
  33:  JMP LBL[100] ;
  34:   ;
  35:  LBL[200:SLOW SEARCH] ;
  36:  PR[31,3:APP Z OFFS]=10    ;
  37:L PR[47:LPOS 1] 500mm/sec FINE    ;
  38:  WAIT    .25(sec) ;
  39:  SKIP CONDITION RI[8:LASER INPUT]=OFF-    ;
  40:L PR[47:LPOS 1] R[85:SEARCH SLOW SPD]mm/sec FINE Offset,PR[31:APP Z OFFS] Skip,LBL[280],PR[46:TEMP Z FOUND]=LPOS    ;
  41:  R[66:TEMP Z TO TABLE]=PR[46,3:TEMP Z FOUND]    ;
  42:  DO[52:PALLET HEIGHT FOUND]=ON ;
  43:  JMP LBL[9999] ;
  44:   ;
  45:  LBL[280:NO TEMPLATE] ;
  46:  CALL LASER_MSG(3) ;
  47:  UALM[12] ;
  48:  JMP LBL[200] ;
  49:   ;
  50:  LBL[9999:END] ;
  51:  R[40:RECOVER START PR]=15    ;
  52:J PR[15:LEN MEAS POUNCE] 100% CNT20    ;
  53:   ;
  54:  IF (DO[51:PALLET TYPE FOUND]=ON) THEN ;
  55:  RO[8:LASER ON]=OFF ;
  56:J PR[1:HOME POSITION] 100% FINE    ;
  57:  R[40:RECOVER START PR]=1    ;
  58:  ENDIF ;
/POS
/END
