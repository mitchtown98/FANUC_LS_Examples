/PROG  CHECK_FIRST_SLOT
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 2043;
CREATE		= DATE 22-12-09  TIME 14:27:10;
MODIFIED	= DATE 22-12-09  TIME 14:57:14;
FILE_NAME	= TABLE_PI;
VERSION		= 0;
LINE_COUNT	= 87;
MEMORY_SIZE	= 2503;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:   ;
   2:  --eg:PICK RAW PART FROM NEXT AVAILABLE SLOT ;
   3:   ;
   4:  LBL[1:START] ;
   5:  CALL FAULT_MESSAGE('Robot is checking first slot',' is empty.') ;
   6:  COL GUARD ADJUST 100 ;
   7:  R[88:NOM SLOW mm/sec]=50    ;
   8:  R[89:NOM FAST mm/sec]=100    ;
   9:  IF DO[42:TABLE READY FOR SERVICE]=OFF,JMP LBL[9980] ;
  10:  IF DO[176:ALL TOOLS UNGRIPPED]=OFF,JMP LBL[9981] ;
  11:   ;
  12:   ;
  13:  --eg:SET R150 EQUAL TO THE POSITION REGISTER USED FOR THE ACTIVE TEMPLATE
    :  TYPE  ;
  14:  IF (R[1:ACTIVE TEMPLATE]=2),R[150:TABLE PICK PR]=(50) ;
  15:  IF (R[1:ACTIVE TEMPLATE]=3),R[150:TABLE PICK PR]=(51) ;
  16:  IF (R[1:ACTIVE TEMPLATE]=4),R[150:TABLE PICK PR]=(52) ;
  17:  IF (R[1:ACTIVE TEMPLATE]=5),R[150:TABLE PICK PR]=(53) ;
  18:  IF (R[1:ACTIVE TEMPLATE]=6),R[150:TABLE PICK PR]=(54) ;
  19:   ;
  20:J PR[10:TABLE PK POUNCE] 100% CNT20    ;
  21:  R[40:RECOVER START PR]=10    ;
  22:   ;
  23:  LBL[10:CALCULATE PICK] ;
  24:  --eg:WHEN PICKING THE FIRST PART FROM THE TABLE CALL THIS PROGRAM TO
    :  DETERMINE STARTING LOCATION BASED ON TEMPLATE TYPE ;
  25:  CALL FIRST_PICK_SLOT    ;
  26:  --eg:CALCULATE XYZ OF THE CURRENT SLOT ;
  27:  CALL CALC_PICK_OFFSET    ;
  28:  --eg:CALCULATE XYZ OF THE CURRENT SLOT ;
  29:  CALL CALC_PICK_R    ;
  30:   ;
  31:  LBL[100:MEASURE] ;
  32:  UTOOL_NUM=1 ;
  33:  IF (DI[1:TABLE AT A POSITION]=ON) THEN ;
  34:  UFRAME_NUM=1 ;
  35:  ELSE ;
  36:  UFRAME_NUM=2 ;
  37:  ENDIF ;
  38:  PR[34,3:APP Z TOOL OFF]=100    ;
  39:L PR[R[150]] 2000mm/sec CNT10 Offset,PR[38:TABLE PCK OFFSET] Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  40:  R[46:COLLISION SENSE]=150    ;
  41:  R[40:RECOVER START PR]=R[150:TABLE PICK PR]    ;
  42:  PR[34,3:APP Z TOOL OFF]=(-10)    ;
  43:  SKIP CONDITION RI[7:SPRING COMPRESSED]=ON    ;
  44:L PR[R[150]] R[88:NOM SLOW mm/sec]mm/sec FINE Offset,PR[38:TABLE PCK OFFSET] Tool_Offset,PR[34:APP Z TOOL OFF] Skip,LBL[180]    ;
  45:  JMP LBL[9983] ;
  46:   ;
  47:   ;
  48:  LBL[180:PART NOT FOUND] ;
  49:  R[2:CUR ROW PICK (X)]=1    ;
  50:  R[3:CUR COL PICK (Y)]=1    ;
  51:  PR[34,3:APP Z TOOL OFF]=R[67:MEAS PART LENGTH]+100    ;
  52:L PR[R[150]] 2000mm/sec CNT20 Offset,PR[38:TABLE PCK OFFSET] Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  53:  R[40:RECOVER START PR]=10    ;
  54:J PR[10:TABLE PK POUNCE] 100% CNT30    ;
  55:  JMP LBL[9999] ;
  56:   ;
  57:  LBL[9980:FAULT] ;
  58:  CALL TABLE_MSG(2) ;
  59:  UALM[11] ;
  60:  JMP LBL[1] ;
  61:   ;
  62:  LBL[9981:FAULT] ;
  63:  CALL TABLE_MSG(5) ;
  64:  UALM[11] ;
  65:  JMP LBL[1] ;
  66:   ;
  67:  LBL[9982:FAULT] ;
  68:  CALL TABLE_MSG(4) ;
  69:  UALM[11] ;
  70:  JMP LBL[1] ;
  71:   ;
  72:  LBL[9983:PART FOUND] ;
  73:  PR[34,3:APP Z TOOL OFF]=R[67:MEAS PART LENGTH]+100    ;
  74:L PR[R[150]] 2000mm/sec CNT20 Offset,PR[38:TABLE PCK OFFSET] Tool_Offset,PR[34:APP Z TOOL OFF]    ;
  75:  R[40:RECOVER START PR]=10    ;
  76:J PR[10:TABLE PK POUNCE] 100% CNT30    ;
  77:  R[40:RECOVER START PR]=1    ;
  78:J PR[1:HOME POSITION] 100% CNT5    ;
  79:  CALL TABLE_MSG(27) ;
  80:  UALM[11] ;
  81:  JMP LBL[1] ;
  82:   ;
  83:   ;
  84:  LBL[9999:END] ;
  85:  CALL MESSAGE19    ;
  86:  R[40:RECOVER START PR]=1    ;
  87:J PR[1:HOME POSITION] 100% CNT5    ;
/POS
/END
