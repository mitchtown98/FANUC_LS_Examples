/PROG  _VISIONTASK
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 3843;
CREATE		= DATE 21-04-07  TIME 13:52:26;
MODIFIED	= DATE 21-04-07  TIME 13:52:26;
FILE_NAME	= ;
VERSION		= 0;
LINE_COUNT	= 159;
MEMORY_SIZE	= 4263;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= *,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;

  AUTOZONE_INTERFERENCE_AVOID;
    AUTOZONE_ENABLED         : FALSE;
    AUTOZONE_SCHEDULE_NUMBER : 1;
/MN
   1:  !Init ;
   2:  R[180:Pall Loc Retry]=0    ;
   3:  R[170:killVision]=0    ;
   4:  GO[7:3DV Status]=1 ;
   5:  !clear parts list ;
   6:  CALL BINPICK_CLEAR("Parts List ID"=1) ;
   7:   ;
   8:  !--------------------- ;
   9:  LBL[1] ;
  10:  !--------------------- ;
  11:  !Search for Pallet Location ;
  12:  R[169:tubFound]=0    ;
  13:  --eg:Wait for PalletInPosition Sensor or skip if running in T1 for now ;
  14:  IF R[170:killVision]=1,JMP LBL[9999] ;
  15:  IF DI[147:PalletInPosn]=OFF OR DO[200:Pallet Zone]=OFF,JMP LBL[1] ;
  16:  GO[7:3DV Status]=10 ;
  17:  VISION RUN_FIND 'PALLET_LOCATOR'    ;
  18:  VISION GET_OFFSET 'PALLET_LOCATOR' VR[1] JMP LBL[991] ;
  19:  JMP LBL[2] ;
  20:   ;
  21:  !--------------------- ;
  22:  LBL[2] ;
  23:  !--------------------- ;
  24:  !Search for Highest Point in Pall ;
  25:  GO[7:3DV Status]=20 ;
  26:  VISION RUN_FIND 'PEAK_SEARCH'    ;
  27:  VISION GET_OFFSET 'PEAK_SEARCH' VR[2] JMP LBL[992] ;
  28:   ;
  29:  !Start Parts List Manager Search ;
  30:  GO[7:3DV Status]=30 ;
  31:  CALL BINPICK_SEARCH("Parts List ID"=1,"SEARCH VP ID"=1,"Status R"=165) ;
  32:  IF R[165:searchStatus]<>0,JMP LBL[993] ;
  33:  JMP LBL[3] ;
  34:   ;
  35:  !--------------------- ;
  36:  LBL[3] ;
  37:  !--------------------- ;
  38:  !POP ;
  39:  GO[7:3DV Status]=40 ;
  40:  CALL BINPICK_POP("Parts List ID"=1,"Status R"=166,"Model ID R"=167) ;
  41:  IF R[166:popStatus]<>0,JMP LBL[994] ;
  42:   ;
  43:  !GETPICKPOS ;
  44:  GO[7:3DV Status]=50 ;
  45:  CALL BINPICK_GETPICKPOS("Parts List ID"=1,"PICK Pos ID"=1,"Status R"=168,"PICK Pos PR"=20,"IA Offset PR"=21,"APP Pos PR"=22) ;
  46:  IF R[168:getPickPosStatus]=0,JMP LBL[4] ;
  47:   ;
  48:  JMP LBL[995] ;
  49:   ;
  50:  !--------------------- ;
  51:  !Valid Pick Position ;
  52:  LBL[4] ;
  53:  GO[7:3DV Status]=60 ;
  54:  !--------------------- ;
  55:  !getPeakZ ;
  56:  R[160]=VR[2].MES[1] ;
  57:  !getTubZ ;
  58:  R[161:foundTubZ]=(PR[20,3:Pick Position]) ;
  59:  //R[161]=VR[3].MES[5] ;
  60:   ;
  61:  !Compare Peak Z to Pick Z Value ;
  62:  R[162:zDifference]=R[160:peakZ]-R[161:foundTubZ]    ;
  63:  IF (R[162:zDifference]>75) THEN ;
  64:  !Delta too Great ;
  65:  R[169:tubFound]=(-1)    ;
  66:  WAIT   1.00(sec) ;
  67:  JMP LBL[996] ;
  68:  ELSE ;
  69:  !Similar Layer/Z Value ;
  70:  LBL[667] ;
  71:  WAIT    .20(sec) ;
  72:  R[169:tubFound]=1    ;
  73:  WAIT R[169:tubFound]=1 TIMEOUT,LBL[667] ;
  74:  ENDIF ;
  75:  GO[7:3DV Status]=62 ;
  76:   ;
  77:  !Wait to run again or kill Vision ;
  78:  WAIT R[169:tubFound]=0 OR R[170:killVision]=1    ;
  79:  IF R[170:killVision]<>1,JMP LBL[1] ;
  80:  JMP LBL[9999] ;
  81:   ;
  82:  !Alarms Section ;
  83:  !--------------------- ;
  84:  LBL[991] ;
  85:  !--------------------- ;
  86:  !Pallet Search Failed ;
  87:   ;
  88:  --eg:Current Retry Method: Search 3 Times ;
  89:  R[180:Pall Loc Retry]=R[180:Pall Loc Retry]+1    ;
  90:  GO[7:3DV Status]=11 ;
  91:  WAIT   2.00(sec) ;
  92:  !Check if Retry is within 3 times ;
  93:  IF (R[180:Pall Loc Retry]<3),JMP LBL[1] ;
  94:  --eg:Retry number exceeded, Set Alarm Status and Abort Vision Task ;
  95:  GO[7:3DV Status]=12 ;
  96:  !Wait for Vision Alarm Ack or Kil ;
  97:  WAIT DI[140:Vision Reset/Ack]=ON OR R[170:killVision]=1    ;
  98:  IF R[170:killVision]=1,JMP LBL[9999] ;
  99:  JMP LBL[1] ;
 100:   ;
 101:  !--------------------- ;
 102:  LBL[992] ;
 103:  !--------------------- ;
 104:  --eg:Peak search failed to find a target. Pallet Empty ;
 105:  GO[7:3DV Status]=21 ;
 106:  !Wait for PalletInPosition to go  ;
 107:  WAIT DI[147:PalletInPosn]=OFF OR R[170:killVision]=1    ;
 108:  !KillVision Check ;
 109:  IF R[170:killVision]=1,JMP LBL[9999] ;
 110:  !Wait for PalletInPosition to go  ;
 111:  WAIT DI[147:PalletInPosn]=ON OR R[170:killVision]=1    ;
 112:  !KillVision Check ;
 113:  IF R[170:killVision]=1,JMP LBL[9999] ;
 114:  JMP LBL[1] ;
 115:   ;
 116:  !--------------------- ;
 117:  LBL[993] ;
 118:  !--------------------- ;
 119:  !Tub Find Failed ;
 120:  !No Valid Tubs Found in BinSearch ;
 121:  GO[7:3DV Status]=31 ;
 122:  !Wait for Vision Alarm Ack or Kil ;
 123:  WAIT DI[140:Vision Reset/Ack]=ON OR R[170:killVision]=1    ;
 124:  IF R[170:killVision]=1,JMP LBL[9999] ;
 125:  JMP LBL[1] ;
 126:   ;
 127:  !--------------------- ;
 128:  LBL[994] ;
 129:  !--------------------- ;
 130:  !POP Status Invalid ;
 131:  GO[7:3DV Status]=41 ;
 132:  !Wait for Vision Alarm Ack or Kil ;
 133:  WAIT DI[140:Vision Reset/Ack]=ON OR R[170:killVision]=1    ;
 134:  IF R[170:killVision]=1,JMP LBL[9999] ;
 135:  JMP LBL[1] ;
 136:  JMP LBL[1] ;
 137:   ;
 138:  !--------------------- ;
 139:  LBL[995] ;
 140:  !--------------------- ;
 141:  !GetPickPos Data Invalid ;
 142:  GO[7:3DV Status]=51 ;
 143:  CALL BINPICK_SETSTAT("Parts List ID"=1,"Part Status"=22) ;
 144:  JMP LBL[3] ;
 145:   ;
 146:  !--------------------- ;
 147:  LBL[996] ;
 148:  !--------------------- ;
 149:  --eg:Peak and GetPickPos Delta Too Large ;
 150:  GO[7:3DV Status]=61 ;
 151:  !Wait for Vision Alarm Ack or Kil ;
 152:  WAIT DI[140:Vision Reset/Ack]=ON OR R[170:killVision]=1    ;
 153:  IF R[170:killVision]=1,JMP LBL[9999] ;
 154:  JMP LBL[1] ;
 155:   ;
 156:  LBL[9999] ;
 157:  !Goodbye ;
 158:  GO[7:3DV Status]=99 ;
 159:  END ;
/POS
/END
