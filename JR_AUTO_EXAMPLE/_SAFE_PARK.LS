/PROG  _SAFE_PARK
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 3329;
CREATE		= DATE 21-04-07  TIME 13:37:56;
MODIFIED	= DATE 21-04-07  TIME 19:36:48;
FILE_NAME	= _MAIN;
VERSION		= 0;
LINE_COUNT	= 147;
MEMORY_SIZE	= 3789;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  !************************* ;
   2:  !262650 - SafePark ;
   3:  !JR Automation ;
   4:  !Created: Feb 2021 ;
   5:  ! ;
   6:  !************************* ;
   7:  !--Set Collision Guard ;
   8:  COL DETECT ON ;
   9:  $HSCDMNGRP[1].$AUTO_RESET=0 ;
  10:  COL GUARD ADJUST 150 ;
  11:  UTOOL_NUM=2 ;
  12:  UFRAME_NUM=0 ;
  13:  R[100:FinalDestination]=0    ;
  14:   ;
  15:  PR[9:Homing PR]=LPOS    ;
  16:  !************************* ;
  17:  !If at Reference Position ;
  18:  !Our work here is done ;
  19:  IF DO[69:ValidPerch]=ON,JMP LBL[9999] ;
  20:   ;
  21:  !Check which zone the Robot is at ;
  22:   ;
  23:  !Lid Conveyor Zone Check ;
  24:  --eg:Check if Robot is at Lid Conveyor Zone ;
  25:  IF DO[202:Lid Conveyor Zone]=OFF,JMP LBL[1000] ;
  26:   ;
  27:  !Reject/ReEntry Zone Check ;
  28:  --eg:Check if Robot is at Reject/ReEntry Zone ;
  29:  IF DO[203:Reject Zone]=OFF,JMP LBL[2000] ;
  30:   ;
  31:  !Scan Zone Check ;
  32:  --eg:Check if Robot is at Scan Zone ;
  33:  IF DO[204:Scan Zone]=OFF,JMP LBL[3000] ;
  34:   ;
  35:  !DeLidding Zone Check ;
  36:  --eg:Check if Robot is at Delidding Zone ;
  37:  IF DO[201:DeLidding Zone]=OFF,JMP LBL[4000] ;
  38:   ;
  39:  !Pallet Zone Check ;
  40:  --eg:Check if Robot is at Pallet Zone ;
  41:  IF DO[200:Pallet Zone]=OFF,JMP LBL[5000] ;
  42:   ;
  43:  !Out of Bounds ;
  44:  JMP LBL[9000] ;
  45:   ;
  46:  !-------------------------------- ;
  47:  LBL[1000] ;
  48:  --eg:Recover from Lid Conveyor Zone ;
  49:  UTOOL_NUM=2 ;
  50:  UFRAME_NUM=0 ;
  51:  PR[9:Homing PR]=LPOS    ;
  52:  !Set/Calc Homing PR ;
  53:  IF (PR[9,3:Homing PR]<100) THEN ;
  54:  PR[9,3:Homing PR]=100    ;
  55:L PR[9:Homing PR] 500mm/sec FINE    ;
  56:  ENDIF ;
  57:   ;
  58:  !Joint to Lid Conveyor Perch ;
  59:J PR[52:LidConvPerch] 12% FINE    ;
  60:  JMP LBL[9999] ;
  61:  !-------------------------------- ;
  62:   ;
  63:  !-------------------------------- ;
  64:  LBL[2000] ;
  65:  --eg:Recover from Reject/Reentry Zone ;
  66:  UTOOL_NUM=2 ;
  67:  UFRAME_NUM=0 ;
  68:  PR[9:Homing PR]=LPOS    ;
  69:  !Set/Calc Homing PR ;
  70:  IF (PR[9,3:Homing PR]<(-100)) THEN ;
  71:  PR[9,3:Homing PR]=(-100)    ;
  72:L PR[9:Homing PR] 500mm/sec FINE    ;
  73:  ENDIF ;
  74:   ;
  75:  !Joint to Reject Conveyor Perch ;
  76:J PR[53:RejConvPerch] 12% FINE    ;
  77:  JMP LBL[9999] ;
  78:  !-------------------------------- ;
  79:   ;
  80:  !-------------------------------- ;
  81:  LBL[3000] ;
  82:  --eg:Recover from Scan Zone ;
  83:  UTOOL_NUM=2 ;
  84:  UFRAME_NUM=0 ;
  85:  PR[9:Homing PR]=LPOS    ;
  86:  !Set/Calc Homing PR ;
  87:  IF (PR[9,3:Homing PR]<(-100)) THEN ;
  88:  PR[9,3:Homing PR]=(-100)    ;
  89:L PR[9:Homing PR] 500mm/sec FINE    ;
  90:  ENDIF ;
  91:   ;
  92:  !Joint to Scan Perch ;
  93:J PR[50:ScanPerch] 12% FINE    ;
  94:  JMP LBL[9999] ;
  95:  !-------------------------------- ;
  96:   ;
  97:  !-------------------------------- ;
  98:  LBL[4000] ;
  99:  --eg:Recover from DeLidding Zone ;
 100:  UTOOL_NUM=2 ;
 101:  UFRAME_NUM=0 ;
 102:  PR[9:Homing PR]=LPOS    ;
 103:  !Set/Calc Homing PR ;
 104:  IF (PR[9,3:Homing PR]<(-100)) THEN ;
 105:  PR[9,3:Homing PR]=(-100)    ;
 106:L PR[9:Homing PR] 500mm/sec FINE    ;
 107:  ENDIF ;
 108:   ;
 109:  !Joint to DeLidding Conveyor Perc ;
 110:J PR[51:DeLiddingPerch] 12% FINE    ;
 111:  JMP LBL[9999] ;
 112:  !-------------------------------- ;
 113:   ;
 114:  !-------------------------------- ;
 115:  LBL[5000] ;
 116:  --eg:Recover from Pallet Zone ;
 117:  UTOOL_NUM=2 ;
 118:  UFRAME_NUM=0 ;
 119:  PR[9:Homing PR]=LPOS    ;
 120:  !Set/Calc Homing PR ;
 121:  IF (PR[9,3:Homing PR]<250) THEN ;
 122:  PR[9,3:Homing PR]=250    ;
 123:L PR[9:Homing PR] 500mm/sec FINE    ;
 124:  ENDIF ;
 125:   ;
 126:  !Move to center ;
 127:  UFRAME_NUM=2 ;
 128:  UTOOL_NUM=2 ;
 129:J PR[62:DePal Cntr Appr] 37% CNT75    ;
 130:   ;
 131:  !Joint to Pallet Perch ;
 132:J PR[49:PalletPerch] 12% FINE    ;
 133:  JMP LBL[9999] ;
 134:  !-------------------------------- ;
 135:   ;
 136:   ;
 137:  LBL[9000:ALARM] ;
 138:  !Generate Warning or Fault ;
 139:  COL GUARD ADJUST 100 ;
 140:  GO[6:Faulted State]=R[1:State] ;
 141:  GO[2:Fault Number]=R[99:Fault Code] ;
 142:  PAUSE ;
 143:  ABORT ;
 144:  LBL[9999:END] ;
 145:  R[1:State]=0    ;
 146:  COL GUARD ADJUST 100 ;
 147:  IF (DI[169:iVacGripVacOK]=OFF),DO[165:oVacOn]=(OFF) ;
/POS
/END
