/PROG  _SP_PU_CORE
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 3197;
CREATE		= DATE 22-12-26  TIME 18:16:20;
MODIFIED	= DATE 23-01-17  TIME 20:01:26;
FILE_NAME	= _SP_DEP_;
VERSION		= 0;
LINE_COUNT	= 138;
MEMORY_SIZE	= 3701;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : FALSE;

  AUTOZONE_INTERFERENCE_AVOID;
    AUTOZONE_ENABLED         : FALSE;
    AUTOZONE_SCHEDULE_NUMBER : 1;
/MN
   1:  !This program expects to be ;
   2:  !started from the Home position ;
   3:  !with an empty EOAT. ;
   4:  !It will finish at the PBS outer ;
   5:  !point with a core and coil held. ;
   6:   ;
   7:  UFRAME_NUM=0 ;
   8:  UTOOL_NUM=1 ;
   9:  OVERRIDE=10% ;
  10:   ;
  11:  IF DI[3:Run P/U Core]<>ON,JMP LBL[1] ;
  12:   ;
  13:  DO[24:Main Prgm Active]=OFF ;
  14:  DO[7:P/U Core Active]=ON ;
  15:   ;
  16:  !Start Monitor for Max Z Travel ;
  17:  MONITOR _SP_CD_PU_CORE_LIMIT ;
  18:   ;
  19:  !Initial offset checks ;
  20:  IF GI[1:CL C3 Z Offset mm]>1150,JMP LBL[1] ;
  21:  IF R[11:Vision Offset X]<879 OR R[11:Vision Offset X]>1079,JMP LBL[1] ;
  22:  IF R[12:Vision Offset Y]<(-2152) OR R[12:Vision Offset Y]>(-1952),JMP LBL[1] ;
  23:  IF R[13:Vision Offset R]<(-180) OR R[13:Vision Offset R]>180,JMP LBL[1] ;
  24:   ;
  25:  !Set Output States ;
  26:  RO[1:Engage Core Gripper]=OFF ;
  27:  RO[2:Release Core Gripper]=ON ;
  28:  RO[3:Squeeze Segments]=OFF ;
  29:  RO[4:Relax Segments]=ON ;
  30:  RO[5:Release Ball Locks]=OFF ;
  31:  RO[6:Release Finger]=OFF ;
  32:   ;
  33:  !PR[90]= Base of theoretical ;
  34:  !lowest level, centered, ;
  35:  !zero-rotation core on pallet. ;
  36:  PR[90:TEMP]=PR[35:Vision Master Pt]    ;
  37:   ;
  38:  !PR[91]= Base of theoretical ;
  39:  !centered, zero-rotation core ;
  40:  !at actual stack height. ;
  41:  PR[91:TEMP]=PR[90:TEMP]    ;
  42:  PR[91,3:TEMP]=PR[91,3:TEMP]+GI[1:CL C3 Z Offset mm]+R[20:Z Ofst @ 0 Cores]    ;
  43:  //PR[91,1:TEMP]=(PR[91,1:TEMP]-(.00548*GI[1:CL C3 Z Offset mm])) ;
  44:  //PR[91,2:TEMP]=(PR[91,2:TEMP]-(.00919*GI[1:CL C3 Z Offset mm])) ;
  45:  PR[91,1:TEMP]=PR[91,1:TEMP]+10    ;
  46:  PR[91,2:TEMP]=PR[91,2:TEMP]+10    ;
  47:   ;
  48:  !PR[92]= Approach/Departure Point ;
  49:  PR[92:TEMP]=PR[91:TEMP]    ;
  50:  PR[92,3:TEMP]=PR[92,3:TEMP]+50    ;
  51:   ;
  52:  !PR[93]= P/U X,Y @ Home Z ;
  53:  PR[93:TEMP]=PR[92:TEMP]    ;
  54:  PR[93,3:TEMP]=PR[10,3:Home]    ;
  55:   ;
  56:  !PR[94]= P/U X,Y @ PBS Z ;
  57:  PR[94:TEMP]=PR[92:TEMP]    ;
  58:  PR[94,3:TEMP]=PR[30,3:PBS Outer Point]    ;
  59:   ;
  60:  !PR[95]= PBS X,Y @ App./Dep. Z ;
  61:  PR[95:TEMP]=PR[30:PBS Outer Point]    ;
  62:  PR[95,3:TEMP]=PR[92,3:TEMP]    ;
  63:   ;
  64:  !Move Above Stack ;
  65:L PR[93:TEMP] max_speed CNT100 VOFFSET,VR[1]    ;
  66:   ;
  67:  !Move to Approach Point ;
  68:L PR[92:TEMP] max_speed CNT5 VOFFSET,VR[1]    ;
  69:   ;
  70:  !Wait on EOAT feedback ;
  71:  $WAITTMOUT=300 ;
  72:  WAIT RI[3:Core Gripper Engaged]=OFF AND RI[4:Core Gripper Released]=ON AND RI[5:Segments Squeezed Inward]=OFF AND RI[6:Ball Locks Released]=OFF AND RI[7:Finger Released]=OFF TIMEOUT,LBL[1] ;
  73:   ;
  74:  !Move to Pickup Location ;
  75:L PR[91:TEMP] 250mm/sec FINE AP_LD40 VOFFSET,VR[1]    ;
  76:   ;
  77:  !Engage Core Grippers ;
  78:  RO[1:Engage Core Gripper]=ON ;
  79:  RO[2:Release Core Gripper]=OFF ;
  80:   ;
  81:  !Wait on Grippers to Engage ;
  82:  $WAITTMOUT=300 ;
  83:  WAIT RI[3:Core Gripper Engaged]=ON AND RI[4:Core Gripper Released]=OFF TIMEOUT,LBL[1] ;
  84:   ;
  85:  !Release Ball Locks ;
  86:  RO[5:Release Ball Locks]=ON ;
  87:   ;
  88:  !Wait on Ball Locks to Release ;
  89:  $WAITTMOUT=300 ;
  90:  WAIT RI[6:Ball Locks Released]=ON TIMEOUT,LBL[1] ;
  91:   ;
  92:  !Update Payload Schedule ;
  93:  PAYLOAD[3:EOAT + Core + Col] ;
  94:   ;
  95:  !Move to Departure Point ;
  96:L PR[92:TEMP] 500mm/sec CNT40 RT_LD40 VOFFSET,VR[1]    ;
  97:   ;
  98:  !Evaluate Departure Z vs. PBS Pt. ;
  99:  IF (PR[92,3:TEMP]<PR[30,3:PBS Outer Point]) THEN ;
 100:  JMP LBL[2] ;
 101:  ELSE ;
 102:  JMP LBL[3] ;
 103:  ENDIF ;
 104:   ;
 105:  !Move from Dep. if Below PBS Z ;
 106:  LBL[2:Move Up to PBS] ;
 107:   ;
 108:  !Move to PBS Point Z ;
 109:L PR[94:TEMP] max_speed CNT100 VOFFSET,VR[1]    ;
 110:   ;
 111:  !Move to PBS Point ;
 112:J PR[30:PBS Outer Point] 100% CNT0    ;
 113:   ;
 114:  !Complete Program ;
 115:  JMP LBL[4] ;
 116:   ;
 117:  !Move from Dep. if Above PBS Z ;
 118:  LBL[3:Move Over to PBS] ;
 119:   ;
 120:  !Move to PBS Point X,Y ;
 121:L PR[95:TEMP] max_speed CNT100    ;
 122:   ;
 123:  !Move to PBS Point ;
 124:J PR[30:PBS Outer Point] 100% CNT0    ;
 125:   ;
 126:  LBL[4:Complete] ;
 127:   ;
 128:  !Stop Monitor for Max Z Travel ;
 129:  MONITOR END _SP_CD_PU_CORE_LIMIT ;
 130:   ;
 131:  DO[8:P/U Core Complete]=PULSE,0.5sec ;
 132:   ;
 133:  END ;
 134:   ;
 135:  LBL[1:FAILURE] ;
 136:  DO[9:P/U Core Failure]=PULSE,0.5sec ;
 137:  MONITOR END _SP_CD_PU_CORE_LIMIT ;
 138:  ABORT ;
/POS
/END
